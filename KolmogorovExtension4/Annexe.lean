/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import KolmogorovExtension4.DependsOn
import Mathlib.Data.Finset.Basic
import Mathlib.MeasureTheory.Function.ConditionalExpectation.Basic
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.MeasurableSpace.PreorderRestrict
import Mathlib.Probability.Kernel.Composition.MeasureCompProd
import Mathlib.Probability.Kernel.Integral
import Mathlib.Probability.Process.Filtration

/-!
New lemmas for mathlib
-/

open MeasureTheory ProbabilityTheory MeasurableSpace ENNReal Finset Function Preorder



section Lemmas

@[measurability, fun_prop]
lemma measurable_cast {X Y : Type u} [mX : MeasurableSpace X] [mY : MeasurableSpace Y] (h : X = Y)
    (hm : HEq mX mY) : Measurable (cast h) := by
  subst h
  subst hm
  exact measurable_id

theorem update_updateFinset_eq {X : â„• â†’ Type*} (x z : Î  n, X n) {m : â„•} :
    update (updateFinset x (Iic m) (frestrictLe m z)) (m + 1) (z (m + 1)) =
    updateFinset x (Iic (m + 1)) (frestrictLe (m + 1) z) := by
  ext i
  simp only [update, updateFinset, mem_Iic, dite_eq_ite]
  split_ifs with h <;> try omega
  cases h
  all_goals rfl

instance subsingleton_subtype {Î± : Type*} (a : Î±) : Subsingleton ({a} : Finset Î±) where
  allEq x y := by
    rw [â† Subtype.coe_inj, eq_of_mem_singleton x.2, eq_of_mem_singleton y.2]

lemma updateFinset_updateFinset_subset {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*}
    {s t : Finset Î¹} (hst : s âŠ† t) (x : (i : Î¹) â†’ Î± i) (y : (i : s) â†’ Î± i) (z : (i : t) â†’ Î± i) :
    updateFinset (updateFinset x s y) t z = updateFinset x t z := by
  ext i
  simp only [updateFinset]
  split_ifs with h1 h2 <;> try rfl
  exact (h1 (hst h2)).elim

end Lemmas

section indicator

lemma indicator_one_mul_const {Î± M : Type*} [MonoidWithZero M] (s : Set Î±) (c : M) (a : Î±) :
    (s.indicator (1 : Î± â†’ M) a) * c = s.indicator (fun _ â†¦ c) a := by
  simp [Set.indicator]

lemma indicator_one_mul_const' {Î± M : Type*} [MonoidWithZero M] (s : Set Î±) (c : M) (a : Î±) :
    (s.indicator (fun _ â†¦ 1 : Î± â†’ M) a) * c = s.indicator (fun _ â†¦ c) a := by
  simp [Set.indicator]

theorem preimage_indicator {Î± Î² M : Type*} [Zero M] (f : Î± â†’ Î²) (s : Set Î²) (a : Î±) (c : M) :
    (f â»Â¹' s).indicator (fun _ â†¦ c) a = s.indicator (fun _ â†¦ c) (f a) := by
  by_cases h : f a âˆˆ s <;> simp [h]

theorem indicator_const_eq {Î± Î² M : Type*} [Zero M] {s : Set Î±} {t : Set Î²} {a : Î±} {b : Î²}
    (c : M) (h : a âˆˆ s â†” b âˆˆ t) :
    s.indicator (fun _ â†¦ c) a = t.indicator (fun _ â†¦ c) b := by
  by_cases h' : a âˆˆ s
  Â· simp [h', h.1 h']
  Â· simp [h', h.not.1 h']

theorem indicator_const_eq_iff {Î± Î² M : Type*} [Zero M] {s : Set Î±} {t : Set Î²} {a : Î±} {b : Î²}
    (c : M) [hc : NeZero c] :
    s.indicator (fun _ â†¦ c) a = t.indicator (fun _ â†¦ c) b â†” (a âˆˆ s â†” b âˆˆ t) := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· by_contra! h'
    rcases h' with (âŸ¨h1, h2âŸ© | âŸ¨h1, h2âŸ©)
    Â· rw [Set.indicator_of_mem h1, Set.indicator_of_not_mem h2] at h
      exact hc.out h
    Â· rw [Set.indicator_of_not_mem h1, Set.indicator_of_mem h2] at h
      exact hc.out h.symm
  Â· by_cases h' : a âˆˆ s
    Â· simp [h', h.1 h']
    Â· simp [h', h.not.1 h']

theorem Set.indicator_const_smul_apply' {Î± R M : Type*} [Zero R] [Zero M] [SMulWithZero R M]
    (s : Set Î±) (r : R) (f : Î± â†’ M) (a : Î±) :
    s.indicator (r â€¢ f) a = (s.indicator (fun _ â†¦ r : Î± â†’ R) a) â€¢ (f a) := by
  by_cases h : a âˆˆ s <;> simp [h]

theorem Set.indicator_one_smul_apply {Î± M Î² : Type*} [Zero Î²] [MonoidWithZero M]
    [MulActionWithZero M Î²] (f : Î± â†’ Î²) (s : Set Î±) (a : Î±) :
    s.indicator f a = (s.indicator (fun _ â†¦ 1 : Î± â†’ M) a) â€¢ (f a) := by
  by_cases h : a âˆˆ s <;> simp [h]

end indicator

section Measure

variable {X Y Z T U : Type*}
variable [MeasurableSpace Z] [MeasurableSpace T] [MeasurableSpace U]

lemma lintegral_le_mul_ae [MeasurableSpace X] {Î¼ : Measure X} {f : X â†’ â„â‰¥0âˆ}
    {b : â„â‰¥0âˆ} (hf : âˆ€áµ x âˆ‚Î¼, f x â‰¤ b) : âˆ«â» x, f x âˆ‚Î¼ â‰¤ b * (Î¼ Set.univ) := by
  rw [â† lintegral_const]
  exact lintegral_mono_ae hf

lemma lintegral_le_mul [MeasurableSpace X] (Î¼ : Measure X) {f : X â†’ â„â‰¥0âˆ}
    {b : â„â‰¥0âˆ} (hf : âˆ€ x, f x â‰¤ b) : âˆ«â» x, f x âˆ‚Î¼ â‰¤ b * (Î¼ Set.univ) :=
  lintegral_le_mul_ae (Filter.Eventually.of_forall hf)

lemma lintegral_le_ae [MeasurableSpace X] {Î¼ : Measure X} [IsProbabilityMeasure Î¼]
    {f : X â†’ â„â‰¥0âˆ} {b : â„â‰¥0âˆ} (hf : âˆ€áµ x âˆ‚Î¼, f x â‰¤ b) : âˆ«â» x, f x âˆ‚Î¼ â‰¤ b := by
  simpa using lintegral_le_mul_ae hf

lemma lintegral_le [MeasurableSpace X] (Î¼ : Measure X) [IsProbabilityMeasure Î¼] {f : X â†’ â„â‰¥0âˆ}
    {b : â„â‰¥0âˆ} (hf : âˆ€ x, f x â‰¤ b) : âˆ«â» x, f x âˆ‚Î¼ â‰¤ b := by
  simpa using lintegral_le_mul Î¼ hf

lemma lintegral_eq_const [MeasurableSpace X] (Î¼ : Measure X) [IsProbabilityMeasure Î¼] {f : X â†’ â„â‰¥0âˆ}
    {c : â„â‰¥0âˆ} (hf : âˆ€ x, f x = c) : âˆ«â» x, f x âˆ‚Î¼ = c := by simp [hf]

section Filtration

/-- If a function `g` is measurable with respect to the pullback along some function `f`, then
to prove `g x = g y` it is enough to prove `f x = f y`. -/
theorem eq_of_measurable_comap [m : MeasurableSpace Y] [MeasurableSingletonClass Z]
    (f : X â†’ Y) {g : X â†’ Z} (hg : Measurable[m.comap f] g)
    {xâ‚ xâ‚‚ : X} (h : f xâ‚ = f xâ‚‚) : g xâ‚ = g xâ‚‚ := by
  obtain âŸ¨s, -, hsâŸ© := hg (measurableSet_singleton (g xâ‚))
  have : xâ‚ âˆˆ f â»Â¹' s := by simp [hs]
  have : xâ‚‚ âˆˆ f â»Â¹' s := by rwa [Set.mem_preimage, â† h]
  rw [hs] at this
  exact (by simpa using this : g xâ‚‚ = g xâ‚).symm

/-- If a function `g` is strongly measurable with respect to the pullback along some function `f`,
then to prove `g x = g y` it is enough to prove `f x = f y`. -/
theorem eq_of_stronglyMeasurable_comap {Z : Type*} [m : MeasurableSpace Y]
    [TopologicalSpace Z] [TopologicalSpace.PseudoMetrizableSpace Z] [T1Space Z]
    (f : X â†’ Y) {g : X â†’ Z} (hg : StronglyMeasurable[m.comap f] g)
    {xâ‚ xâ‚‚ : X} (h : f xâ‚ = f xâ‚‚) : g xâ‚ = g xâ‚‚ := by
  let _ : MeasurableSpace Z := borel Z
  have : BorelSpace Z := BorelSpace.mk rfl
  exact eq_of_measurable_comap f hg.measurable h

variable {Î¹ : Type*} [Preorder Î¹] [LocallyFiniteOrderBot Î¹]
  {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]

/-- The canonical filtration on dependent functions indexed by `â„•`, where `ğ“• n` consists of
measurable sets depending only on coordinates `â‰¤ n`. -/
def Filtration.pi_preorder : @Filtration ((i : Î¹) â†’ X i) Î¹ _ inferInstance where
  seq n := (inferInstance : MeasurableSpace (Î  i : Iic n, X i)).comap (frestrictLe n)
  mono' i j hij := by
    simp only
    rw [â† frestrictLeâ‚‚_comp_frestrictLe hij, â† comap_comp]
    exact MeasurableSpace.comap_mono (measurable_frestrictLeâ‚‚ _).comap_le
  le' n := (measurable_frestrictLe n).comap_le

variable {E : Type*} [NormedAddCommGroup E]

/-- If a function is strongly measurable with respect to the Ïƒ-algebra generated by the
first coordinates, then it only depends on those first coordinates. -/
theorem dependsOn_of_stronglyMeasurable {i : Î¹} {f : ((i : Î¹) â†’ X i) â†’ E}
    (mf : StronglyMeasurable[Filtration.pi_preorder i] f) : DependsOn f (Set.Iic i) :=
  fun _ _ h â†¦ eq_of_stronglyMeasurable_comap _ mf (dependsOn_frestrictLe i h)

end Filtration

variable [MeasurableSpace X] [MeasurableSpace Y]
  {Îº : Kernel X Y} {Î· : Kernel Y Z} {x : X} {s : Set Z}

theorem comp_null (x : X) (hs : MeasurableSet s) :
    (Î· âˆ˜â‚– Îº) x s = 0 â†” (fun y â†¦ Î· y s) =áµ[Îº x] 0 := by
  rw [Kernel.comp_apply' _ _ _ hs, lintegral_eq_zero_iff]
  Â· exact Kernel.measurable_coe _ hs

theorem ae_null_of_comp_null (h : (Î· âˆ˜â‚– Îº) x s = 0) :
    (fun y => Î· y s) =áµ[Îº x] 0 := by
  obtain âŸ¨t, hst, mt, htâŸ© := exists_measurable_superset_of_null h
  simp_rw [comp_null x mt] at ht
  rw [Filter.eventuallyLE_antisymm_iff]
  exact
    âŸ¨Filter.EventuallyLE.trans_eq
        (Filter.Eventually.of_forall fun x => measure_mono hst) ht,
      Filter.Eventually.of_forall fun x => zero_le _âŸ©

theorem ae_ae_of_ae_comp {p : Z â†’ Prop} (h : âˆ€áµ z âˆ‚(Î· âˆ˜â‚– Îº) x, p z) :
    âˆ€áµ y âˆ‚Îº x, âˆ€áµ z âˆ‚Î· y, p z :=
  ae_null_of_comp_null h

lemma ae_comp_of_ae_ae {Îº : Kernel X Y} {Î· : Kernel Y Z}
    {p : Z â†’ Prop} (hp : MeasurableSet {z | p z})
    (h : âˆ€áµ y âˆ‚Îº x, âˆ€áµ z âˆ‚Î· y, p z) :
    âˆ€áµ z âˆ‚(Î· âˆ˜â‚– Îº) x, p z := by
  simp_rw [ae_iff] at h âŠ¢
  rw [comp_null]
  Â· exact h
  Â· exact hp.compl

lemma ae_comp_iff {p : Z â†’ Prop} (hp : MeasurableSet {z | p z}) :
    (âˆ€áµ z âˆ‚(Î· âˆ˜â‚– Îº) x, p z) â†” âˆ€áµ y âˆ‚Îº x, âˆ€áµ z âˆ‚Î· y, p z :=
  âŸ¨fun h â†¦ ae_ae_of_ae_comp h, fun h â†¦ ae_comp_of_ae_ae hp hâŸ©

theorem ProbabilityTheory.Kernel.compProd_apply_prod (Îº : Kernel X Y) [IsSFiniteKernel Îº]
    (Î· : Kernel (X Ã— Y) Z) [IsSFiniteKernel Î·]
    {s : Set Y} (hs : MeasurableSet s) {t : Set Z} (ht : MeasurableSet t) (x : X) :
    (Îº âŠ—â‚– Î·) x (s Ã—Ë¢ t) = âˆ«â» y in s, Î· (x, y) t âˆ‚Îº x := by
  rw [Kernel.compProd_apply (hs.prod ht), â† lintegral_indicator hs]
  congr with y
  by_cases hy : y âˆˆ s <;> simp [Set.indicator, hy]

theorem ProbabilityTheory.Kernel.map_map (Îº : Kernel X Y) {f : Y â†’ Z} (hf : Measurable f)
    {g : Z â†’ T} (hg : Measurable g) :
    (Îº.map f).map g = Îº.map (g âˆ˜ f) := by
  ext1 x
  rw [Kernel.map_apply _ hg, Kernel.map_apply _ hf, Measure.map_map hg hf,
    â† Kernel.map_apply _ (hg.comp hf)]

theorem ProbabilityTheory.Kernel.id_map {f : X â†’ Y} (hf : Measurable f) :
    Kernel.id.map f = Kernel.deterministic f hf := by
  ext1 x
  rw [map_apply _ hf, id_apply, Measure.map_dirac hf, deterministic_apply]

theorem ProbabilityTheory.Kernel.lintegral_id {f : X â†’ â„â‰¥0âˆ} (x : X) (hf : Measurable f) :
    âˆ«â» y, f y âˆ‚(@Kernel.id X inferInstance x) = f x := by
  rw [id_apply, lintegral_dirac' _ hf]

theorem ProbabilityTheory.Kernel.lintegral_id_prod (Îº : Kernel X Y) [IsSFiniteKernel Îº]
    {f : X Ã— Y â†’ â„â‰¥0âˆ} (x : X) (hf : Measurable f) :
    âˆ«â» z, f z âˆ‚((Kernel.id Ã—â‚– Îº) x) = âˆ«â» y, f (x, y) âˆ‚Îº x := by
  rw [lintegral_prod _ _ _ hf, lintegral_id]
  exact hf.lintegral_prod_right'

theorem Measure.map_prod (Î¼ : Measure X) [IsFiniteMeasure Î¼]
    (Î½ : Measure Y) [IsFiniteMeasure Î½] {f : X â†’ Z} (hf : Measurable f)
    {g : Y â†’ T} (hg : Measurable g) :
    (Î¼.prod Î½).map (Prod.map f g) = (Î¼.map f).prod (Î½.map g) := by
  refine (Measure.prod_eq fun s t ms mt â†¦ ?_).symm
  rw [Measure.map_apply (hf.prod_map hg) (ms.prod mt)]
  Â· have : Prod.map f g â»Â¹' s Ã—Ë¢ t = (f â»Â¹' s) Ã—Ë¢ (g â»Â¹' t) := Set.prod_preimage_eq.symm
    rw [this, Measure.prod_prod, Measure.map_apply hf ms, Measure.map_apply hg mt]

theorem Kernel.map_prod (Îº : Kernel X Y) [IsFiniteKernel Îº] (Î· : Kernel X T) [IsFiniteKernel Î·]
    {f : Y â†’ Z} (hf : Measurable f) {g : T â†’ U} (hg : Measurable g) :
    (Îº Ã—â‚– Î·).map (Prod.map f g) = (Îº.map f) Ã—â‚– (Î·.map g) := by
  ext1 x
  rw [Kernel.map_apply _ (hf.prod_map hg), Kernel.prod_apply, Measure.map_prod _ _ hf hg,
    Kernel.prod_apply, Kernel.map_apply _ hf, Kernel.map_apply _ hg]

theorem Kernel.map_prod_fst (Îº : Kernel X Y) [IsSFiniteKernel Îº]
    (Î· : Kernel X Z) [IsMarkovKernel Î·] :
    (Îº Ã—â‚– Î·).map Prod.fst = Îº := by
  rw [â† Kernel.fst_eq, Kernel.fst_prod Îº Î·]

theorem Kernel.map_prod_snd (Îº : Kernel X Y) [IsMarkovKernel Îº]
    (Î· : Kernel X Z) [IsSFiniteKernel Î·] :
    (Îº Ã—â‚– Î·).map Prod.snd = Î· := by
  rw [â† Kernel.snd_eq, Kernel.snd_prod Îº Î·]

theorem ProbabilityTheory.Kernel.map_deterministic {f : X â†’ Y} (hf : Measurable f)
    {g : Y â†’ Z} (hg : Measurable g) :
    (Kernel.deterministic f hf).map g = Kernel.deterministic (g âˆ˜ f) (hg.comp hf) := by
  ext x s ms
  rw [Kernel.map_apply' _ hg _ ms, Kernel.deterministic_apply' _ _ (hg ms),
    Kernel.deterministic_apply' _ _ ms, preimage_indicator]
  rfl

lemma ProbabilityTheory.Kernel.eq_zero_of_isEmpty [IsEmpty Y] (Îº : Kernel X Y) :
    Îº = 0 := by
  ext1 x
  rw [Measure.eq_zero_of_isEmpty (Îº x), zero_apply]

theorem ProbabilityTheory.Kernel.deterministic_prod_apply' {f : X â†’ Y} (mf : Measurable f)
    (Î· : Kernel X Z) [IsSFiniteKernel Î·] (x : X)
    {s : Set (Y Ã— Z)} (ms : MeasurableSet s) :
    ((Kernel.deterministic f mf) Ã—â‚– Î·) x s = Î· x {z | (f x, z) âˆˆ s} := by
  rw [Kernel.prod_apply' _ _ _ ms, Kernel.lintegral_deterministic']
  exact measurable_measure_prod_mk_left ms

theorem ProbabilityTheory.Kernel.id_prod_apply' (Î· : Kernel X Y) [IsSFiniteKernel Î·] (x : X)
    {s : Set (X Ã— Y)} (ms : MeasurableSet s) :
    (Kernel.id Ã—â‚– Î·) x s = Î· x (Prod.mk x â»Â¹' s) := by
  rw [Kernel.id, Kernel.deterministic_prod_apply']
  rfl
  exact ms

theorem ProbabilityTheory.Kernel.prod_apply_symm' (Îº : Kernel X Y) [IsSFiniteKernel Îº]
    (Î· : Kernel X Z) [IsSFiniteKernel Î·]
    (x : X) {s : Set (Y Ã— Z)} (hs : MeasurableSet s) :
    (Îº Ã—â‚– Î·) x s = âˆ«â» z, Îº x ((fun y â†¦ (y, z)) â»Â¹' s) âˆ‚Î· x := by
  rw [Kernel.prod_apply, Measure.prod_apply_symm hs]

theorem ProbabilityTheory.Kernel.prod_deterministic_apply' {f : X â†’ Z} (mf : Measurable f)
    (Î· : Kernel X Y) [IsSFiniteKernel Î·] (x : X)
    {s : Set (Y Ã— Z)} (ms : MeasurableSet s) :
    (Î· Ã—â‚– (Kernel.deterministic f mf)) x s = Î· x {y | (y, f x) âˆˆ s} := by
  rw [Kernel.prod_apply_symm' _ _ _ ms, Kernel.lintegral_deterministic']
  Â· rfl
  Â· exact measurable_measure_prod_mk_right ms

theorem ProbabilityTheory.Kernel.comp_apply'' (Îº : Kernel X Y) (Î· : Kernel Y Z) (x : X) :
    (Î· âˆ˜â‚– Îº) x = (Îº x).bind Î· := by
  ext s hs
  rw [Kernel.comp_apply' _ _ _ hs, Measure.bind_apply hs Î·.measurable]

theorem Measure.map_snd_compProd (Î¼ : Measure X) [IsProbabilityMeasure Î¼] (Îº : Kernel X Y)
    [IsSFiniteKernel Îº] {f : Y â†’ Z} (hf : Measurable f) :
    (Î¼ âŠ—â‚˜ Îº).snd.map f = (Î¼ âŠ—â‚˜ (Îº.map f)).snd := by
  ext s ms
  rw [Measure.map_apply hf ms, Measure.snd_apply (hf ms), â† Set.univ_prod,
    Measure.compProd_apply_prod MeasurableSet.univ (hf ms), Measure.snd_apply ms, â† Set.univ_prod,
    Measure.compProd_apply_prod MeasurableSet.univ ms]
  simp_rw [Kernel.map_apply' _ hf _ ms]

theorem Measure.fst_compProd (Î¼ : Measure X) [SFinite Î¼]
    (Îº : Kernel X Y) [IsMarkovKernel Îº] :
    (Î¼ âŠ—â‚˜ Îº).fst = Î¼ := by
  ext s ms
  rw [Measure.fst_apply ms, â† Set.prod_univ, Measure.compProd_apply_prod ms MeasurableSet.univ]
  simp

/-- from #19639 -/
@[simp]
lemma snd_compProd (Î¼ : Measure X) [SFinite Î¼] (Îº : Kernel X Y) [IsSFiniteKernel Îº] :
    (Î¼ âŠ—â‚˜ Îº).snd = Measure.bind Î¼ Îº := by
  ext s hs
  rw [Measure.bind_apply hs Îº.measurable, Measure.snd_apply hs, Measure.compProd_apply]
  Â· rfl
  Â· exact measurable_snd hs

lemma snd_compProd_kernel (Îº : Kernel X Y) [IsSFiniteKernel Îº] (Î· : Kernel Y Z)
    [IsSFiniteKernel Î·] (x : X) :
    ((Îº x) âŠ—â‚˜ Î·).snd = (Î· âˆ˜â‚– Îº) x := by
  rw [snd_compProd, Kernel.comp_apply'']

theorem ProbabilityTheory.Kernel.comap_const (Î¼ : Measure Z) {f : X â†’ Y} (hf : Measurable f) :
    Kernel.comap (Kernel.const Y Î¼) f hf = Kernel.const X Î¼ := by
  ext1 x
  rw [Kernel.const_apply, Kernel.comap_apply, Kernel.const_apply]

lemma ProbabilityTheory.Kernel.comp_map (Îº : Kernel X Y) (Î· : Kernel Z T) {f : Y â†’ Z}
    (hf : Measurable f) :
    Î· âˆ˜â‚– (Îº.map f) = (Î·.comap f hf) âˆ˜â‚– Îº := by
  ext x s ms
  rw [comp_apply' _ _ _ ms, lintegral_map _ hf _ (Î·.measurable_coe ms), comp_apply' _ _ _ ms]
  simp_rw [comap_apply']

lemma ProbabilityTheory.Kernel.prod_comap (Îº : Kernel Y Z) [IsSFiniteKernel Îº]
    (Î· : Kernel Y T) [IsSFiniteKernel Î·] {f : X â†’ Y} (hf : Measurable f) :
    (Îº Ã—â‚– Î·).comap f hf = (Îº.comap f hf) Ã—â‚– (Î·.comap f hf) := by
  ext1 x
  rw [comap_apply, prod_apply, prod_apply, comap_apply, comap_apply]

lemma ProbabilityTheory.Kernel.const_compProd_const (Î¼ : Measure Y) [SFinite Î¼]
    (Î½ : Measure Z) [SFinite Î½] :
    (const X Î¼) âŠ—â‚– (const (X Ã— Y) Î½) = const X (Î¼.prod Î½) := by
  ext x s ms
  simp_rw [compProd_apply ms, const_apply, Measure.prod_apply ms]
  rfl

lemma ProbabilityTheory.Kernel.prod_const_comp (Îº : Kernel X Y) [IsSFiniteKernel Îº]
    (Î· : Kernel Y Z) [IsSFiniteKernel Î·] (Î¼ : Measure T) [SFinite Î¼] :
    (Î· Ã—â‚– (const Y Î¼)) âˆ˜â‚– Îº = (Î· âˆ˜â‚– Îº) Ã—â‚– (const X Î¼) := by
  ext x s ms
  simp_rw [comp_apply' _ _ _ ms, prod_apply' _ _ _ ms, const_apply]
  rw [lintegral_comp]
  exact measurable_measure_prod_mk_left ms

instance IsZeroOrMarkovKernel.compProd (Îº : Kernel X Y) [IsZeroOrMarkovKernel Îº]
    (Î· : Kernel (X Ã— Y) Z) [IsZeroOrMarkovKernel Î·] : IsZeroOrMarkovKernel (Îº âŠ—â‚– Î·) := by
  obtain rfl | _ := eq_zero_or_isMarkovKernel Îº <;> obtain rfl | _ := eq_zero_or_isMarkovKernel Î·
  all_goals simpa using by infer_instance

instance IsZeroOrMarkovKernel.comp (Îº : Kernel X Y) [IsZeroOrMarkovKernel Îº]
    (Î· : Kernel Y Z) [IsZeroOrMarkovKernel Î·] : IsZeroOrMarkovKernel (Î· âˆ˜â‚– Îº) := by
  obtain rfl | _ := eq_zero_or_isMarkovKernel Îº <;> obtain rfl | _ := eq_zero_or_isMarkovKernel Î·
  all_goals simpa using by infer_instance

variable {E : Type*} [NormedAddCommGroup E]

theorem MeasureTheory.AEStronglyMeasurable.compProd {Î¼ : Measure X} [SFinite Î¼]
    {Îº : Kernel X Y} [IsMarkovKernel Îº] {f : X â†’ Y â†’ E}
    (hf : AEStronglyMeasurable f.uncurry (Î¼ âŠ—â‚˜ Îº)) :
    âˆ€áµ x âˆ‚Î¼, AEStronglyMeasurable (f x) (Îº x) := by
  rw [Measure.compProd] at hf
  apply compProd_mk_left at hf
  simpa using hf

lemma MeasureTheory.AEStronglyMeasurable.comp {f : Z â†’ E}
    (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) x)) :
    âˆ€áµ y âˆ‚Îº x, AEStronglyMeasurable f (Î· y) := by
  filter_upwards [ae_ae_of_ae_comp hf.ae_eq_mk] with x' hx'
  exact hf.stronglyMeasurable_mk.aestronglyMeasurable.congr (ae_eq_symm hx')

theorem MeasureTheory.AEStronglyMeasurable.comp_mk_left [NormedSpace â„ E]
    [IsSFiniteKernel Î·] [IsSFiniteKernel Îº] {f : Z â†’ E} {x : X}
    (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) x)) :
    AEStronglyMeasurable (fun y â†¦ âˆ« z, f z âˆ‚Î· y) (Îº x) := by
  rw [â† Kernel.snd_compProd_prodMkLeft, Kernel.snd_apply] at hf
  replace hf := hf.comp_measurable measurable_snd
  exact hf.integral_kernel_compProd

theorem Kernel.integrable_prod_iff (Îº : Kernel X Y) [IsFiniteKernel Îº]
    (Î· : Kernel X Z) [IsFiniteKernel Î·] (x : X) {f : (Y Ã— Z) â†’ E}
    (hf : AEStronglyMeasurable f ((Îº Ã—â‚– Î·) x)) : Integrable f ((Îº Ã—â‚– Î·) x) â†”
      (âˆ€áµ y âˆ‚Îº x, Integrable (fun z â†¦ f (y, z)) (Î· x)) âˆ§
      Integrable (fun y â†¦ âˆ« z, â€–f (y, z)â€– âˆ‚Î· x) (Îº x) := by
  rwa [Kernel.prod_apply, MeasureTheory.integrable_prod_iff] at *

theorem Kernel.integrable_prod_iff' (Îº : Kernel X Y) [IsFiniteKernel Îº]
    (Î· : Kernel X Z) [IsFiniteKernel Î·] (x : X) {f : (Y Ã— Z) â†’ E}
    (hf : AEStronglyMeasurable f ((Îº Ã—â‚– Î·) x)) : Integrable f ((Îº Ã—â‚– Î·) x) â†”
      (âˆ€áµ z âˆ‚Î· x, Integrable (fun y â†¦ f (y, z)) (Îº x)) âˆ§
      Integrable (fun z â†¦ âˆ« y, â€–f (y, z)â€– âˆ‚Îº x) (Î· x) := by
  rwa [Kernel.prod_apply, MeasureTheory.integrable_prod_iff'] at *

theorem integrable_dirac {f : X â†’ E} (mf : StronglyMeasurable f) {x : X} :
    Integrable f (Measure.dirac x) := by
    let _ : MeasurableSpace E := borel E
    have _ : BorelSpace E := BorelSpace.mk rfl
    have : f =áµ[Measure.dirac x] (fun _ â†¦ f x) := ae_eq_dirac' mf.measurable
    rw [integrable_congr this]
    exact integrable_const _

theorem Kernel.integrable_deterministic_prod {f : X â†’ Y} (mf : Measurable f)
    (Îº : Kernel X Z) [IsFiniteKernel Îº] (x : X)
    {g : (Y Ã— Z) â†’ E} (mg : StronglyMeasurable g) :
    Integrable g (((Kernel.deterministic f mf) Ã—â‚– Îº) x) â†”
      Integrable (fun z â†¦ g (f x, z)) (Îº x) := by
  rw [Kernel.integrable_prod_iff]
  Â· constructor
    Â· rintro âŸ¨h, -âŸ©
      rwa [Kernel.deterministic_apply, ae_dirac_iff] at h
      exact measurableSet_integrable mg
    Â· intro h
      constructor
      Â· rwa [Kernel.deterministic_apply, ae_dirac_iff]
        exact measurableSet_integrable mg
      Â· rw [Kernel.deterministic_apply]
        apply integrable_dirac
        exact mg.norm.integral_prod_right'
  Â· exact mg.aestronglyMeasurable

theorem integrable_comp_iff {Î· : Kernel Y Z} [IsSFiniteKernel Î·]
    {Îº : Kernel X Y} [IsSFiniteKernel Îº] {x : X}
    {f : Z â†’ E} (hf : AEStronglyMeasurable f ((Î· âˆ˜â‚– Îº) x)) :
    Integrable f ((Î· âˆ˜â‚– Îº) x) â†”
    (âˆ€áµ y âˆ‚Îº x, Integrable f (Î· y)) âˆ§ (Integrable (fun y â†¦ âˆ« z, â€–f zâ€– âˆ‚Î· y) (Îº x)) := by
  rw [Kernel.comp_eq_snd_compProd, Kernel.snd_eq, Kernel.map_apply _ measurable_snd] at *
  rw [integrable_map_measure, ProbabilityTheory.integrable_compProd_iff]
  Â· rfl
  Â· exact hf.comp_measurable measurable_snd
  Â· exact hf
  Â· exact measurable_snd.aemeasurable

variable [NormedSpace â„ E]

theorem MeasureTheory.Integrable.integral_comp {Î· : Kernel Y Z} [IsSFiniteKernel Î·] {Îº : Kernel X Y}
    [IsSFiniteKernel Îº] {x : X} {f : Z â†’ E} (hf : Integrable f ((Î· âˆ˜â‚– Îº) x)) :
    Integrable (fun y â†¦ âˆ« z, f z âˆ‚Î· y) (Îº x) :=
  ((integrable_comp_iff hf.1).1 hf).2.mono' hf.1.comp_mk_left <|
    Filter.Eventually.of_forall fun _ â†¦ norm_integral_le_integral_norm _

theorem Kernel.integral_prod (Îº : Kernel X Y) [IsFiniteKernel Îº]
    (Î· : Kernel X Z) [IsFiniteKernel Î·] (x : X)
    {f : (Y Ã— Z) â†’ E} (hf : Integrable f ((Îº Ã—â‚– Î·) x)) :
    âˆ« p, f p âˆ‚(Îº Ã—â‚– Î·) x = âˆ« y, âˆ« z, f (y, z) âˆ‚Î· x âˆ‚Îº x := by
  rw [Kernel.prod_apply, MeasureTheory.integral_prod]
  rwa [â† Kernel.prod_apply]

theorem Kernel.integral_comp (Î· : Kernel Y Z) [IsFiniteKernel Î·]
    (Îº : Kernel X Y) [IsFiniteKernel Îº]
    (x : X) {g : Z â†’ E} (hg : Integrable g ((Î· âˆ˜â‚– Îº) x)) :
    âˆ« z, g z âˆ‚(Î· âˆ˜â‚– Îº) x = âˆ« y, âˆ« z, g z âˆ‚Î· y âˆ‚Îº x := by
  rw [Kernel.comp_eq_snd_compProd, Kernel.snd_apply, integral_map,
    ProbabilityTheory.integral_compProd]
  Â· simp_rw [Kernel.prodMkLeft_apply Î·]
  Â· apply Integrable.comp_measurable
    Â· convert hg
      rw [Kernel.comp_eq_snd_compProd, Kernel.snd_apply]
    Â· exact measurable_snd
  Â· exact measurable_snd.aemeasurable
  Â· convert hg.aestronglyMeasurable
    rw [Kernel.comp_eq_snd_compProd, Kernel.snd_apply]

theorem setIntegral_eq {Î¼ : Measure X} (f : X â†’ E) {s : Set X} (hs : MeasurableSet s) :
    âˆ« x in s, f x âˆ‚Î¼ = âˆ« x, (s.indicator (fun _ â†¦ (1 : â„)) x) â€¢ (f x) âˆ‚Î¼ := by
  simp_rw [â† Set.indicator_one_smul_apply]
  rw [integral_indicator hs]

variable [CompleteSpace E]

theorem Kernel.integral_deterministic_prod {f : X â†’ Y} (mf : Measurable f)
    (Îº : Kernel X Z) [IsFiniteKernel Îº] (x : X)
    {g : (Y Ã— Z) â†’ E} (mg : StronglyMeasurable g)
    (i_g : Integrable (fun z â†¦ g (f x, z)) (Îº x)) :
    âˆ« p, g p âˆ‚((Kernel.deterministic f mf) Ã—â‚– Îº) x = âˆ« z, g (f x, z) âˆ‚Îº x := by
  rw [Kernel.integral_prod, Kernel.integral_deterministic']
  Â· exact mg.integral_prod_right'
  Â· rwa [Kernel.integrable_deterministic_prod _ _ _ mg]

theorem Kernel.lintegral_deterministic_prod {f : X â†’ Y} (mf : Measurable f)
    (Îº : Kernel X Z) [IsFiniteKernel Îº] (x : X)
    {g : (Y Ã— Z) â†’ â„â‰¥0âˆ} (mg : Measurable g) :
    âˆ«â» p, g p âˆ‚((Kernel.deterministic f mf) Ã—â‚– Îº) x = âˆ«â» z, g (f x, z) âˆ‚Îº x := by
  rw [Kernel.lintegral_prod _ _ _ mg, Kernel.lintegral_deterministic']
  exact mg.lintegral_prod_right'

theorem MeasureTheory.Filtration.condExp_condExp {Î¹ : Type*} [Preorder Î¹]
    (f : X â†’ E) {Î¼ : Measure X} (â„± : @Filtration X Î¹ _ inferInstance)
    {i j : Î¹} (hij : i â‰¤ j) [SigmaFinite (Î¼.trim (â„±.le j))] :
    Î¼[Î¼[f|â„± j]|â„± i] =áµ[Î¼] Î¼[f|â„± i] := condExp_condExp_of_le (â„±.mono hij) (â„±.le j)

end Measure

section Finset

lemma mem_Ioc_succ {n i : â„•} : i âˆˆ Ioc n (n + 1) â†” i = n + 1 := by
  rw [mem_Ioc]
  omega

lemma mem_Ioc_succ' {n : â„•} (i : Ioc n (n + 1)) : i = âŸ¨n + 1, mem_Ioc_succ.2 rflâŸ© := by
  simp [â† mem_Ioc_succ.1 i.2]

theorem updateFinset_self {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*} (x : (i : Î¹) â†’ Î± i)
    {s : Finset Î¹} (y : (i : s) â†’ Î± i) : (fun i : s â†¦ updateFinset x s y i) = y := by
  ext i
  simp [updateFinset, i.2]

lemma Finset.sub_Iic (I : Finset â„•) : I âŠ† (Iic (I.sup id)) :=
  fun _ hi â†¦ mem_Iic.2 <| le_sup (f := id) hi

theorem Set.Iic_diff_Ioc_same {Î± : Type*} [LinearOrder Î±]
    {a b : Î±} (hab : a â‰¤ b) : (Set.Iic b) \ (Set.Ioc a b) = Set.Iic a := by
  ext x
  simp only [mem_diff, mem_Iic, mem_Ioc, not_and, not_le]
  refine âŸ¨fun âŸ¨h1, h2âŸ© â†¦ ?_, fun h â†¦ âŸ¨h.trans hab, fun h' â†¦ (not_le.2 h' h).elimâŸ©âŸ©
  Â· by_contra h3
    exact (not_le.2 (h2 (not_le.1 h3))) h1

theorem Finset.Iic_sdiff_Ioc_same {Î± : Type*} [LinearOrder Î±] [OrderBot Î±] [LocallyFiniteOrder Î±]
    {a b : Î±} (hab : a â‰¤ b) : (Iic b) \ (Ioc a b) = Iic a := by
  rw [â† coe_inj, coe_sdiff, coe_Iic, coe_Ioc, coe_Iic, Set.Iic_diff_Ioc_same hab]

theorem Finset.right_mem_Iic {Î± : Type*} [Preorder Î±] [LocallyFiniteOrderBot Î±] (a : Î±) :
    a âˆˆ Iic a := mem_Iic.2 <| le_rfl

theorem Finset.Iic_union_Ioc_eq_Iic {Î± : Type*} [LinearOrder Î±] [LocallyFiniteOrder Î±] [OrderBot Î±]
    {a b : Î±} (h : a â‰¤ b) : Iic a âˆª Ioc a b = Iic b := by
  rw [â† coe_inj, coe_union, coe_Iic, coe_Iic, coe_Ioc, Set.Iic_union_Ioc_eq_Iic h]

theorem Finset.disjoint_Iic_Ioc {Î± : Type*} [Preorder Î±] [LocallyFiniteOrder Î±] [OrderBot Î±]
    {a b c : Î±} (h : a â‰¤ b) : Disjoint (Iic a) (Ioc b c) :=
  disjoint_left.2 fun _ hax hbcx â†¦ (mem_Iic.1 hax).not_lt <| lt_of_le_of_lt h (mem_Ioc.1 hbcx).1

theorem restrict_updateFinset' {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*} {s t : Finset Î¹}
    (hst : s âŠ† t) (x : Î  i, Î± i) (y : Î  i : t, Î± i) :
    s.restrict (updateFinset x t y) = restrictâ‚‚ hst y := by
  ext i
  simp only [restrict, updateFinset, restrictâ‚‚]
  split_ifs with hi
  Â· rfl
  Â· exact (hi (hst i.2)).elim

theorem restrict_updateFinset {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*} {s : Finset Î¹}
    (x : Î  i, Î± i) (y : Î  i : s, Î± i) :
    s.restrict (updateFinset x s y) = y := by
  rw [restrict_updateFinset' subset_rfl]
  rfl

@[simp]
theorem updateFinset_restrict {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*} {s : Finset Î¹}
    (x : Î  i, Î± i) : updateFinset x s (s.restrict x) = x := by
  ext i
  simp [updateFinset]

open Preorder

theorem frestrictLe_updateFinset' {Î¹ : Type*} [DecidableEq Î¹] [Preorder Î¹] [LocallyFiniteOrderBot Î¹]
    {Î± : Î¹ â†’ Type*} {i j : Î¹} (hij : i â‰¤ j) (x : Î  k, Î± k) (y : Î  k : Iic j, Î± k) :
    frestrictLe i (updateFinset x _ y) = frestrictLeâ‚‚ hij y :=
  restrict_updateFinset' (Iic_subset_Iic.2 hij) ..

theorem frestrictLe_updateFinset {Î¹ : Type*} [DecidableEq Î¹] [Preorder Î¹] [LocallyFiniteOrderBot Î¹]
    {Î± : Î¹ â†’ Type*} {i : Î¹} (x : Î  j, Î± j) (y : Î  j : Iic i, Î± j) :
    frestrictLe i (updateFinset x _ y) = y :=
  restrict_updateFinset ..

@[simp]
theorem updateFinset_frestrictLe {Î¹ : Type*} [DecidableEq Î¹] [Preorder Î¹] [LocallyFiniteOrderBot Î¹]
    {Î± : Î¹ â†’ Type*} {i : Î¹} (x : Î  i, Î± i) : updateFinset x _ (frestrictLe i x) = x := by
  simp [frestrictLe]

end Finset

section Product

theorem Finset.prod_congr' {Î± M : Type*} [CommMonoid M] {s t : Finset Î±} (hst : s = t)
    (f : t â†’ M) : âˆ i : s, f âŸ¨i.1, hst â–¸ i.2âŸ© = âˆ i : t, f i := by cases hst; rfl

theorem Finset.prod_union' {Î± M : Type*} [DecidableEq Î±] [CommMonoid M] {s t : Finset Î±}
    (hst : Disjoint s t) (f : â†‘(s âˆª t) â†’ M) :
    (âˆ i : s, f âŸ¨i.1, subset_union_left i.2âŸ©) * âˆ i : t, f âŸ¨i.1, subset_union_right i.2âŸ© =
    âˆ i : â†‘(s âˆª t), f i := by
  let g : Î± â†’ M := fun a â†¦ if ha : a âˆˆ s âˆª t then f âŸ¨a, haâŸ© else 1
  have h1 : âˆ i : s, f âŸ¨i.1, subset_union_left i.2âŸ© = âˆ i : s, g i := by simp [g]
  have h2 : âˆ i : t, f âŸ¨i.1, subset_union_right i.2âŸ© = âˆ i : t, g i := by simp [g]
  have h3 : âˆ i : â†‘(s âˆª t), f i = âˆ i : â†‘(s âˆª t), g i := by simp [g, -mem_union]
  rw [h1, h2, h3, prod_coe_sort, prod_coe_sort, prod_coe_sort, â† disjUnion_eq_union _ _ hst,
    prod_disjUnion hst]

theorem prod_Ioc {M : Type*} [CommMonoid M] {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c)
    (f : (Ioc a c) â†’ M) :
    (âˆ i : Ioc a b, f (Set.inclusion (Ioc_subset_Ioc_right hbc) i)) *
      (âˆ i : Ioc b c, f (Set.inclusion (Ioc_subset_Ioc_left hab) i)) = (âˆ i : Ioc a c, f i) := by
  have : Ioc a b âˆª Ioc b c = Ioc a c := by simp [hab, hbc]
  rw [â† prod_congr' this, â† prod_union']
  rw [â† disjoint_coe, coe_Ioc, coe_Ioc, Set.Ioc_disjoint_Ioc, min_eq_left hbc, max_eq_right hab]

theorem prod_Iic {M : Type*} [CommMonoid M] {a b : â„•} (hab : a â‰¤ b)
    (f : (Iic b) â†’ M) :
    (âˆ i : Iic a, f (Set.inclusion (Iic_subset_Iic.2 hab) i)) *
      (âˆ i : Ioc a b, f (Set.inclusion Ioc_subset_Iic_self i)) = (âˆ i, f i) := by
  have : Iic a âˆª Ioc a b = Iic b := by
    rw [â† Finset.coe_inj, coe_union, coe_Iic, coe_Ioc, coe_Iic]
    simp [hab]
  rw [â† prod_congr' this, â† prod_union']
  rw [â† disjoint_coe, coe_Iic, coe_Ioc]
  exact Set.Iic_disjoint_Ioc le_rfl

end Product
