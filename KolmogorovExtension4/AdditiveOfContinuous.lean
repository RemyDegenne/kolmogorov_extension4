/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Peter Pfaffelhuber
-/
import KolmogorovExtension4.Content

open Filter

open scoped ENNReal Topology

namespace MeasureTheory

variable {Î± : Type*} {C : Set (Set Î±)}

/-- In a ring of sets, continuity of an additive content at `âˆ…` implies Ïƒ-additivity.
This is not true in general in semirings, or without the hypothesis that `m` is finite. See the
examples 7 and 8 in Halmos' book Measure Theory (1974), page 40. -/
theorem addContent_iUnion_eq_sum_of_tendsto_zero (hC : IsSetRing C) (m : AddContent C)
    (hm_ne_top : âˆ€ s âˆˆ C, m s â‰  âˆž)
    (hm_tendsto : âˆ€ â¦ƒs : â„• â†’ Set Î±â¦„ (_ : âˆ€ n, s n âˆˆ C),
      Antitone s â†’ (â‹‚ n, s n) = âˆ… â†’ Tendsto (fun n â†¦ m (s n)) atTop (ð“ 0))
    â¦ƒf : â„• â†’ Set Î±â¦„ (hf : âˆ€ i, f i âˆˆ C) (hUf : (â‹ƒ i, f i) âˆˆ C)
    (h_disj : Pairwise (Function.onFun Disjoint f)) :
    m (â‹ƒ i, f i) = âˆ‘' i, m (f i) := by
  -- We use the continuity of `m` at `âˆ…` on the sequence `n â†¦ (â‹ƒ i, f i) \ (set.accumulate f n)`
  let s : â„• â†’ Set Î± := fun n â†¦ (â‹ƒ i, f i) \ Set.Accumulate f n
  have hCs n : s n âˆˆ C := hC.diff_mem hUf (hC.accumulate_mem hf n)
  have h_tendsto : Tendsto (fun n â†¦ m (s n)) atTop (ð“ 0) := by
    refine hm_tendsto hCs ?_ ?_
    Â· intro i j hij x hxj
      rw [Set.mem_diff] at hxj âŠ¢
      exact âŸ¨hxj.1, fun hxi â†¦ hxj.2 (Set.monotone_accumulate hij hxi)âŸ©
    Â· simp_rw [s, Set.diff_eq]
      rw [Set.iInter_inter_distrib, Set.iInter_const, â† Set.compl_iUnion, Set.iUnion_accumulate]
      exact Set.inter_compl_self _
  have hmsn n : m (s n) = m (â‹ƒ i, f i) - âˆ‘ i in Finset.range (n + 1), m (f i) := by
    rw [addContent_diff_of_ne_top m hC hm_ne_top hUf (hC.accumulate_mem hf n)
      (Set.accumulate_subset_iUnion _), addContent_accumulate m hC h_disj hf n]
  simp_rw [hmsn] at h_tendsto
  refine tendsto_nhds_unique ?_ (ENNReal.tendsto_nat_tsum fun i â†¦ m (f i))
  refine (Filter.tendsto_add_atTop_iff_nat 1).mp ?_
  rwa [ENNReal.tendsto_atTop_zero_const_sub_iff _ _ (hm_ne_top _ hUf) (fun n â†¦ ?_)] at h_tendsto
  rw [â† addContent_accumulate m hC h_disj hf]
  exact addContent_mono hC.isSetSemiring (hC.accumulate_mem hf n) hUf
    (Set.accumulate_subset_iUnion _)

/-- A function which is additive on disjoint elements in a ring of sets `C` defines an
additive content on `C`. -/
def IsSetRing.addContent_of_union (m : Set Î± â†’ â„â‰¥0âˆž) (hC : IsSetRing C) (m_empty : m âˆ… = 0)
    (m_add : âˆ€ {s t : Set Î±} (_hs : s âˆˆ C) (_ht : t âˆˆ C), Disjoint s t â†’ m (s âˆª t) = m s + m t) :
    AddContent C where
  toFun := m
  empty' := m_empty
  sUnion' I h_ss h_dis h_mem := by
    classical
    induction I using Finset.induction with
    | empty => simp only [Finset.coe_empty, Set.sUnion_empty, Finset.sum_empty, m_empty]
    | @insert s I hsI h =>
      rw [Finset.coe_insert] at *
      rw [Set.insert_subset_iff] at h_ss
      rw [Set.pairwiseDisjoint_insert_of_not_mem] at h_dis
      swap; Â· exact hsI
      have h_sUnion_mem : â‹ƒâ‚€ â†‘I âˆˆ C := by
        rw [Set.sUnion_eq_biUnion]
        apply hC.biUnion_mem
        intro n hn
        exact h_ss.2 hn
      rw [Set.sUnion_insert, m_add h_ss.1 h_sUnion_mem (Set.disjoint_sUnion_right.mpr h_dis.2),
        Finset.sum_insert hsI, h h_ss.2 h_dis.1]
      rwa [Set.sUnion_insert] at h_mem

end MeasureTheory
