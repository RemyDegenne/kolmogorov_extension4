/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Peter Pfaffelhuber
-/
import KolmogorovExtension4.Content

open Filter

open scoped ENNReal Topology

namespace MeasureTheory

variable {Î± : Type*} {C : Set (Set Î±)}

/-- In a ring of sets, continuity of an additive content at `âˆ…` implies Ïƒ-additivity.
This is not true in general in semirings, or without the hypothesis that `m` is finite. See the
examples 7 and 8 in Halmos' book Measure Theory (1974), page 40. -/
theorem sigma_additive_addContent_of_tendsto_zero (hC : IsSetRing C) (m : AddContent C)
    (hm_ne_top : âˆ€ s âˆˆ C, m s â‰  âˆ)
    (hm_tendsto : âˆ€ â¦ƒs : â„• â†’ Set Î±â¦„ (_ : âˆ€ n, s n âˆˆ C),
      Antitone s â†’ (â‹‚ n, s n) = âˆ… â†’ Tendsto (fun n â†¦ m (s n)) atTop (ğ“ 0))
    â¦ƒf : â„• â†’ Set Î±â¦„ (hf : âˆ€ i, f i âˆˆ C) (hUf : (â‹ƒ i, f i) âˆˆ C) (h_disj : Pairwise (Disjoint on f)) :
    m (â‹ƒ i, f i) = âˆ‘' i, m (f i) := by
  -- We use the continuity of `m` at `âˆ…` on the sequence `n â†¦ (â‹ƒ i, f i) \ (set.accumulate f n)`
  let s : â„• â†’ Set Î± := fun n â†¦ (â‹ƒ i, f i) \ Set.Accumulate f n
  have hCs n : s n âˆˆ C := hC.diff_mem hUf (hC.accumulate_mem hf n)
  have h_tendsto : Tendsto (fun n â†¦ m (s n)) atTop (ğ“ 0) := by
    refine hm_tendsto hCs ?_ ?_
    Â· intro i j hij x hxj
      rw [Set.mem_diff] at hxj âŠ¢
      exact âŸ¨hxj.1, fun hxi â†¦ hxj.2 (Set.monotone_accumulate hij hxi)âŸ©
    Â· simp_rw [s, Set.diff_eq]
      rw [Set.iInter_inter_distrib, Set.iInter_const, â† Set.compl_iUnion, Set.iUnion_accumulate]
      exact Set.inter_compl_self _
  have hmsn n : m (s n) = m (â‹ƒ i, f i) - âˆ‘ i in Finset.range (n + 1), m (f i) := by
    rw [addContent_diff_of_ne_top m hC hm_ne_top hUf (hC.accumulate_mem hf n)
      (Set.accumulate_subset_iUnion _), addContent_accumulate m hC h_disj hf n]
  simp_rw [hmsn] at h_tendsto
  have h_tendsto' :
      Tendsto (fun n â†¦ âˆ‘ i in Finset.range n, m (f i)) atTop (ğ“ (m (â‹ƒ i, f i))) := by
    refine (Filter.tendsto_add_atTop_iff_nat 1).mp ?_
    rwa [ENNReal.tendsto_atTop_zero_const_sub_iff _ _ (hm_ne_top _ hUf)] at h_tendsto
    intro n
    rw [â† addContent_accumulate m hC h_disj hf]
    exact addContent_mono hC.isSetSemiring (hC.accumulate_mem hf n) hUf
      (Set.accumulate_subset_iUnion _)
  exact tendsto_nhds_unique h_tendsto' (ENNReal.tendsto_nat_tsum fun i â†¦ m (f i))

theorem sUnion_eq_sum_of_union_eq_add (hC_empty : âˆ… âˆˆ C)
    (hC_union : âˆ€ {s t : Set Î±} (_ : s âˆˆ C) (_ : t âˆˆ C), s âˆª t âˆˆ C) (m : Set Î± â†’ â„â‰¥0âˆ)
    (m_empty : m âˆ… = 0)
    (m_add : âˆ€ {s t : Set Î±} (_ : s âˆˆ C) (_ : t âˆˆ C), Disjoint s t â†’ m (s âˆª t) = m s + m t)
    (I : Finset (Set Î±)) (h_ss : â†‘I âŠ† C) (h_dis : Set.PairwiseDisjoint (I : Set (Set Î±)) id)
    (h_mem : â‹ƒâ‚€ â†‘I âˆˆ C) :
    m (â‹ƒâ‚€ I) = âˆ‘ u in I, m u := by
  classical
  induction I using Finset.induction with
  | empty =>
    simp only [Finset.coe_empty, Set.sUnion_empty, Finset.sum_empty, m_empty]
  | @insert s I hsI h =>
    rw [Finset.coe_insert] at *
    rw [Set.insert_subset_iff] at h_ss
    rw [Set.pairwiseDisjoint_insert_of_not_mem] at h_dis
    swap; Â· exact hsI
    have h_sUnion_mem : â‹ƒâ‚€ â†‘I âˆˆ C := by
      have (J : Finset (Set Î±)) : â†‘J âŠ† C â†’ â‹ƒâ‚€ â†‘J âˆˆ C := by
        induction J using Finset.induction with --s J _ h
        | empty => simp only [Finset.coe_empty, Set.empty_subset, Set.sUnion_empty,
            forall_true_left, hC_empty]
        | @insert s I _ h =>
          intro h_insert
          simp only [Finset.coe_insert, Set.sUnion_insert, Set.insert_subset_iff] at h_insert âŠ¢
          exact hC_union h_insert.1 (h h_insert.2)
      exact this I h_ss.2
    rw [Set.sUnion_insert, m_add h_ss.1 h_sUnion_mem (Set.disjoint_sUnion_right.mpr h_dis.2),
      Finset.sum_insert hsI, h h_ss.2 h_dis.1]
    rwa [Set.sUnion_insert] at h_mem

theorem sUnion_eq_sum_of_union_eq_add' (hC_empty : âˆ… âˆˆ C)
    (hC_union : âˆ€ {s t : Set Î±} (_ : s âˆˆ C) (_ : t âˆˆ C), s âˆª t âˆˆ C)
    (m : âˆ€ s : Set Î±, s âˆˆ C â†’ â„â‰¥0âˆ) (m_empty : m âˆ… hC_empty = 0)
    (m_add : âˆ€ {s t : Set Î±} (hs : s âˆˆ C) (ht : t âˆˆ C),
      Disjoint s t â†’ m (s âˆª t) (hC_union hs ht) = m s hs + m t ht)
    (I : Finset (Set Î±)) (h_ss : â†‘I âŠ† C) (h_dis : Set.PairwiseDisjoint (I : Set (Set Î±)) id)
    (h_mem : â‹ƒâ‚€ â†‘I âˆˆ C) : m (â‹ƒâ‚€ I) h_mem = âˆ‘ u : I, m u (h_ss u.property) := by
  have h : extend m (â‹ƒâ‚€ â†‘I) = âˆ‘ u âˆˆ I, extend m u :=
    sUnion_eq_sum_of_union_eq_add hC_empty (fun hs ht â†¦ hC_union hs ht) (extend m)
      (extend_empty hC_empty m_empty) ?_ I h_ss h_dis h_mem
  Â· rw [extend_eq m h_mem] at h
    refine h.trans ?_
    simp_rw [â† extend_eq m, Finset.univ_eq_attach]
    exact (Finset.sum_attach _ _).symm
  Â· simp_rw [â† extend_eq m] at m_add
    exact m_add

end MeasureTheory
