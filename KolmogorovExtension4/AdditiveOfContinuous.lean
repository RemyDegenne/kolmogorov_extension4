/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Peter Pfaffelhuber
-/
import KolmogorovExtension4.Content

open Filter

open scoped ENNReal Topology

namespace MeasureTheory

variable {Î± : Type*} {C : Set (Set Î±)}

lemma apply_sdiff_eq_sub_of_apply_union_eq_add (hC : IsSetRing C) (m : âˆ€ s : Set Î±, s âˆˆ C â†’ â„â‰¥0âˆ)
    (hm_ne_top : âˆ€ {s} (hs : s âˆˆ C), m s hs â‰  âˆ)
    (hm_add : âˆ€ {s t : Set Î±} (hs : s âˆˆ C) (ht : t âˆˆ C),
      Disjoint s t â†’ m (s âˆª t) (hC.union_mem hs ht) = m s hs + m t ht) :
    âˆ€ {s t} (hs : s âˆˆ C) (ht : t âˆˆ C), t âŠ† s â†’
      m (s \ t) (hC.diff_mem hs ht) = m s hs - m t ht := by
  intro s t hs ht hst
  have h_union := hm_add ht (hC.diff_mem hs ht) disjoint_sdiff_self_right
  simp_rw [Set.union_diff_self, Set.union_eq_right.mpr hst] at h_union
  rw [h_union, ENNReal.add_sub_cancel_left (hm_ne_top ht)]

lemma apply_accumulate_eq_sum (hC : IsSetRing C) (m : âˆ€ s : Set Î±, s âˆˆ C â†’ â„â‰¥0âˆ)
    (hm_add : âˆ€ {s t : Set Î±} (hs : s âˆˆ C) (ht : t âˆˆ C),
      Disjoint s t â†’ m (s âˆª t) (hC.union_mem hs ht) = m s hs + m t ht)
    {s : â„• â†’ Set Î±} (hs_disj : Pairwise (Disjoint on s)) (hsC : âˆ€ i, s i âˆˆ C) (n : â„•) :
      m (Set.Accumulate s n) (hC.accumulate_mem hsC n) =
        âˆ‘ i in Finset.range (n + 1), m (s i) (hsC i) := by
  induction n with
  | zero => simp
  | succ n hn =>
    rw [Finset.sum_range_succ, â† hn]
    simp_rw [Set.accumulate_succ]
    rw [hm_add]
    exact Set.disjoint_accumulate hs_disj (Nat.lt_succ_self n)

/-- In a ring of sets, continuity of an additive function at `âˆ…` implies Ïƒ-additivity.
This is not true in general in semirings, or without the hypothesis that `m` is finite. See the
examples 7 and 8 in Halmos' book Measure Theory (1974), page 40. -/
theorem sigma_additive_of_tendsto_zero (hC : IsSetRing C) (m : âˆ€ s : Set Î±, s âˆˆ C â†’ â„â‰¥0âˆ)
    (hm_ne_top : âˆ€ {s} (hs : s âˆˆ C), m s hs â‰  âˆ)
    (hm_add : âˆ€ {s t : Set Î±} (hs : s âˆˆ C) (ht : t âˆˆ C),
      Disjoint s t â†’ m (s âˆª t) (hC.union_mem hs ht) = m s hs + m t ht)
    (hm : âˆ€ â¦ƒs : â„• â†’ Set Î±â¦„ (hs : âˆ€ n, s n âˆˆ C),
      Antitone s â†’ (â‹‚ n, s n) = âˆ… â†’ Tendsto (fun n â†¦ m (s n) (hs n)) atTop (ğ“ 0))
    â¦ƒf : â„• â†’ Set Î±â¦„ (h : âˆ€ i, f i âˆˆ C) (hUf : (â‹ƒ i, f i) âˆˆ C) (h_disj : Pairwise (Disjoint on f)) :
    m (â‹ƒ i, f i) hUf = âˆ‘' i, m (f i) (h i) := by
  -- extend the properties of `m` to `set.sdiff` and `set.accumulate`
  have hm_diff : âˆ€ {s t} (hs : s âˆˆ C) (ht : t âˆˆ C), t âŠ† s â†’
      m (s \ t) (hC.diff_mem hs ht) = m s hs - m t ht :=
    apply_sdiff_eq_sub_of_apply_union_eq_add hC m hm_ne_top hm_add
  have hm_acc : âˆ€ {s : â„• â†’ Set Î±} (_ : Pairwise (Disjoint on s)) (h_meas : âˆ€ i, s i âˆˆ C) (n : â„•),
      m (Set.Accumulate s n) (hC.accumulate_mem h_meas n) =
        âˆ‘ i in Finset.range (n + 1), m (s i) (h_meas i) := apply_accumulate_eq_sum hC m hm_add
  have hm_mono : âˆ€ {s t} (hs : s âˆˆ C) (ht : t âˆˆ C), t âŠ† s â†’ m t ht â‰¤ m s hs := by
    intro s t hs ht hst
    have h_union := hm_add ht (hC.diff_mem hs ht) disjoint_sdiff_self_right
    simp_rw [Set.union_diff_self, Set.union_eq_right.mpr hst] at h_union
    rw [h_union]
    exact le_add_right le_rfl
  -- main proof: we use the continuity of `m` at `âˆ…` on the sequence
  -- `n â†¦ (â‹ƒ i, f i) \ (set.accumulate f n)`
  let s : â„• â†’ Set Î± := fun n â†¦ (â‹ƒ i, f i) \ Set.Accumulate f n
  have hCs : âˆ€ n, s n âˆˆ C := fun n â†¦ hC.diff_mem hUf (hC.accumulate_mem h n)
  have hs_anti : Antitone s := by
    intro i j hij x hxj
    rw [Set.mem_diff] at hxj âŠ¢
    exact âŸ¨hxj.1, fun hxi â†¦ hxj.2 (Set.monotone_accumulate hij hxi)âŸ©
  have hs_Inter : (â‹‚ n, s n) = âˆ… := by
    simp_rw [s, Set.diff_eq]
    rw [Set.iInter_inter_distrib, Set.iInter_const, â† Set.compl_iUnion, Set.iUnion_accumulate]
    exact Set.inter_compl_self _
  have h_tendsto : Tendsto (fun n â†¦ m (s n) (hCs n)) atTop (ğ“ 0) := hm hCs hs_anti hs_Inter
  have hmsn :
      âˆ€ n, m (s n) (hCs n) = m (â‹ƒ i, f i) hUf - âˆ‘ i in Finset.range (n + 1), m (f i) (h i) := by
    intro n
    rw [hm_diff hUf (hC.accumulate_mem h n)]
    Â· congr
      exact hm_acc h_disj _ n
    Â· exact Set.accumulate_subset_iUnion _
  simp_rw [hmsn] at h_tendsto
  have h_tendsto' :
    Tendsto (fun n â†¦ âˆ‘ i in Finset.range n, m (f i) (h i)) atTop (ğ“ (m (â‹ƒ i, f i) hUf)) := by
    refine (Filter.tendsto_add_atTop_iff_nat 1).mp ?_
    rwa [ENNReal.tendsto_atTop_zero_const_sub_iff _ _ (hm_ne_top _)] at h_tendsto
    intro n
    rw [â† hm_acc h_disj]
    exact hm_mono _ _ (Set.accumulate_subset_iUnion _)
  exact tendsto_nhds_unique h_tendsto' (ENNReal.tendsto_nat_tsum fun i â†¦ m (f i) (h i))

/-- In a ring of sets, continuity of an additive content at `âˆ…` implies Ïƒ-additivity.
This is not true in general in semirings, or without the hypothesis that `m` is finite. See the
examples 7 and 8 in Halmos' book Measure Theory (1974), page 40. -/
theorem sigma_additive_addContent_of_tendsto_zero (hC : IsSetRing C) (m : AddContent C)
    (hm_ne_top : âˆ€ s âˆˆ C, m s â‰  âˆ)
    (hm_tendsto : âˆ€ â¦ƒs : â„• â†’ Set Î±â¦„ (_ : âˆ€ n, s n âˆˆ C),
      Antitone s â†’ (â‹‚ n, s n) = âˆ… â†’ Tendsto (fun n â†¦ m (s n)) atTop (ğ“ 0))
    â¦ƒf : â„• â†’ Set Î±â¦„ (hf : âˆ€ i, f i âˆˆ C) (hUf : (â‹ƒ i, f i) âˆˆ C) (h_disj : Pairwise (Disjoint on f)) :
    m (â‹ƒ i, f i) = âˆ‘' i, m (f i) := by
  -- We use the continuity of `m` at `âˆ…` on the sequence `n â†¦ (â‹ƒ i, f i) \ (set.accumulate f n)`
  let s : â„• â†’ Set Î± := fun n â†¦ (â‹ƒ i, f i) \ Set.Accumulate f n
  have hCs : âˆ€ n, s n âˆˆ C := fun n â†¦ hC.diff_mem hUf (hC.accumulate_mem hf n)
  have h_tendsto : Tendsto (fun n â†¦ m (s n)) atTop (ğ“ 0) := by
    refine hm_tendsto hCs ?_ ?_
    Â· intro i j hij x hxj
      rw [Set.mem_diff] at hxj âŠ¢
      exact âŸ¨hxj.1, fun hxi â†¦ hxj.2 (Set.monotone_accumulate hij hxi)âŸ©
    Â· simp_rw [s, Set.diff_eq]
      rw [Set.iInter_inter_distrib, Set.iInter_const, â† Set.compl_iUnion, Set.iUnion_accumulate]
      exact Set.inter_compl_self _
  have hmsn : âˆ€ n, m (s n) = m (â‹ƒ i, f i) - âˆ‘ i in Finset.range (n + 1), m (f i) := by
    intro n
    rw [addContent_diff_of_ne_top m hC hm_ne_top hUf (hC.accumulate_mem hf n)]
    Â· congr
      exact addContent_accumulate m hC h_disj hf n
    Â· exact Set.accumulate_subset_iUnion _
  simp_rw [hmsn] at h_tendsto
  have h_tendsto' :
      Tendsto (fun n â†¦ âˆ‘ i in Finset.range n, m (f i)) atTop (ğ“ (m (â‹ƒ i, f i))) := by
    refine (Filter.tendsto_add_atTop_iff_nat 1).mp ?_
    rwa [ENNReal.tendsto_atTop_zero_const_sub_iff _ _ (hm_ne_top _ hUf)] at h_tendsto
    intro n
    rw [â† addContent_accumulate m hC h_disj hf]
    exact addContent_mono hC.isSetSemiring (hC.accumulate_mem hf n) hUf
      (Set.accumulate_subset_iUnion _)
  exact tendsto_nhds_unique h_tendsto' (ENNReal.tendsto_nat_tsum fun i â†¦ m (f i))

theorem sUnion_eq_sum_of_union_eq_add (hC_empty : âˆ… âˆˆ C)
    (hC_union : âˆ€ {s t : Set Î±} (_ : s âˆˆ C) (_ : t âˆˆ C), s âˆª t âˆˆ C) (m : Set Î± â†’ â„â‰¥0âˆ)
    (m_empty : m âˆ… = 0)
    (m_add : âˆ€ {s t : Set Î±} (_ : s âˆˆ C) (_ : t âˆˆ C), Disjoint s t â†’ m (s âˆª t) = m s + m t)
    (I : Finset (Set Î±)) (h_ss : â†‘I âŠ† C) (h_dis : Set.PairwiseDisjoint (I : Set (Set Î±)) id)
    (h_mem : â‹ƒâ‚€ â†‘I âˆˆ C) : m (â‹ƒâ‚€ I) = âˆ‘ u in I, m u := by
  classical
  induction' I using Finset.induction with s I hsI h
  Â· simp only [Finset.coe_empty, Set.sUnion_empty, Finset.sum_empty, m_empty]
  rw [Finset.coe_insert] at *
  rw [Set.insert_subset_iff] at h_ss
  rw [Set.pairwiseDisjoint_insert_of_not_mem] at h_dis
  swap
  Â· exact hsI
  have h_sUnion_mem : â‹ƒâ‚€ â†‘I âˆˆ C :=
    haveI : âˆ€ J : Finset (Set Î±), â†‘J âŠ† C â†’ â‹ƒâ‚€ â†‘J âˆˆ C := by
      intro J
      induction' J using Finset.induction with s J _ h
      Â· simp only [Finset.coe_empty, Set.empty_subset, Set.sUnion_empty, forall_true_left, hC_empty]
      Â· intro h_insert
        rw [Finset.coe_insert] at h_insert âŠ¢
        rw [Set.insert_subset_iff] at h_insert
        rw [Set.sUnion_insert]
        exact hC_union h_insert.1 (h h_insert.2)
    this I h_ss.2
  rw [Set.sUnion_insert, m_add h_ss.1 h_sUnion_mem (Set.disjoint_sUnion_right.mpr h_dis.2),
    Finset.sum_insert hsI, h h_ss.2 h_dis.1]
  rw [Set.sUnion_insert] at h_mem
  exact h_sUnion_mem

theorem sUnion_eq_sum_of_union_eq_add' (hC_empty : âˆ… âˆˆ C)
    (hC_union : âˆ€ {s t : Set Î±} (_ : s âˆˆ C) (_ : t âˆˆ C), s âˆª t âˆˆ C)
    (m : âˆ€ s : Set Î±, s âˆˆ C â†’ â„â‰¥0âˆ) (m_empty : m âˆ… hC_empty = 0)
    (m_add : âˆ€ {s t : Set Î±} (hs : s âˆˆ C) (ht : t âˆˆ C),
      Disjoint s t â†’ m (s âˆª t) (hC_union hs ht) = m s hs + m t ht)
    (I : Finset (Set Î±)) (h_ss : â†‘I âŠ† C) (h_dis : Set.PairwiseDisjoint (I : Set (Set Î±)) id)
    (h_mem : â‹ƒâ‚€ â†‘I âˆˆ C) : m (â‹ƒâ‚€ I) h_mem = âˆ‘ u : I, m u (h_ss u.property) := by
  have h :=
    sUnion_eq_sum_of_union_eq_add hC_empty (fun hs ht â†¦ hC_union hs ht) (extend m)
      (extend_empty hC_empty m_empty) ?_ I h_ss h_dis h_mem
  Â· rw [extend_eq m h_mem] at h
    refine h.trans ?_
    simp_rw [â† extend_eq m, Finset.univ_eq_attach]
    exact (Finset.sum_attach _ _).symm
  Â· simp_rw [â† extend_eq m] at m_add
    exact m_add

end MeasureTheory
