/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import KolmogorovExtension4.IonescuTulceaKernel
import Mathlib.Probability.Kernel.Composition.MeasureComp

open MeasureTheory ProbabilityTheory Finset Filter Topology Kernel Preorder MeasurableEquiv

open scoped ENNReal

namespace MeasureTheory

section Preliminaries

variable {Î¹ : Type*}

theorem _root_.preimage_restrictâ‚‚ {Î± : Î¹ â†’ Type*} {I J : Finset Î¹} [âˆ€ i : Î¹, Decidable (i âˆˆ I)]
    (hIJ : I âŠ† J) (s : (i : I) â†’ Set (Î± i)) :
    (restrictâ‚‚ hIJ) â»Â¹' (Set.univ.pi s) =
      (@Set.univ J).pi (fun j â†¦ if h : j.1 âˆˆ I then s âŸ¨j.1, hâŸ© else Set.univ) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall]
  refine âŸ¨fun h i hi â†¦ ?_, fun h i i_mem â†¦ by simpa [i_mem] using h i (hIJ i_mem)âŸ©
  split_ifs with i_mem
  Â· exact h i i_mem
  Â· trivial

variable {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]
variable (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

/-- Consider a family of probability measures. You can take their products for any fimite
subfamily. This gives a projective family of measures, see `IsProjectiveMeasureFamily`. -/
lemma isProjectiveMeasureFamily_pi :
    IsProjectiveMeasureFamily (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  refine fun I J hJI â†¦ Measure.pi_eq (fun s ms â†¦ ?_)
  classical
  rw [Measure.map_apply (measurable_restrictâ‚‚ hJI) (MeasurableSet.univ_pi ms),
    preimage_restrictâ‚‚ hJI, Measure.pi_pi]
  let g := fun i â†¦ (Î¼ i) (if hi : i âˆˆ J then s âŸ¨i, hiâŸ© else Set.univ)
  conv_lhs => change âˆ i : I, g i
  have h2 : univ.prod (fun i : J â†¦ (Î¼ i) (s i)) = univ.prod (fun i : J â†¦ g i) :=
    Finset.prod_congr rfl (by simp [g])
  rw [h2, prod_coe_sort, prod_coe_sort, prod_subset hJI (fun _ h h' â†¦ by simp [g, h, h'])]

theorem kolContent_eq_measure_pi [Fintype Î¹] {s : Set (Î  i, X i)} (hs : MeasurableSet s) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) s = Measure.pi Î¼ s := by
  let aux : (Î  i : univ, X i) â†’ (Î  i, X i) := fun x i â†¦ x âŸ¨i, mem_univ iâŸ©
  have maux : Measurable aux := measurable_pi_lambda _ (fun _ â†¦ measurable_pi_apply _)
  have pi_eq : Measure.pi Î¼ = (Measure.pi (fun i : univ â†¦ Î¼ i)).map aux := by
    refine Measure.pi_eq fun a ma â†¦ ?_
    rw [Measure.map_apply maux (MeasurableSet.univ_pi ma)]
    have : aux â»Â¹' Set.univ.pi a = Set.univ.pi (fun i : @univ Î¹ _ â†¦ a i) := by ext x; simp [aux]
    rw [this, Measure.pi_pi]
    simp
  have : s = cylinder univ (aux â»Â¹' s) := by ext x; simp [aux]
  nth_rw 1 [this]
  rw [kolContent_cylinder _ (maux hs), pi_eq, Measure.map_apply maux hs]

end Preliminaries

section Nat

/-! ### Product of measures indexed by `â„•` -/

variable {X : â„• â†’ Type*}

lemma _root_.IocProdIoc_preim {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c)
    (s : (i : Ioc a c) â†’ Set (X i)) :
    IocProdIoc a b c â»Â¹' (Set.univ.pi s) =
      (Set.univ.pi <| restrictâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) (Ioc_subset_Ioc_right hbc) s) Ã—Ë¢
        (Set.univ.pi <| restrictâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) (Ioc_subset_Ioc_left hab) s) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, IocProdIoc, forall_const, Subtype.forall,
    mem_Ioc, Set.mem_prod, restrictâ‚‚]
  refine âŸ¨fun h â†¦ âŸ¨fun i âŸ¨hi1, hi2âŸ© â†¦ ?_, fun i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©, fun âŸ¨h1, h2âŸ© i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©
  Â· convert h i âŸ¨hi1, hi2.trans hbcâŸ©
    rw [dif_pos hi2]
  Â· convert h i âŸ¨lt_of_le_of_lt hab hi1, hi2âŸ©
    rw [dif_neg (not_le.2 hi1)]
  Â· split_ifs with hi3
    Â· exact h1 i âŸ¨hi1, hi3âŸ©
    Â· exact h2 i âŸ¨not_le.1 hi3, hi2âŸ©

lemma _root_.IicProdIoc_preim {a b : â„•} (hab : a â‰¤ b) (s : (i : Iic b) â†’ Set (X i)) :
    IicProdIoc a b â»Â¹' (Set.univ.pi s) =
      (Set.univ.pi <| frestrictLeâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) hab s) Ã—Ë¢
        (Set.univ.pi <| restrictâ‚‚ (Ï€ := (fun n â†¦ Set (X n))) Ioc_subset_Iic_self s) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, IicProdIoc, forall_const, Subtype.forall,
    mem_Iic, Set.mem_prod, frestrictLeâ‚‚_apply, restrictâ‚‚, mem_Ioc]
  refine âŸ¨fun h â†¦ âŸ¨fun i hi â†¦ ?_, fun i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©, fun âŸ¨h1, h2âŸ© i hi â†¦ ?_âŸ©
  Â· convert h i (hi.trans hab)
    rw [dif_pos hi]
  Â· convert h i hi2
    rw [dif_neg (not_le.2 hi1)]
  Â· split_ifs with hi3
    Â· exact h1 i hi3
    Â· exact h2 i âŸ¨not_le.1 hi3, hiâŸ©

variable [âˆ€ n, MeasurableSpace (X n)]
  (Î¼ : (n : â„•) â†’ Measure (X n)) [hÎ¼ : âˆ€ n, IsProbabilityMeasure (Î¼ n)]

/-- Infinite product measure indexed by `â„•`. This is an auxiliary construction, you should use
the generic product measure `Measure.productMeasure`. -/
noncomputable def Measure.infinitePiNat : Measure ((n : â„•) â†’ X n) :=
  (traj (fun n â†¦ const _ (Î¼ (n + 1))) 0) âˆ˜â‚˜ (Measure.pi (fun i : Iic 0 â†¦ Î¼ i))

instance : IsProbabilityMeasure (Measure.infinitePiNat Î¼) := by
  rw [Measure.infinitePiNat]; infer_instance

lemma Measure.pi_prod_map_IocProdIoc {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c) :
    ((Measure.pi (fun i : Ioc a b â†¦ Î¼ i)).prod (Measure.pi (fun i : Ioc b c â†¦ Î¼ i))).map
      (IocProdIoc a b c) = Measure.pi (fun i : Ioc a c â†¦ Î¼ i) := by
  refine (Measure.pi_eq fun s ms â†¦ ?_).symm
  rw [Measure.map_apply, IocProdIoc_preim hab hbc, Measure.prod_prod, Measure.pi_pi, Measure.pi_pi,
    â† prod_Ioc hab hbc]
  Â· rfl
  Â· fun_prop
  Â· exact MeasurableSet.univ_pi ms

lemma Measure.pi_prod_map_IicProdIoc {a b : â„•} :
    ((Measure.pi (fun i : Iic a â†¦ Î¼ i)).prod (Measure.pi (fun i : Ioc a b â†¦ Î¼ i))).map
      (IicProdIoc a b) = Measure.pi (fun i : Iic b â†¦ Î¼ i) := by
  obtain hab | hba := le_total a b
  Â· refine (Measure.pi_eq fun s ms â†¦ ?_).symm
    rw [Measure.map_apply, IicProdIoc_preim hab, Measure.prod_prod, Measure.pi_pi, Measure.pi_pi,
      â† prod_Iic hab (f := fun i â†¦ Î¼ i (s i))]
    Â· rfl
    Â· fun_prop
    Â· exact MeasurableSet.univ_pi ms
  Â· rw [IicProdIoc_le hba, â† Measure.map_map, â† Measure.fst, Measure.fst_prod]
    exact isProjectiveMeasureFamily_pi Î¼ (Iic a) (Iic b) (Iic_subset_Iic.2 hba) |>.symm
    all_goals fun_prop

lemma Measure.map_piSingleton (Î¼ : (n : â„•) â†’ Measure (X n)) [âˆ€ n, SigmaFinite (Î¼ n)] (n : â„•) :
    (Î¼ (n + 1)).map (piSingleton n) = Measure.pi (fun i : Ioc n (n + 1) â†¦ Î¼ i) := by
  refine (Measure.pi_eq fun s hs â†¦ ?_).symm
  have : Subsingleton (Ioc n (n + 1)) := by
    rw [Nat.Ioc_succ_singleton]
    infer_instance
  rw [Fintype.prod_subsingleton _ âŸ¨n + 1, mem_Ioc_succ.2 rflâŸ©, Measure.map_apply]
  Â· congr with x
    simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, forall_const, Subtype.forall,
      Nat.Ioc_succ_singleton, mem_singleton]
    exact âŸ¨fun h â†¦ h (n + 1) rfl, fun h a b â†¦ b.symm â–¸ hâŸ©
  Â· exact (piSingleton n).measurable
  Â· exact MeasurableSet.univ_pi hs

theorem ptraj_const' {a b : â„•} :
    (ptraj (fun n â†¦ const _ (Î¼ (n + 1))) a b).map (restrictâ‚‚ (Ioc_subset_Iic_self (a := a))) =
    const _ (Measure.pi (fun i : Ioc a b â†¦ Î¼ i)) := by
  obtain hab | hba := lt_or_le a b
  Â· refine Nat.le_induction ?_ (fun n hn hind â†¦ ?_) b (Nat.succ_le.2 hab) <;> ext1 xâ‚€
    Â· rw [ptraj_succ_self, Kernel.map_map, map_apply, prod_apply, map_apply, const_apply,
        const_apply, Measure.map_piSingleton, restrictâ‚‚_comp_IicProdIoc, Measure.map_snd_prod,
        measure_univ, one_smul]
      all_goals fun_prop
    Â· have : (restrictâ‚‚ (Ioc_subset_Iic_self (a := a))) âˆ˜ (IicProdIoc (X := X) n (n + 1)) =
          (IocProdIoc a n (n + 1)) âˆ˜ (Prod.map (restrictâ‚‚ Ioc_subset_Iic_self) id) := rfl
      rw [const_apply, ptraj_succ_of_le (by omega), map_const, prod_const_comp, id_comp,
        Kernel.map_map, this, â† Kernel.map_map, Kernel.map_prod, hind,  Kernel.map_id, map_apply,
        prod_apply, const_apply, const_apply, Measure.map_piSingleton,
        Measure.pi_prod_map_IocProdIoc]
      any_goals fun_prop
      all_goals omega
  Â· have : IsEmpty (Ioc a b) := by simpa [hba] using Subtype.isEmpty_false
    ext x s ms
    by_cases hs : s.Nonempty
    Â· rw [Subsingleton.eq_univ_of_nonempty hs, @measure_univ .., measure_univ]
      exact (IsMarkovKernel.map _ (measurable_restrictâ‚‚ _)) |>.isProbabilityMeasure x
    Â· rw [Set.not_nonempty_iff_eq_empty.1 hs]
      simp

theorem ptraj_const {a b : â„•} :
    ptraj (fun n â†¦ const _ (Î¼ (n + 1))) a b =
      (Kernel.id Ã—â‚– (const _ (Measure.pi (fun i : Ioc a b â†¦ Î¼ i)))).map (IicProdIoc a b) := by
  rw [ptraj_eq_prod, ptraj_const']

theorem isProjectiveLimit_infinitePiNat :
    IsProjectiveLimit (Measure.infinitePiNat Î¼)
      (fun I : Finset â„• â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  have _ := ProbabilityMeasure.nonempty âŸ¨Î¼ 0, hÎ¼ 0âŸ©
  intro I
  simp_rw [isProjectiveMeasureFamily_pi Î¼ _ _ I.sub_Iic]
  rw [â† restrictâ‚‚_comp_restrict I.sub_Iic, â† Measure.map_map, â† frestrictLe, Measure.infinitePiNat,
    Measure.map_comp, traj_map_frestrictLe, ptraj_const, â† Measure.map_comp,
    â† Measure.compProd_eq_comp_prod, Measure.compProd_const, Measure.pi_prod_map_IicProdIoc]
  any_goals fun_prop

theorem kolContent_eq_infinitePiNat {A : Set ((n : â„•) â†’ X n)} (hA : A âˆˆ measurableCylinders X) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) A = Measure.infinitePiNat Î¼ A := by
  obtain âŸ¨s, S, mS, A_eqâŸ© : âˆƒ s S, MeasurableSet S âˆ§ A = cylinder s S := by
    simpa [mem_measurableCylinders] using hA
  rw [kolContent_congr _ A A_eq mS, A_eq, cylinder, â† Measure.map_apply (measurable_restrict _) mS,
    isProjectiveLimit_infinitePiNat Î¼]

end Nat

section ProductMeasure

/-! ### Product of infinitely many probability measures -/

variable {Î¹ : Type*} {X : Î¹ â†’ Type*}

section castLemmas

lemma cast_pi_eval (s : Set Î¹) (x : Î  i : s, X i) (i j : s) (h : i = j) :
    cast (congrArg X (Subtype.coe_inj.2 h)) (x i) = x j := by cases h; rfl

lemma cast_mem_cast (Î± Î² : Type u) (h : Î± = Î²) (a : Î±) (s : Set Î±) :
    (cast h a âˆˆ cast (congrArg Set h) s) = (a âˆˆ s) := by cases h; rfl

variable [mX : âˆ€ i, MeasurableSpace (X i)]

lemma HEq_measurableSpace {i j : Î¹} (hij : i = j) : HEq (mX i) (mX j) := by cases hij; rfl

end castLemmas

variable [âˆ€ i, MeasurableSpace (X i)]
  (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

/-- This theorem is used to prove the existence of the product measure: the `kolContent` of
a decreasing sequence of cylinders with empty intersection converges to `0`, in the case where
the measurable spaces are indexed by a countable type. This implies the Ïƒ-additivity of
`kolContent` (see `sigma_additive_addContent_of_tendsto_zero`),
which allows to extend it to the Ïƒ-algebra by CarathÃ©odory's theorem. -/
theorem secondLemma
    (Ï† : â„• â‰ƒ Î¹) {A : â„• â†’ Set (Î  i, X i)} (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ kolContent (isProjectiveMeasureFamily_pi Î¼) (A n)) atTop (ğ“ 0) := by
  set Î¼_proj := isProjectiveMeasureFamily_pi Î¼
  let Î¼_fproj := isProjectiveMeasureFamily_pi (fun k : â„• â†¦ Î¼ (Ï† k))
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S := by
    simpa [mem_measurableCylinders] using A_mem n
  choose s S mS A_eq using A_cyl
  -- The goal of the proof is to apply the same result when the index set is `â„•`. To do so we
  -- have to pull back the sets `sâ‚™` and `Sâ‚™` using equivalences.
  let t n := (s n).preimage Ï† Ï†.injective.injOn
  have h i : X (Ï† (Ï†.symm i)) = X i := congrArg X (Ï†.apply_symm_apply i)
  have e n i (h : i âˆˆ s n) : Ï†.symm i âˆˆ t n := by simpa [t] using h
  have e' n k (h : k âˆˆ t n) : Ï† k âˆˆ s n := by simpa [t] using h
  -- The function `f` does the link between families indexed by `â„•` and those indexed by `Î¹`.
  -- Here we have to use `cast` because otherwhise we land in `X (Ï† (Ï†.symm i))`, which is not
  -- definitionally equal to X i.
  have meas_cast i : Measurable (cast (h i)) := measurable_cast _ (HEq_measurableSpace (by simp))
  let f : (Î  k, X (Ï† k)) â†’ Î  i, X i := fun x i â†¦ cast (h i) (x (Ï†.symm i))
  -- `aux n` is an equivalence between `sâ‚™` ans `tâ‚™`, it will be used to link the two.
  let aux n : s n â‰ƒ t n :=
    { toFun := fun i â†¦ âŸ¨Ï†.symm i, e n i.1 i.2âŸ©
      invFun := fun k â†¦ âŸ¨Ï† k, e' n k.1 k.2âŸ©
      left_inv := fun _ â†¦ by simp
      right_inv := fun _ â†¦ by simp }
  -- `gâ‚™` is the equivalent of `f` for families indexed by `tâ‚™` and `sâ‚™`.
  let g n : (Î  k : t n, X (Ï† k)) â†’ Î  i : s n, X i := fun x i â†¦ cast (h i) (x (aux n i))
  -- Now fe define `Bâ‚™` and `Tâ‚™` as follows. `Bâ‚™` is a cylinder.
  let B n := f â»Â¹' (A n)
  let T n := (g n) â»Â¹' (S n)
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq]
    rfl
  -- `gâ‚™` is measurable. We have to play with `Heq` to prove measurability of `cast`.
  have mg n : Measurable (g n) :=
    measurable_pi_lambda _ (fun i â†¦ (meas_cast _).comp <| measurable_pi_apply _)
  -- We deduce that `Tâ‚™` is measurable.
  have mT n : MeasurableSet (T n) := (mS n).preimage (mg n)
  -- The sequence `(Bâ‚™)` satisfies the hypotheses of `firstLemma`, we now have to prove that we can
  -- rewrite the goal in terms of `B`.
  have B_anti : Antitone B := fun m n hmn â†¦ Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  have B_mem n : B n âˆˆ measurableCylinders (fun k â†¦ X (Ï† k)) :=
    (mem_measurableCylinders (B n)).2 âŸ¨t n, T n, mT n, B_eq nâŸ©
  -- Taking the preimage of a product indexed by `sâ‚™` by `gâ‚™` yields a product indexed by `tâ‚™`,
  -- again we have to play with `cast`.
  have g_preim n (u : (i : s n) â†’ Set (X i)) : (g n) â»Â¹' (Set.univ.pi u) =
      Set.univ.pi (fun k : t n â†¦ u ((aux n).symm k)) := by
    ext x
    simp [Equiv.coe_fn_mk, Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies,
      Subtype.forall, Equiv.coe_fn_symm_mk, g, aux]
    refine âŸ¨fun h' k hk â†¦ ?_, fun h' i hi â†¦ ?_âŸ©
    Â· convert h' (Ï† k) (e' n k hk)
      rw [@cast_pi_eval â„• (fun k â†¦ X (Ï† k)) (t n) x âŸ¨Ï†.symm (Ï† k), by simp [hk]âŸ© âŸ¨k, hkâŸ© (by simp)]
    Â· convert h' (Ï†.symm i) (e n i hi)
      rw [â† @cast_pi_eval Î¹ (fun i â†¦ Set (X i)) (s n) u âŸ¨Ï† (Ï†.symm i), by simp [hi]âŸ©
          âŸ¨i, hiâŸ© (by simp),
        cast_mem_cast (X (Ï† (Ï†.symm i))) (X i) (by simp) (x âŸ¨Ï†.symm i, e n i hiâŸ©)
          (u âŸ¨Ï† (Ï†.symm i), by simp [hi]âŸ©)]
  -- The pushforward measure of the product measure of `(Î¼_{Ï† k})_{k âˆˆ tâ‚™}` by `gâ‚™` is the
  -- product measre of `(Î¼áµ¢)_{i âˆˆ sâ‚™}`.
  have pi_s n : Measure.pi (fun i : s n â†¦ Î¼ i) =
      (Measure.pi (fun k : t n â†¦ Î¼ (Ï† k))).map (g n) := by
    refine Measure.pi_eq (fun x mx â†¦ ?_)
    rw [Measure.map_apply (mg n), g_preim n, Measure.pi_pi,
      Fintype.prod_equiv (aux n).symm _ (fun i â†¦ (Î¼ i) (x i))]
    Â· simp [aux]
    Â· exact MeasurableSet.pi Set.countable_univ (by simp [mx])
  -- This yields the desired result: the `kolContent` of `Aâ‚™` is the same as the one of `Bâ‚™`.
  have kol n : kolContent Î¼_proj (A n) = kolContent Î¼_fproj (B n) := by
    rw [kolContent_congr Î¼_proj _ (A_eq n) (mS n), kolContent_congr Î¼_fproj _ (B_eq n) (mT n),
      pi_s n, Measure.map_apply (mg n) (mS n)]
  simp_rw [kol, fun n â†¦ kolContent_eq_infinitePiNat (fun k â†¦ Î¼ (Ï† k)) (B_mem n),
    â† measure_empty (Î¼ := Measure.infinitePiNat (fun k â†¦ Î¼ (Ï† k))), â† B_inter]
  exact tendsto_measure_iInter_atTop
    (fun n â†¦ (MeasurableSet.of_mem_measurableCylinders (B_mem n)).nullMeasurableSet)
    B_anti âŸ¨0, measure_ne_top _ _âŸ©

theorem thirdLemma {A : â„• â†’ Set (Î  i, X i)} (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ kolContent (isProjectiveMeasureFamily_pi Î¼) (A n)) atTop (ğ“ 0) := by
  have : âˆ€ i, Nonempty (X i) := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©
  set Î¼_proj := isProjectiveMeasureFamily_pi Î¼
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S :=
    (mem_measurableCylinders _).1 (A_mem n)
  choose s S mS A_eq using A_cyl
  -- The family `(Aâ‚™)` only depends on a countable set of coordinates, called `u`. Therefore our
  -- goal is to see it as a family indexed by this countable set,
  -- so that we can apply `secondLemma`. The proof is very similar to the previous one, except
  -- that the use of coercions avoids manipulating `cast`, as types will be definitionally equal.
  let u := â‹ƒ n, (s n).toSet
  let Î¼_fproj := isProjectiveMeasureFamily_pi (fun i : u â†¦ Î¼ i)
  -- `tâ‚™` will be `sâ‚™` seen as a subset of `u`.
  let t n : Finset u := (s n).preimage Subtype.val Subtype.val_injective.injOn
  -- These are a few lemmas to move between `sâ‚™` and `tâ‚™`.
  have su n : (s n).toSet âŠ† u := Set.subset_iUnion (fun n â†¦ (s n).toSet) n
  have st n i (hi : i âˆˆ s n) : âŸ¨i, su n hiâŸ© âˆˆ t n := by simpa [t] using hi
  have ts n i (hi : i âˆˆ t n) : i.1 âˆˆ s n := by simpa [t] using hi
  classical
  let f : (Î  i : u, X i) â†’ Î  i, X i :=
    fun x i â†¦ if hi : i âˆˆ u then x âŸ¨i, hiâŸ© else Classical.ofNonempty
  -- This brings again `aux`.
  let aux n : s n â‰ƒ t n :=
    { toFun := fun i â†¦ âŸ¨âŸ¨i.1, su n i.2âŸ©, st n i i.2âŸ©
      invFun := fun i â†¦ âŸ¨i.1.1, ts n i i.2âŸ©
      left_inv := fun i â†¦ by simp
      right_inv := fun i â†¦ by simp }
  let g n : (Î  i : t n, X i) â†’ Î  i : s n, X i := fun x i â†¦ x (aux n i)
  have test n : (s n).restrict âˆ˜ f = (g n) âˆ˜ (fun (x : (i : u) â†’ X i) i â†¦ x i) := by
    ext x i
    simp [f, g, aux, su n i.2]
  let B n := f â»Â¹' (A n)
  let T n := (g n) â»Â¹' (S n)
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq, cylinder, â† Set.preimage_comp, test]
    rfl
  have mg n : Measurable (g n) := by fun_prop
  have mT n : MeasurableSet (T n) := mg n (mS n)
  have B_anti : Antitone B := fun m n hmn â†¦ Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  have B_mem n : B n âˆˆ measurableCylinders (fun i : u â†¦ X i) :=
    (mem_measurableCylinders (B n)).2 âŸ¨t n, T n, mT n, B_eq nâŸ©
  have g_preim n (a : Î  i : s n, Set (X i)) : (g n) â»Â¹' (Set.univ.pi a) =
      Set.univ.pi (fun i : t n â†¦ a ((aux n).symm i)) := by
    ext x
    simp only [Equiv.coe_fn_mk, Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies,
      Equiv.coe_fn_symm_mk, g]
    exact âŸ¨fun h i â†¦ h ((aux n).symm i), fun h i â†¦ h (aux n i)âŸ©
  -- The pushforward measure of the product measure of `(Î¼áµ¢)_{i âˆˆ tâ‚™}` by `gâ‚™` is the
  -- product measre of `(Î¼áµ¢)_{i âˆˆ sâ‚™}`.
  have pi_s n : Measure.pi (fun i : s n â†¦ Î¼ i) =
      (Measure.pi (fun i : t n â†¦ Î¼ i)).map (g n) := by
    refine Measure.pi_eq (fun x mx â†¦ ?_)
    rw [Measure.map_apply (mg n), g_preim n, Measure.pi_pi,
      Fintype.prod_equiv (aux n).symm _ (fun i â†¦ (Î¼ i) (x i))]
    Â· simp [aux]
    Â· exact MeasurableSet.pi Set.countable_univ (by simp [mx])
  -- This yields the desired result: the `kolContent` of `Aâ‚™` is the same as the one of `Bâ‚™`.
  have kol n : kolContent Î¼_proj (A n) = kolContent Î¼_fproj (B n) := by
    rw [kolContent_congr Î¼_proj _ (A_eq n) (mS n), kolContent_congr Î¼_fproj _ (B_eq n) (mT n),
      pi_s n, Measure.map_apply (mg n) (mS n)]
  -- We now have two cases: if `u` is finite, then the result is simple because
  -- we have an actual measure.
  obtain u_fin | u_inf := finite_or_infinite u
  Â· have := Fintype.ofFinite u
    simp_rw [kol, fun n â†¦ kolContent_eq_measure_pi (fun i : u â†¦ Î¼ i)
      (MeasurableSet.of_mem_measurableCylinders (B_mem n)),
      â† measure_empty (Î¼ := Measure.pi (fun i : u â†¦ Î¼ i)), â† B_inter]
    exact tendsto_measure_iInter_atTop
      (fun n â†¦ (MeasurableSet.of_mem_measurableCylinders (B_mem n)).nullMeasurableSet)
      B_anti âŸ¨0, measure_ne_top _ _âŸ©
  Â· -- If `u` is infinite, then we have an equivalence with `â„•` so we can apply `secondLemma`.
    have count_u : Countable u := Set.countable_iUnion (fun n â†¦ (s n).countable_toSet)
    obtain âŸ¨Ï†, -âŸ© := Classical.exists_true_of_nonempty (Î± := â„• â‰ƒ u) nonempty_equiv_of_countable
    simp_rw [kol]
    exact secondLemma (fun i : u â†¦ Î¼ i) Ï† B_mem B_anti B_inter

/-- The `kolContent` associated to a family of probability measures is Ïƒ-subadditive. -/
theorem kolContent_pi_sigma_subadditive â¦ƒf : â„• â†’ Set (Î  i, X i)â¦„
    (hf : âˆ€ n, f n âˆˆ measurableCylinders X) (hf_Union : (â‹ƒ n, f n) âˆˆ measurableCylinders X) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) (â‹ƒ n, f n) â‰¤
    âˆ‘' n, kolContent (isProjectiveMeasureFamily_pi Î¼) (f n) := by
  classical
  have : âˆ€ i, Nonempty (X i) := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©
  refine addContent_iUnion_le_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' â†¦ ?_) f hf hf_Union
  exact addContent_iUnion_eq_sum_of_tendsto_zero isSetRing_measurableCylinders
    (kolContent (isProjectiveMeasureFamily_pi Î¼)) (fun s hs â†¦ kolContent_ne_top _)
    (fun _ â†¦ thirdLemma Î¼) hf hf_Union hf'

/-- The product measure of an arbitrary family of probability measures. It is defined as the unique
extension of the function which gives to cylinders the measure given by the associated product
measure. -/
noncomputable def Measure.productMeasure : Measure (Î  i, X i) := by
  exact (kolContent (isProjectiveMeasureFamily_pi Î¼)).measure isSetSemiring_measurableCylinders
    generateFrom_measurableCylinders.ge (kolContent_pi_sigma_subadditive Î¼)

open Measure

/-- The product measure is the projective limit of the partial product measures. This ensures
uniqueness and expresses the value of the product measures applied to cylinders. -/
theorem isProjectiveLimit_productMeasure :
    IsProjectiveLimit (productMeasure Î¼) (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  intro I
  ext1 s hs
  rw [Measure.map_apply (measurable_restrict I) hs, productMeasure, AddContent.measure_eq,
    kolContent_congr _ (_ â»Â¹' s) rfl hs]
  Â· exact generateFrom_measurableCylinders.symm
  Â· exact cylinder_mem_measurableCylinders _ _ hs

instance : IsProbabilityMeasure (productMeasure Î¼) := by
  constructor
  rw [â† cylinder_univ âˆ…, cylinder, â† Measure.map_apply, isProjectiveLimit_productMeasure Î¼,
    measure_univ]
  Â· exact measurable_restrict _
  Â· exact MeasurableSet.univ

lemma productMeasure_boxes {s : Finset Î¹} {t : (i : Î¹) â†’ Set (X i)}
    (mt : âˆ€ i âˆˆ s, MeasurableSet (t i)) :
    productMeasure Î¼ (Set.pi s t) = âˆ i âˆˆ s, (Î¼ i) (t i) := by
  have : Set.pi s t = cylinder s ((@Set.univ s).pi (fun i : s â†¦ t i)) := by
    ext x
    simp
  rw [this, cylinder, â† Measure.map_apply, isProjectiveLimit_productMeasure Î¼,
    Measure.pi_pi]
  Â· rw [Finset.univ_eq_attach, Finset.prod_attach _ (fun i â†¦ (Î¼ i) (t i))]
  Â· exact measurable_restrict _
  Â· exact MeasurableSet.pi Set.countable_univ fun i _ â†¦ mt i.1 i.2

theorem productMeasure_eq_pi [Fintype Î¹] : productMeasure Î¼ = Measure.pi Î¼ := by
  refine (pi_eq fun s hs â†¦ ?_).symm
  rw [â† coe_univ, productMeasure_boxes]
  simpa

lemma productMeasure_cylinder {s : Finset Î¹} {S : Set (Î  i : s, X i)} (mS : MeasurableSet S) :
    productMeasure Î¼ (cylinder s S) = Measure.pi (fun i : s â†¦ Î¼ i) S := by
  rw [cylinder, â† Measure.map_apply (measurable_restrict _) mS, isProjectiveLimit_productMeasure Î¼]

theorem integral_dep_productMeasure {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {s : Finset Î¹} {f : (Î  i : s, X i) â†’ E}
    (hf : AEStronglyMeasurable f (Measure.pi (fun i : s â†¦ Î¼ i))) :
    âˆ« y, f (s.restrict y) âˆ‚productMeasure Î¼ = âˆ« y, f y âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† integral_map, isProjectiveLimit_productMeasure Î¼]
  Â· fun_prop
  Â· rwa [isProjectiveLimit_productMeasure Î¼]

/-- The canonical filtration on dependent functions indexed by `Î¹`, where `piFinset s` consists of
measurable sets depending only on coordinates is `s`. -/
def Filtration.piFinset : @Filtration (Î  i, X i) (Finset Î¹) _ inferInstance where
  seq s := (inferInstance : MeasurableSpace (Î  i : s, X i)).comap s.restrict
  mono' s t hst := by
    simp only
    rw [â† restrictâ‚‚_comp_restrict hst, â† MeasurableSpace.comap_comp]
    exact MeasurableSpace.comap_mono (measurable_restrictâ‚‚ _).comap_le
  le' s := (measurable_restrict s).comap_le

open Filtration

/-- If a function is strongly measurable with respect to the Ïƒ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem dependsOn_of_stronglyMeasurable' {E : Type*} [NormedAddCommGroup E]
    {s : Finset Î¹} {f : (Î  i, X i) â†’ E}
    (mf : StronglyMeasurable[piFinset s] f) : DependsOn f s := by
  intro x y hxy
  apply eq_of_stronglyMeasurable_comap s.restrict mf
  exact dependsOn_restrict s hxy

theorem integral_stronglyMeasurable [DecidableEq Î¹] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {s : Finset Î¹} {f : (Î  i, X i) â†’ E}
    (mf : StronglyMeasurable[piFinset s] f) (x : Î  i, X i) :
    âˆ« y, f y âˆ‚productMeasure Î¼ =
    âˆ« y, f (Function.updateFinset x s y) âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  let g : (Î  i : s, X i) â†’ E := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    apply dependsOn_of_stronglyMeasurable' mf
    intro i hi
    simp only [Function.updateFinset, Finset.restrict, dite_eq_ite, ite_eq_left_iff]
    exact fun h â†¦ (h hi).elim
  rw [â† integral_congr_ae <| Eventually.of_forall this, integral_dep_productMeasure]
  exact mf.comp_measurable (measurable_updateFinset.mono le_rfl (piFinset.le s))
    |>.aestronglyMeasurable

theorem lintegral_dep {s : Finset Î¹} {f : (Î  i : s, X i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» y, f (s.restrict y) âˆ‚productMeasure Î¼ = âˆ«â» y, f y âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† lintegral_map hf (measurable_restrict _), isProjectiveLimit_productMeasure Î¼]

/-- If a function is measurable with respect to the Ïƒ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem measurable_dependsOn' {s : Finset Î¹} {f : (Î  i, X i) â†’ â„â‰¥0âˆ}
    (mf : Measurable[piFinset s] f) : DependsOn f s := by
  intro x y hxy
  apply eq_of_measurable_comap s.restrict mf
  exact dependsOn_restrict s hxy

theorem lintegral_measurable_piFinset [DecidableEq Î¹] {s : Finset Î¹}
    {f : (Î  i, X i) â†’ â„â‰¥0âˆ} (mf : Measurable[piFinset s] f)
    (x : Î  i, X i) : âˆ«â» y, f y âˆ‚productMeasure Î¼ = (âˆ«â‹¯âˆ«â»_s, f âˆ‚Î¼) x := by
  let g : (Î  i : s, X i) â†’ â„â‰¥0âˆ := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    refine measurable_dependsOn' mf fun i hi â†¦ ?_
    simp only [Function.updateFinset, Finset.restrict, dite_eq_ite, ite_eq_left_iff]
    exact fun h â†¦ (h hi).elim
  simp_rw [â† this]
  rw [lintegral_dep]
  Â· rfl
  Â· exact mf.comp (measurable_updateFinset.mono (_root_.le_refl _) (piFinset.le s))

end ProductMeasure
