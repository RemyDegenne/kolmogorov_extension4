/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import KolmogorovExtension4.IonescuTulceaKernel

open MeasureTheory MeasurableSpace ProbabilityTheory Finset ENNReal Filter Topology Function Kernel Preorder

namespace MeasureTheory

section Preliminaries

variable {Î¹ : Type*}

theorem preimage_proj {Î± : Î¹ â†’ Type*} (I J : Finset Î¹) [âˆ€ i : Î¹, Decidable (i âˆˆ I)]
    (hIJ : I âŠ† J) (s : (i : I) â†’ Set (Î± i)) :
    (restrictâ‚‚ hIJ) â»Â¹' (Set.univ.pi s) =
    (@Set.univ J).pi (fun j â†¦ if h : j.1 âˆˆ I then s âŸ¨j.1, hâŸ© else Set.univ) := by
  ext x
  simp only [Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall]
  refine âŸ¨fun h i hi â†¦ ?_, fun h i i_mem â†¦ by simpa [i_mem] using h i (hIJ i_mem)âŸ©
  split_ifs with i_mem
  Â· exact h i i_mem
  Â· trivial

variable {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]
variable (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

/-- Consider a family of probability measures. You can take their products for any fimite
subfamily. This gives a projective family of measures, see `IsProjectiveMeasureFamily`. -/
theorem isProjectiveMeasureFamily_pi :
    IsProjectiveMeasureFamily (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  refine fun I J hJI â†¦ Measure.pi_eq (fun s ms â†¦ ?_)
  classical
  rw [Measure.map_apply (measurable_restrictâ‚‚ hJI) (MeasurableSet.univ_pi ms),
    preimage_proj J I hJI, Measure.pi_pi]
  let g := fun i â†¦ (Î¼ i) (if hi : i âˆˆ J then s âŸ¨i, hiâŸ© else Set.univ)
  conv_lhs => change âˆ i : I, g i
  have h2 : univ.prod (fun i : J â†¦ (Î¼ i) (s i)) = univ.prod (fun i : J â†¦ g i) :=
    Finset.prod_congr rfl (by simp [g])
  rw [h2, Finset.prod_coe_sort, Finset.prod_coe_sort,
    Finset.prod_subset hJI (fun _ h h' â†¦ by simp [g, h, h'])]

theorem kolContent_eq_measure_pi [Fintype Î¹] {s : Set ((i : Î¹) â†’ X i)} (hs : MeasurableSet s) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) s = Measure.pi Î¼ s := by
  let aux : ((i : univ) â†’ X i) â†’ ((i : Î¹) â†’ X i) := fun x i â†¦ x âŸ¨i, mem_univ iâŸ©
  have maux : Measurable aux := measurable_pi_lambda _ (fun _ â†¦ measurable_pi_apply _)
  let t := aux â»Â¹' s
  have : s = cylinder Finset.univ t := by ext x; simp [t, aux]
  nth_rw 1 [this]
  rw [kolContent_cylinder _ (maux hs)]
  have : Measure.pi Î¼ = (Measure.pi (fun i : @univ Î¹ _ â†¦ Î¼ i)).map aux := by
    refine Measure.pi_eq fun a ma â†¦ ?_
    rw [Measure.map_apply maux (MeasurableSet.univ_pi ma)]
    have : aux â»Â¹' Set.univ.pi a = Set.univ.pi (fun i : @univ Î¹ _ â†¦ a i) := by ext x; simp [aux]
    rw [this, Measure.pi_pi]
    simp
  rw [this, Measure.map_apply maux hs]

end Preliminaries

section Nat

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]
variable (Î¼ : (n : â„•) â†’ Measure (X n)) [hÎ¼ : âˆ€ n, IsProbabilityMeasure (Î¼ n)]

lemma mem_Iic_zero {i : â„•} (hi : i âˆˆ Iic 0) : 0 = i := (by simpa using hi : i = 0).symm

/-- `{0} = Ici 0`, version as a measurable equivalence for dependent functions. -/
def zer : (X 0) â‰ƒáµ ((i : Iic 0) â†’ X i) where
  toFun := fun xâ‚€ i â†¦ mem_Iic_zero i.2 â–¸ xâ‚€
  invFun := fun x â†¦ x âŸ¨0, mem_Iic.2 <| _root_.le_refl 0âŸ©
  left_inv := fun xâ‚€ â†¦ by simp
  right_inv := fun x â†¦ by
    ext i
    have : âŸ¨0, mem_Iic.2 <| _root_.le_refl 0âŸ© = i := by simp [mem_Iic_zero i.2]
    cases this; rfl
  measurable_toFun := by
    refine measurable_pi_lambda _ (fun i â†¦ ?_)
    simp_rw [eqRec_eq_cast]
    apply measurable_cast
    have : âŸ¨0, mem_Iic.2 <| _root_.le_refl 0âŸ© = i := by simp [mem_Iic_zero i.2]
    cases this; rfl
  measurable_invFun := measurable_pi_apply _

/-- Infinite product measure indexed by `â„•`. Use instead `Measure.productMeasure` for the case of a
general index space. -/
noncomputable def Measure.infinitePiNat : Measure ((n : â„•) â†’ X n) :=
  (Measure.pi (fun i : Iic 0 â†¦ Î¼ i)).bind
    (@ionescuTulceaKernel _ _
      (fun n â†¦ const _ (Î¼ (n + 1))) _ (ProbabilityMeasure.nonempty âŸ¨Î¼ 0, hÎ¼ 0âŸ©) 0)

open Measure

instance {X Y : Type*} [MeasurableSpace X] [MeasurableSpace Y] {Î¼ : Measure X} {Îº : Kernel X Y}
    [IsProbabilityMeasure Î¼] [IsMarkovKernel Îº] : IsProbabilityMeasure (Î¼.bind Îº) where
  measure_univ := by
    rw [bind_apply MeasurableSet.univ (Kernel.measurable Îº)]
    simp

instance : IsProbabilityMeasure (infinitePiNat Î¼) := by
  rw [infinitePiNat]
  infer_instance

theorem er_succ_preimage_pi {n : â„•} (hn : 0 < n) (s : (i : Ioc 0 (n + 1)) â†’ Set (X i)) :
    er 0 n (n + 1) hn n.le_succ â»Â¹' Set.univ.pi s =
      (Set.univ.pi (fun i : Ioc 0 n â†¦ s âŸ¨i.1, Ioc_subset_Ioc_right n.le_succ i.2âŸ©)) Ã—Ë¢
        ((e n).symm â»Â¹' (s âŸ¨n + 1, right_mem_Ioc.2 n.succ_posâŸ©)) := by
  ext p
  simp only [er, Nat.succ_eq_add_one, Nat.reduceAdd, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk,
    Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall, mem_Ioc, e,
    MeasurableEquiv.symm_mk, Equiv.coe_fn_symm_mk, Set.mem_prod]
  refine âŸ¨fun h â†¦ âŸ¨fun i âŸ¨hi1, hi2âŸ© â†¦ ?_, ?_âŸ©, fun âŸ¨h1, h2âŸ© i âŸ¨hi1, hi2âŸ© â†¦ ?_âŸ©
  Â· convert h i âŸ¨hi1, hi2.trans n.le_succâŸ©
    rw [dif_pos hi2]
  Â· convert h (n + 1) âŸ¨n.succ_pos, _root_.le_refl _âŸ©
    simp
  Â· split_ifs with h
    Â· exact h1 i âŸ¨hi1, hâŸ©
    Â· cases (by omega : i = n + 1)
      exact h2

theorem kerNat_prod {N : â„•} (hN : 0 < N) :
    (kerNat (fun n â†¦ const _ (Î¼ (n + 1))) 0 N) =
      Kernel.const _ (Measure.pi (fun i : Ioc 0 N â†¦ Î¼ i)) := by
  ext1 xâ‚€
  refine Nat.le_induction ?_ (fun n hn hind â†¦ ?_) N (Nat.succ_le.2 hN)
  Â· rw [kerNat_succ_self, Kernel.const_apply]
    refine (Measure.pi_eq (fun s ms â†¦ ?_)).symm
    have : Subsingleton (Ioc 0 1) := by
      constructor
      rintro âŸ¨i, hiâŸ© âŸ¨j, hjâŸ©
      rw [mem_Ioc_succ] at hi hj
      simp [hi, hj]
    rw [Fintype.prod_subsingleton _ âŸ¨1, right_mem_Ioc.2 zero_lt_oneâŸ©,
      map_apply' _ (e 0).measurable, Kernel.const_apply]
    Â· congr with x
      simp only [Nat.reduceAdd, e, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk, Nat.succ_eq_add_one,
        Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall,
        Nat.Ioc_succ_singleton, zero_add, mem_singleton, Nat.zero_eq]
      refine âŸ¨fun h â†¦ h 1 rfl, fun h i hi â†¦ ?_âŸ©
      cases hi; exact h
    Â· exact MeasurableSet.univ_pi ms
  Â· rw [Kernel.const_apply]
    refine (Measure.pi_eq fun s ms â†¦ ?_).symm
    rw [kerNat_succ_right _ _ _ (Nat.succ_le.1 hn), kerNat_succ_self, compProdNat,
      dif_pos âŸ¨Nat.succ_le.1 hn, n.lt_succ_selfâŸ©,
      map_apply' _ _ _ (MeasurableSet.univ_pi ms), er_succ_preimage_pi (Nat.succ_le.1 hn),
      split, Kernel.map_const, Kernel.comap_const, Kernel.compProd_apply_prod, â† prod_Ioc,
      â† Measure.pi_pi, â† setLIntegral_const, hind, Kernel.const_apply]
    Â· congr with x
      rw [Kernel.const_apply, Measure.map_apply (e n).measurable]
      Â· congr
      Â· exact (e n).measurable_invFun (ms _)
    Â· exact MeasurableSet.univ_pi (fun _ â†¦ ms _)
    Â· exact (e n).measurable_invFun (ms _)
    Â· exact (e n).measurable
    Â· exact (er ..).measurable

theorem prod_noyau_proj (N : â„•) :
    partialKernel (fun n â†¦ const ((i : Iic n) â†’ X i) (Î¼ (n + 1))) 0 N =
      Kernel.map ((deterministic id measurable_id) Ã—â‚–
          (const _ (Measure.pi (fun i : Ioc 0 N â†¦ Î¼ i))))
        (el 0 N (zero_le N)) := by
  rcases eq_zero_or_pos N with rfl | hN
  Â· have : IsEmpty (Ioc 0 0) := by simp
    rw [partialKernel, dif_neg (lt_irrefl 0), Measure.pi_of_empty]
    ext x s ms
    rw [Kernel.map_apply _ (el ..).measurable, deterministic_apply, Kernel.prod_apply,
      deterministic_apply, Kernel.const_apply, Measure.dirac_prod_dirac,
      Measure.map_apply (el 0 0 (_root_.le_refl 0)).measurable ms,
      Measure.dirac_apply' _ ((el 0 0 (_root_.le_refl 0)).measurable ms),
      Measure.dirac_apply' _ ms]
    apply indicator_const_eq
    simp only [id_eq, el, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk, mem_preimage]
    congrm (fun i â†¦ ?_) âˆˆ s
    simp [(mem_Iic_zero i.2).symm]
  Â· rw [partialKernel, dif_pos hN, kerNat_prod _ hN]

theorem el_preimage {n : â„•} (s : (i : Iic n) â†’ Set (X i)) :
    (el 0 n (zero_le n)) â»Â¹' (Set.univ.pi s) =
      (Set.univ.pi fun i : Iic 0 â†¦ s âŸ¨i.1, Iic_subset_Iic.2 (zero_le n) i.2âŸ©) Ã—Ë¢
      (Set.univ.pi fun i : Ioc 0 n â†¦ s âŸ¨i.1, Ioc_subset_Iic_self i.2âŸ©) := by
  ext p
  simp only [el, nonpos_iff_eq_zero, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk, Set.mem_preimage,
    Set.mem_pi, Set.mem_univ, true_implies, Subtype.forall, mem_Iic, Set.mem_prod, mem_Ioc]
  constructor
  Â· intro h
    constructor
    Â· rintro - rfl
      exact h 0 (zero_le n)
    Â· rintro a âŸ¨h1, h2âŸ©
      convert h a h2
      rw [dif_neg h1.ne']
  Â· intro h a ha
    obtain rfl | ha' := eq_zero_or_pos a
    Â· exact h.1 0 rfl
    Â· rw [dif_neg ha'.ne']
      exact h.2 a âŸ¨ha', haâŸ©

theorem Measure.map_bind {X Y Z : Type*} [MeasurableSpace X] [MeasurableSpace Y]
    [MeasurableSpace Z]
    (Î¼ : Measure X) (Îº : Kernel X Y) (f : Y â†’ Z) (mf : Measurable f) :
    (Î¼.bind Îº).map f = Î¼.bind (Îº.map f) := by
  ext s ms
  rw [Measure.map_apply mf ms, Measure.bind_apply ms (Kernel.measurable _),
    Measure.bind_apply (mf ms) (Kernel.measurable _)]
  simp_rw [Kernel.map_apply' _ mf _ ms]

theorem map_bind_eq_bind_comap {X Y Z : Type*} [MeasurableSpace X] [MeasurableSpace Y]
    [MeasurableSpace Z]
    (Î¼ : Measure X) (Îº : Kernel Y Z) (f : X â†’ Y) (mf : Measurable f) :
    (Î¼.map f).bind Îº = Î¼.bind (Kernel.comap Îº f mf) := by
  ext s ms
  rw [Measure.bind_apply ms (Kernel.measurable _), lintegral_map, Measure.bind_apply ms]
  Â· rfl
  Â· exact Kernel.measurable _
  Â· exact Kernel.measurable_coe _ ms
  Â· exact mf

theorem isProjectiveLimit_infinitePiNat :
    IsProjectiveLimit (infinitePiNat Î¼) (fun I : Finset â„• â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  have _M := ProbabilityMeasure.nonempty âŸ¨Î¼ 0, hÎ¼ 0âŸ©
  intro I
  simp_rw [isProjectiveMeasureFamily_pi Î¼ _ _ I.sub_Iic]
  rw [â† restrictâ‚‚_comp_restrict I.sub_Iic,
    â† Measure.map_map (measurable_restrictâ‚‚ _) (measurable_restrict _), â† frestrictLe]
  congr
  rw [infinitePiNat, Measure.map_bind, ionescuTulceaKernel_proj]; swap
  Â· exact measurable_frestrictLe _
  refine (Measure.pi_eq fun s ms â†¦ ?_).symm
  have mpis := MeasurableSet.univ_pi ms
  rw [Measure.bind_apply mpis (Kernel.measurable _),
    â† prod_congr' (Iic_union_Ioc_eq_Iic (zero_le _)), â† prod_union' (disjoint_Iic_Ioc (zero_le _)),
    mul_comm, â† Measure.pi_pi (Î¹ := Iic 0), â† setLIntegral_const,
    â† lintegral_indicator _ (MeasurableSet.univ_pi (fun _ â†¦ ms _))]
  congr with xâ‚€
  rw [prod_noyau_proj, Kernel.map_apply', Kernel.prod_apply, el_preimage,
    Measure.prod_prod, deterministic_apply', Kernel.const_apply, indicator_one_mul_const']
  Â· simp
  Â· exact MeasurableSet.univ_pi fun _ â†¦ ms _
  Â· exact (el ..).measurable
  Â· exact mpis

theorem kolContent_eq_infinitePiNat {A : Set ((n : â„•) â†’ X n)} (hA : A âˆˆ measurableCylinders X) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) A = infinitePiNat Î¼ A := by
  obtain âŸ¨s, S, mS, A_eqâŸ© : âˆƒ s S, MeasurableSet S âˆ§ A = cylinder s S := by
    simpa [mem_measurableCylinders] using hA
  rw [kolContent_congr _ A A_eq mS, A_eq, cylinder, â† Measure.map_apply (measurable_restrict _) mS,
    isProjectiveLimit_infinitePiNat Î¼]

end Nat

section ProductMeasure

variable {Î¹ : Type*}
variable {X : Î¹ â†’ Type*} [hX : âˆ€ i, MeasurableSpace (X i)]
variable (Î¼ : (i : Î¹) â†’ Measure (X i)) [hÎ¼ : âˆ€ i, IsProbabilityMeasure (Î¼ i)]

lemma cast_pi_eval {X : Î¹ â†’ Type*} (s : Set Î¹) (x : (i : s) â†’ X i) (i j : s) (h : i = j)
    (h' : X i = X j) :
    cast h' (x i) = x j := by
  subst h
  rfl

lemma cast_mem_cast (Î± Î² : Type u) (h : Î± = Î²) (a : Î±) (s : Set Î±) (h' : Set Î± = Set Î²) :
    (cast h a âˆˆ cast h' s) = (a âˆˆ s) := by
  subst h
  rfl

lemma HEq_meas {i j : Î¹} (hij : i = j) :
    HEq (inferInstance : MeasurableSpace (X i)) (inferInstance : MeasurableSpace (X j)) := by
  cases hij; rfl

/-- This theorem is used to prove the existence of the product measure: the `kolContent` of
a decresaing sequence of cylinders with empty intersection converges to `0`, in the case where
the measurable spaces are indexed by a countable type. This implies the Ïƒ-additivity of
`kolContent` (see `sigma_additive_addContent_of_tendsto_zero`),
which allows to extend it to the Ïƒ-algebra by CarathÃ©odory's theorem. -/
theorem secondLemma
    (Ï† : â„• â‰ƒ Î¹) {A : â„• â†’ Set ((i : Î¹) â†’ X i)} (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ kolContent (isProjectiveMeasureFamily_pi Î¼) (A n)) atTop (ğ“ 0) := by
  set Î¼_proj := isProjectiveMeasureFamily_pi Î¼
  let Î¼_fproj := isProjectiveMeasureFamily_pi (fun k : â„• â†¦ Î¼ (Ï† k))
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S := by
    simpa [mem_measurableCylinders] using A_mem n
  choose s S mS A_eq using A_cyl
  -- The goal of the proof is to apply the same result when the index set is `â„•`. To do so we
  -- have to pull back the sets `sâ‚™` and `Sâ‚™` using equivalences.
  let t n := (s n).preimage Ï† Ï†.injective.injOn
  have h i : X (Ï† (Ï†.symm i)) = X i := congrArg X (Ï†.apply_symm_apply i)
  have e n i (h : i âˆˆ s n) : Ï†.symm i âˆˆ t n := by simpa [t] using h
  have e' n k (h : k âˆˆ t n) : Ï† k âˆˆ s n := by simpa [t] using h
  -- The function `f` does the link between families indexed by `â„•` and those indexed by `Î¹`.
  -- Here we have to use `cast` because otherwhise we land in `X (Ï† (Ï†.symm i))`, which is not
  -- definitionally equal to X i.
  have meas_cast i : Measurable (cast (h i)) := by
    apply measurable_cast
    exact HEq_meas (by simp)
  let f : ((k : â„•) â†’ X (Ï† k)) â†’ (i : Î¹) â†’ X i := fun x i â†¦ cast (h i) (x (Ï†.symm i))
  -- `aux n` is an equivalence between `sâ‚™` ans `tâ‚™`, it will be used to link the two.
  let aux n : s n â‰ƒ t n :=
    { toFun := fun i â†¦ âŸ¨Ï†.symm i, e n i.1 i.2âŸ©
      invFun := fun k â†¦ âŸ¨Ï† k, e' n k.1 k.2âŸ©
      left_inv := fun _ â†¦ by simp
      right_inv := fun _ â†¦ by simp }
  -- `gâ‚™` is the equivalent of `f` for families indexed by `tâ‚™` and `sâ‚™`.
  let g n : ((k : t n) â†’ X (Ï† k)) â†’ (i : s n) â†’ X i :=
    fun x i â†¦ cast (h i) (x (aux n i))
  -- Now fe define `Bâ‚™` and `Tâ‚™` as follows. `Bâ‚™` is a cylinder.
  let B n := f â»Â¹' (A n)
  let T n := (g n) â»Â¹' (S n)
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq]
    rfl
  -- `gâ‚™` is measurable. We have to play with `Heq` to prove measurability of `cast`.
  have mg n : Measurable (g n) :=
    measurable_pi_lambda _ (fun i â†¦ (meas_cast _).comp <| measurable_pi_apply _)
  -- We deduce that `Tâ‚™` is measurable.
  have mT n : MeasurableSet (T n) := (mS n).preimage (mg n)
  -- The sequence `(Bâ‚™)` satisfies the hypotheses of `firstLemma`, we now have to prove that we can
  -- rewrite the goal in terms of `B`.
  have B_anti : Antitone B := fun m n hmn â†¦ Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  have B_mem n : B n âˆˆ measurableCylinders (fun k â†¦ X (Ï† k)) :=
    (mem_measurableCylinders (B n)).2 âŸ¨t n, T n, mT n, B_eq nâŸ©
  -- Taking the preimage of a product indexed by `sâ‚™` by `gâ‚™` yields a product indexed by `uâ‚™`,
  -- again we have to play with `cast`.
  have imp n (u : (i : s n) â†’ Set (X i)) : (g n) â»Â¹' (Set.univ.pi u) =
      Set.univ.pi (fun k : t n â†¦ u ((aux n).symm k)) := by
    ext x
    simp only [Equiv.coe_fn_mk, Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies,
      Subtype.forall, Equiv.coe_fn_symm_mk, g, aux]
    refine âŸ¨fun h' k hk â†¦ ?_, fun h' i hi â†¦ ?_âŸ©
    Â· convert h' (Ï† k) (e' n k hk)
      rw [@cast_pi_eval â„• (fun k â†¦ X (Ï† k)) (t n) x âŸ¨Ï†.symm (Ï† k), by simp [hk]âŸ© âŸ¨k, hkâŸ©]
      simp
    Â· convert h' (Ï†.symm i) (e n i hi)
      rw [â† @cast_pi_eval Î¹ (fun i â†¦ Set (X i)) (s n) u âŸ¨Ï† (Ï†.symm i), by simp [hi]âŸ©
          âŸ¨i, hiâŸ© (by simp) _,
        cast_mem_cast (X (Ï† (Ï†.symm i))) (X i) (by simp) (x âŸ¨Ï†.symm i, e n i hiâŸ©)
          (u âŸ¨Ï† (Ï†.symm i), by simp [hi]âŸ©) (by simp)]
  -- The pushforward measure of the product measure of `(Î¼_{Ï† k})_{k âˆˆ tâ‚™}` by `gâ‚™` is the
  -- product measre of `(âˆ¨áµ¢)_{i âˆˆ sâ‚™}`.
  have test' n : Measure.pi (fun i : s n â†¦ Î¼ i) =
      (Measure.pi (fun k : t n â†¦ Î¼ (Ï† k))).map (g n) := by
    refine Measure.pi_eq (fun x mx â†¦ ?_)
    rw [Measure.map_apply (mg n), imp n, Measure.pi_pi,
      Fintype.prod_equiv (aux n).symm _ (fun i â†¦ (Î¼ i) (x i))]
    Â· simp [aux]
    Â· exact MeasurableSet.pi Set.countable_univ (by simp [mx])
  -- This yields the desired result: the `kolContent` of `Aâ‚™` is the same as the one of `Bâ‚™`.
  have crucial n : kolContent Î¼_proj (A n) = kolContent Î¼_fproj (B n) := by
    simp_rw [fun n â†¦ kolContent_congr Î¼_proj _ (A_eq n) (mS n),
      fun n â†¦ kolContent_congr Î¼_fproj _ (B_eq n) (mT n), T, test' n]
    rw [Measure.map_apply (mg n) (mS n)]
  simp_rw [crucial, fun n â†¦ kolContent_eq_infinitePiNat (fun k â†¦ Î¼ (Ï† k)) (B_mem n),
    â† measure_empty (Î¼ := Measure.infinitePiNat (fun k â†¦ Î¼ (Ï† k))), â† B_inter]
  exact tendsto_measure_iInter
    (fun n â†¦ (MeasurableSet.of_mem_measurableCylinders (B_mem n)).nullMeasurableSet)
    B_anti âŸ¨0, measure_ne_top _ _âŸ©

/-- The `kolContent` of `cylinder I S` can be computed by integrating the indicator of
`cylinder I S` over the variables indexed by `I`. -/
theorem kolContent_eq_lmarginal [DecidableEq Î¹]
    (I : Finset Î¹) {S : Set ((i : I) â†’ X i)} (mS : MeasurableSet S) (x : (i : Î¹) â†’ X i) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) (cylinder I S) =
    (âˆ«â‹¯âˆ«â»_I, (cylinder I S).indicator 1 âˆ‚Î¼) x := by
  rw [kolContent_cylinder _ mS, â† lintegral_indicator_one mS]
  refine lintegral_congr <| fun x â†¦ ?_
  by_cases hx : x âˆˆ S <;> simp [hx, Function.updateFinset, restrict_def]

theorem thirdLemma (A : â„• â†’ Set ((i : Î¹) â†’ X i)) (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X)
    (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…) :
    Tendsto (fun n â†¦ kolContent (isProjectiveMeasureFamily_pi Î¼) (A n)) atTop (ğ“ 0) := by
  have : âˆ€ i, Nonempty (X i) := by
    have := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©
    infer_instance
  set Î¼_proj := isProjectiveMeasureFamily_pi Î¼
  have A_cyl n : âˆƒ s S, MeasurableSet S âˆ§ A n = cylinder s S :=
    (mem_measurableCylinders _).1 (A_mem n)
  choose s S mS A_eq using A_cyl
  -- The family `(Aâ‚™)` only depends on a countable set of coordinates, called `u`. Therefore our
  -- goal is to see it as a family indexed by this countable set,
  -- so that we can apply `secondLemma`. The proof is very similar to the previous one, except
  -- that the use of coercions avoids manipulating `cast`, as equalities will hold by `rfl`.
  let u := â‹ƒ n, (s n).toSet
  let Î¼_fproj := isProjectiveMeasureFamily_pi (fun i : u â†¦ Î¼ i)
  -- `tâ‚™` will be `sâ‚™` seen as a subset of `u`.
  let t n : Finset u := (s n).preimage Subtype.val Subtype.val_injective.injOn
  -- These are a few lemmas to move between `sâ‚™` and `tâ‚™`.
  have su n : (s n).toSet âŠ† u := Set.subset_iUnion (fun n â†¦ (s n).toSet) n
  have st n i (hi : i âˆˆ s n) : âŸ¨i, su n hiâŸ© âˆˆ t n := by simpa [t] using hi
  have ts n i (hi : i âˆˆ t n) : i.1 âˆˆ s n := by simpa [t] using hi
  classical
  let f : ((i : u) â†’ X i) â†’ (i : Î¹) â†’ X i :=
    fun x i â†¦ if hi : i âˆˆ u then x âŸ¨i, hiâŸ© else Classical.ofNonempty
  -- This brings again `aux`.
  let aux : (n : â„•) â†’ (s n â‰ƒ t n) := fun n â†¦
    { toFun := fun i â†¦ âŸ¨âŸ¨i.1, su n i.2âŸ©, st n i i.2âŸ©
      invFun := fun i â†¦ âŸ¨i.1.1, ts n i i.2âŸ©
      left_inv := fun i â†¦ by simp
      right_inv := fun i â†¦ by simp }
  let g n : ((i : t n) â†’ X i) â†’ (i : s n) â†’ X i := fun x i â†¦ x (aux n i)
  have test n : (s n).restrict âˆ˜ f = (g n) âˆ˜ (fun (x : (i : u) â†’ X i) i â†¦ x i) := by
    ext x i
    simp [f, g, aux, su n i.2, restrict_def]
  let B n := f â»Â¹' (A n)
  let T n := (g n) â»Â¹' (S n)
  have B_eq n : B n = cylinder (t n) (T n) := by
    simp_rw [B, A_eq, cylinder, â† Set.preimage_comp, test]
    rfl
  have mg n : Measurable (g n) := measurable_pi_lambda _ (fun _ â†¦ measurable_pi_apply _)
  have mT n : MeasurableSet (T n) := mg n (mS n)
  have B_anti : Antitone B := fun m n hmn â†¦ Set.preimage_mono <| A_anti hmn
  have B_inter : â‹‚ n, B n = âˆ… := by
    simp_rw [B, â† Set.preimage_iInter, A_inter, Set.preimage_empty]
  have B_mem n : B n âˆˆ measurableCylinders (fun i : u â†¦ X i) :=
    (mem_measurableCylinders (B n)).2 âŸ¨t n, T n, mT n, B_eq nâŸ©
  have imp n (a : (i : s n) â†’ Set (X i)) : (g n) â»Â¹' (Set.univ.pi a) =
      Set.univ.pi (fun i : t n â†¦ a ((aux n).symm i)) := by
    ext x
    simp only [Equiv.coe_fn_mk, Set.mem_preimage, Set.mem_pi, Set.mem_univ, true_implies,
      Equiv.coe_fn_symm_mk, g]
    exact âŸ¨fun h i â†¦ h ((aux n).symm i), fun h i â†¦ h (aux n i)âŸ©
  -- The pushforward measure of the product measure of `(Î¼_{Ï† k})_{k âˆˆ tâ‚™}` by `gâ‚™` is the
  -- product measre of `(âˆ¨áµ¢)_{i âˆˆ sâ‚™}`.
  have test' n : Measure.pi (fun i : s n â†¦ Î¼ i) =
      (Measure.pi (fun i : t n â†¦ Î¼ i)).map (g n) := by
    refine Measure.pi_eq (fun x mx â†¦ ?_)
    rw [Measure.map_apply (mg n), imp n, Measure.pi_pi,
      Fintype.prod_equiv (aux n).symm _ (fun i â†¦ (Î¼ i) (x i))]
    Â· simp [aux]
    Â· exact MeasurableSet.pi Set.countable_univ (by simp [mx])
  -- This yields the desired result: the `kolContent` of `Aâ‚™` is the same as the one of `Bâ‚™`.
  have crucial n : kolContent Î¼_proj (A n) = kolContent Î¼_fproj (B n) := by
    simp_rw [fun n â†¦ kolContent_congr Î¼_proj _ (A_eq n) (mS n),
      fun n â†¦ kolContent_congr Î¼_fproj _ (B_eq n) (mT n), T, test' n]
    rw [Measure.map_apply (mg n) (mS n)]
  -- We now have two cases: if `u` is finite, then the result is simple because
  -- we have an actual measure.
  rcases finite_or_infinite u with (u_fin | u_inf)
  Â· have := Fintype.ofFinite u
    simp_rw [crucial,
      fun n â†¦ kolContent_eq_measure_pi (fun i : u â†¦ Î¼ i)
        (MeasurableSet.of_mem_measurableCylinders (B_mem n)),
      â† measure_empty (Î¼ := Measure.pi (fun i : u â†¦ Î¼ i)), â† B_inter]
    exact tendsto_measure_iInter
      (fun n â†¦ (MeasurableSet.of_mem_measurableCylinders (B_mem n)).nullMeasurableSet)
      B_anti âŸ¨0, measure_ne_top _ _âŸ©
  Â· -- If `u` is infinite, then we have an equivalence with `â„•` so we can apply `secondLemma`.
    have count_u : Countable u := Set.countable_iUnion (fun n â†¦ (s n).countable_toSet)
    obtain âŸ¨Ï†, -âŸ© := Classical.exists_true_of_nonempty (Î± := â„• â‰ƒ u) nonempty_equiv_of_countable
    simp_rw [crucial]
    exact secondLemma (fun i : u â†¦ Î¼ i) Ï† B_mem B_anti B_inter

/-- The `kolContent` associated to a family of probability measures is Ïƒ-subadditive. -/
theorem kolContent_pi_sigma_subadditive â¦ƒf : â„• â†’ Set ((i : Î¹) â†’ X i)â¦„
    (hf : âˆ€ n, f n âˆˆ measurableCylinders X)
    (hf_Union : (â‹ƒ n, f n) âˆˆ measurableCylinders X) :
    kolContent (isProjectiveMeasureFamily_pi Î¼) (â‹ƒ n, f n) â‰¤
    âˆ‘' n, kolContent (isProjectiveMeasureFamily_pi Î¼) (f n) := by
  classical
  have : âˆ€ i, Nonempty (X i) := by
    have := fun i â†¦ ProbabilityMeasure.nonempty âŸ¨Î¼ i, hÎ¼ iâŸ©;
    infer_instance
  refine addContent_iUnion_le_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' â†¦ ?_) f hf hf_Union
  refine sigma_additive_addContent_of_tendsto_zero isSetRing_measurableCylinders
    (kolContent (isProjectiveMeasureFamily_pi Î¼)) (fun s hs â†¦ ?_) ?_ hf hf_Union hf'
  Â· rcases (mem_measurableCylinders _).1 hs with âŸ¨I, S, mS, s_eqâŸ©
    rw [s_eq, kolContent_eq_lmarginal Î¼ (mS := mS) (x := Classical.ofNonempty)]
    refine ne_of_lt (lt_of_le_of_lt ?_ (by norm_num : (1 : â„â‰¥0âˆ) < âŠ¤))
    rw [â† lmarginal_const (Î¼ := Î¼) (s := I) 1 Classical.ofNonempty]
    apply lmarginal_mono
    intro x
    apply Set.indicator_le
    simp
  Â· intro s hs anti_s inter_s
    exact thirdLemma Î¼ s hs anti_s inter_s

/-- The product measure of an arbitrary family of probability measures. It is defined as the unique
extension of the function which gives to cylinders the measure given by the associated product
measure. -/
noncomputable def productMeasure : Measure ((i : Î¹) â†’ X i) := by
  exact Measure.ofAddContent isSetSemiring_measurableCylinders generateFrom_measurableCylinders
    (kolContent (isProjectiveMeasureFamily_pi Î¼))
    (kolContent_pi_sigma_subadditive Î¼)

/-- The product measure is the projective limit of the partial product measures. This ensures
uniqueness and expresses the value of the product measures applied to cylinders. -/
theorem isProjectiveLimit_productMeasure :
    IsProjectiveLimit (productMeasure Î¼) (fun I : Finset Î¹ â†¦ (Measure.pi (fun i : I â†¦ Î¼ i))) := by
  intro I
  ext1 s hs
  rw [Measure.map_apply _ hs]
  swap; Â· apply measurable_restrict
  have h_mem : I.restrict â»Â¹' s âˆˆ measurableCylinders X := by
    rw [mem_measurableCylinders]; exact âŸ¨I, s, hs, rflâŸ©
  conv_lhs => change (productMeasure Î¼) (I.restrict â»Â¹' s)
  rw [productMeasure, Measure.ofAddContent_eq _ _ _ _ h_mem,
    kolContent_congr _ (I.restrict â»Â¹' s) rfl hs]

instance : IsProbabilityMeasure (productMeasure Î¼) := by
  constructor
  rw [â† cylinder_univ âˆ…, cylinder, â† Measure.map_apply, isProjectiveLimit_productMeasure Î¼]
  Â· simp
  Â· exact measurable_restrict _
  Â· exact MeasurableSet.univ

theorem productMeasure_boxes {s : Finset Î¹} {t : (i : Î¹) â†’ Set (X i)}
    (mt : âˆ€ i âˆˆ s, MeasurableSet (t i)) :
    productMeasure Î¼ (Set.pi s t) = âˆ i âˆˆ s, (Î¼ i) (t i) := by
  have : Set.pi s t = cylinder s ((@Set.univ s).pi (fun i : s â†¦ t i)) := by
    ext x
    simp
  rw [this, cylinder, â† Measure.map_apply, isProjectiveLimit_productMeasure Î¼,
    Measure.pi_pi]
  Â· rw [Finset.univ_eq_attach, Finset.prod_attach _ (fun i â†¦ (Î¼ i) (t i))]
  Â· exact measurable_restrict _
  Â· exact MeasurableSet.pi Set.countable_univ fun i _ â†¦ mt i.1 i.2

theorem productMeasure_cylinder {s : Finset Î¹} {S : Set ((i : s) â†’ X i)} (mS : MeasurableSet S) :
    productMeasure Î¼ (cylinder s S) = Measure.pi (fun i : s â†¦ Î¼ i) S := by
  rw [cylinder, â† Measure.map_apply (measurable_restrict _) mS, isProjectiveLimit_productMeasure Î¼]

theorem integral_dep_productMeasure {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {s : Finset Î¹} {f : ((i : s) â†’ X i) â†’ E} (hf : StronglyMeasurable f) :
    âˆ« y, f (s.restrict y) âˆ‚productMeasure Î¼ =
    âˆ« y, f y âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by

  rw [â† integral_map (measurable_restrict _).aemeasurable hf.aestronglyMeasurable,
    isProjectiveLimit_productMeasure Î¼]

/-- The canonical filtration on dependent functions indexed by Î¹, where `ğ“• s` consists of
measurable sets depending only on coordinates is `s`. -/
def â„± : @Filtration ((i : Î¹) â†’ X i) (Finset Î¹) _ inferInstance where
  seq s := (inferInstance : MeasurableSpace ((i : s) â†’ X i)).comap s.restrict
  mono' s t hst := by
    simp only
    rw [â† restrictâ‚‚_comp_restrict hst, â† comap_comp]
    exact MeasurableSpace.comap_mono (measurable_restrictâ‚‚ _).comap_le
  le' s := (measurable_restrict s).comap_le

/-- If a function is strongly measurable with respect to the Ïƒ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem stronglyMeasurable_dependsOn' {E : Type*} [NormedAddCommGroup E]
    {s : Finset Î¹} {f : ((i : Î¹) â†’ X i) â†’ E}
    (mf : @StronglyMeasurable _ _ _ (â„± s) f) : DependsOn f s := by
  intro x y hxy
  apply eq_of_stronglyMeasurable_comap s.restrict mf
  exact dependsOn_restrict s hxy

theorem integral_stronglyMeasurable [DecidableEq Î¹] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {s : Finset Î¹} {f : ((i : Î¹) â†’ X i) â†’ E}
    (mf : @StronglyMeasurable _ _ _ (â„± s) f) (x : (i : Î¹) â†’ X i) :
    âˆ« y, f y âˆ‚productMeasure Î¼ =
    âˆ« y, f (Function.updateFinset x s y) âˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  let g : ((i : s) â†’ X i) â†’ E := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    apply stronglyMeasurable_dependsOn' mf
    intro i hi
    simp only [updateFinset, restrict, dite_eq_ite, ite_eq_then]
    exact fun h â†¦ (h hi).elim
  rw [â† integral_congr_ae <| Eventually.of_forall this, integral_dep_productMeasure]
  exact mf.comp_measurable (measurable_updateFinset.mono (_root_.le_refl _) (â„±.le s))

theorem lintegral_dep {s : Finset Î¹} {f : ((i : s) â†’ X i) â†’ â„â‰¥0âˆ} (hf : Measurable f) :
    âˆ«â» y, f (s.restrict y) âˆ‚productMeasure Î¼ =
    âˆ«â» y, f yâˆ‚Measure.pi (fun i : s â†¦ Î¼ i) := by
  rw [â† lintegral_map hf (measurable_restrict _), isProjectiveLimit_productMeasure Î¼]

/-- If a function is measurable with respect to the Ïƒ-algebra generated by
the finite set of coordinates `s`, then it only depends on those coordinates. -/
theorem measurable_dependsOn' {s : Finset Î¹} {f : ((i : Î¹) â†’ X i) â†’ â„â‰¥0âˆ}
    (mf : @Measurable _ _ (â„± s) _ f) : DependsOn f s := by
  intro x y hxy
  apply eq_of_measurable_comap s.restrict mf
  exact dependsOn_restrict s hxy

theorem lintegral_measurable [DecidableEq Î¹] {s : Finset Î¹}
    {f : ((i : Î¹) â†’ X i) â†’ â„â‰¥0âˆ} (mf : @Measurable _ _ (â„± s) _ f)
    (x : (i : Î¹) â†’ X i) : âˆ«â» y, f y âˆ‚productMeasure Î¼ = (âˆ«â‹¯âˆ«â»_s, f âˆ‚Î¼) x := by
  let g : ((i : s) â†’ X i) â†’ â„â‰¥0âˆ := fun y â†¦ f (Function.updateFinset x _ y)
  have this y : g (s.restrict y) = f y := by
    refine measurable_dependsOn' mf fun i hi â†¦ ?_
    simp only [updateFinset, restrict, dite_eq_ite, ite_eq_then]
    exact fun h â†¦ (h hi).elim
  simp_rw [â† this]
  rw [lintegral_dep]
  Â· rfl
  Â· exact mf.comp (measurable_updateFinset.mono (_root_.le_refl _) (â„±.le s))

end ProductMeasure
