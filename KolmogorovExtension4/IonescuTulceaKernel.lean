/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import KolmogorovExtension4.compProdNat
import KolmogorovExtension4.DependsOn
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import KolmogorovExtension4.KolmogorovExtension
import Batteries.Data.Nat.Lemmas

open MeasureTheory ProbabilityTheory Finset ENNReal Filter Topology Function MeasurableSpace Preorder

section castLemmas

variable {X : â„• â†’ Type*}

private lemma Iic_pi_eq {a b : â„•} (h : a = b) :
    ((i : Iic a) â†’ X i) = ((i : Iic b) â†’ X i) := by cases h; rfl

private lemma cast_pi {s t : Set â„•} (h : s = t) (x : (i : s) â†’ X i) (i : t) :
    cast (congrArg (fun u : Set â„• â†¦ (Î  i : u, X i)) h) x i = x âŸ¨i.1, h.symm â–¸ i.2âŸ© := by
  subst h
  rfl

/-- This function takes a trajectory up to time `p` and a way of building the next step of the
trajectory and returns a whole trajectory whose first steps correspond
to the initial ones provided. -/
def iterate_induction {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    (ind : (k : â„•) â†’ ((n : Iic k) â†’ X n) â†’ X (k + 1)) : (k : â„•) â†’ X k
  | 0 => xâ‚€ âŸ¨0, mem_Iic.2 <| zero_le pâŸ©
  | q + 1 =>
    if hq : q + 1 â‰¤ p
      then xâ‚€ âŸ¨q + 1, mem_Iic.2 hqâŸ©
      else ind q (fun i â†¦ iterate_induction xâ‚€ ind i)
  decreasing_by exact Nat.lt_succ.2 (mem_Iic.1 i.2)

theorem iterate_induction_le {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    (ind : (k : â„•) â†’ ((n : Iic k) â†’ X n) â†’ X (k + 1)) (k : Iic p) :
    iterate_induction xâ‚€ ind k = xâ‚€ k := by
  obtain âŸ¨(zero | j), hiâŸ© := k
  Â· rw [iterate_induction]
  Â· rw [iterate_induction]
    simp [mem_Iic.1 hi]

lemma frestrictLe_iterate_induction {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    (ind : (k : â„•) â†’ ((n : Iic k) â†’ X n) â†’ X (k + 1)) :
    frestrictLe p (iterate_induction xâ‚€ ind) = xâ‚€ := by
  ext i
  simp [iterate_induction_le]

variable [âˆ€ n, MeasurableSpace (X n)]

private lemma measure_cast {a b : â„•} (h : a = b) (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) :
    (Î¼ a).map (cast (Iic_pi_eq h)) = Î¼ b := by
  subst h
  exact Measure.map_id

private lemma heq_measurableSpace_Iic_pi {a b : â„•} (h : a = b) :
    HEq (inferInstance : MeasurableSpace ((i : Iic a) â†’ X i))
    (inferInstance : MeasurableSpace ((i : Iic b) â†’ X i)) := by cases h; rfl

end castLemmas

section ProjectiveFamily

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]

/-- To check that a measure `Î½` is the projective limit of a projective family of measures indexed
by `Finset â„•`, it is enough to check on intervals of the form `Iic n`, where `n` is larger than
a given integer. -/
theorem isProjectiveLimit_nat_iff' (Î¼ : (I : Finset â„•) â†’ Measure ((i : I) â†’ X i))
    (hÎ¼ : IsProjectiveMeasureFamily Î¼) (Î½ : Measure ((n : â„•) â†’ X n)) (a : â„•) :
    IsProjectiveLimit Î½ Î¼ â†” âˆ€ â¦ƒnâ¦„, n â‰¥ a â†’ Î½.map (frestrictLe n) = Î¼ (Iic n) := by
  refine âŸ¨fun h n _ â†¦ h (Iic n), fun h I â†¦ ?_âŸ©
  have := (I.sub_Iic.trans (Iic_subset_Iic.2 (le_max_left (I.sup id) a)))
  rw [â† restrictâ‚‚_comp_restrict this, â† Measure.map_map, â† frestrictLe, h (le_max_right _ _), â† hÎ¼]
  any_goals fun_prop

/-- To check that a measure `Î½` is the projective limit of a projective family of measures indexed
by `Finset â„•`, it is enough to check on intervals of the form `Iic n`. -/
theorem isProjectiveLimit_nat_iff (Î¼ : (I : Finset â„•) â†’ Measure ((i : I) â†’ X i))
    (hÎ¼ : IsProjectiveMeasureFamily Î¼) (Î½ : Measure ((n : â„•) â†’ X n)) :
    IsProjectiveLimit Î½ Î¼ â†” âˆ€ n, Î½.map (frestrictLe n) = Î¼ (Iic n) := by
  rw [isProjectiveLimit_nat_iff' _ hÎ¼ _ 0]
  simp

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)`, we can define a family
of measures indexed by `Finset â„•` by projecting the measures. -/
noncomputable def inducedFamily (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) :
    (S : Finset â„•) â†’ Measure ((k : S) â†’ X k) :=
  fun S â†¦ (Î¼ (S.sup id)).map (restrictâ‚‚ S.sub_Iic)

instance (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) [âˆ€ n, IsFiniteMeasure (Î¼ n)] (I : Finset â„•) :
    IsFiniteMeasure (inducedFamily Î¼ I) := by
  rw [inducedFamily]
  infer_instance

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)`, the induced family
equals `Î¼` over the intervals `Iic n`. -/
theorem inducedFamily_Iic (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) (n : â„•) :
    inducedFamily Î¼ (Iic n) = Î¼ n := by
  rw [inducedFamily, â† measure_cast (sup_Iic n) Î¼]
  congr with x i
  rw [restrictâ‚‚, cast_pi (by rw [sup_Iic n])]

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)`, the induced family
will be projective only if `Î¼` is projective, in the sense that if `a â‰¤ b`, then projecting
`Î¼ b` gives `Î¼ a`. -/
theorem isProjectiveMeasureFamily_inducedFamily (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i))
    (h : âˆ€ a b : â„•, âˆ€ hab : a â‰¤ b, (Î¼ b).map (frestrictLeâ‚‚ hab) = Î¼ a) :
    IsProjectiveMeasureFamily (inducedFamily Î¼) := by
  intro I J hJI
  have sls : J.sup id â‰¤ I.sup id := sup_mono hJI
  simp only [inducedFamily]
  rw [Measure.map_map, restrictâ‚‚_comp_restrictâ‚‚,
    â† restrictâ‚‚_comp_restrictâ‚‚ J.sub_Iic (Iic_subset_Iic.2 sls), â† Measure.map_map, â† frestrictLeâ‚‚,
    h (J.sup id) (I.sup id) sls]
  any_goals fun_prop

end ProjectiveFamily

open Kernel

section definition

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]
  (Îº : (k : â„•) â†’ Kernel (Î  i : Iic k, X i) (X (k + 1)))
  [âˆ€ k, IsMarkovKernel (Îº k)]

/-- Given a family of kernels `Îº : (n : â„•) â†’ Kernel (Î  i : Iic k, X i) (X (n + 1))`, and the
trajectory up to time `n` we can construct an additive content over cylinders. It corresponds
to composing the kernels by starting at time `n + 1`. -/
noncomputable def trajContent {n : â„•} (xâ‚€ : (i : Iic n) â†’ X i) :
    AddContent (measurableCylinders X) :=
  kolContent (isProjectiveMeasureFamily_inducedFamily _ (fun _ _ â†¦ ptraj_map_frestrictLeâ‚‚_apply' Îº xâ‚€))

/-- The `trajContent Îº xâ‚€` of a cylinder indexed by first coordinates is given by
`ptraj`. -/
theorem trajContent_cylinder {a b : â„•} (xâ‚€ : (i : Iic a) â†’ X i)
    {S : Set ((i : Iic b) â†’ X i)} (mS : MeasurableSet S) :
    trajContent Îº xâ‚€ (cylinder _ S) = ptraj Îº a b xâ‚€ S := by
  rw [trajContent, kolContent_cylinder _ mS, inducedFamily_Iic]

/-- The `trajContent` of a cylinder is equal to the integral of its indicator function. -/
theorem trajContent_eq_lmarginalPTraj {N : â„•} {S : Set ((i : Iic N) â†’ X i)}
    (mS : MeasurableSet S) (xâ‚€ : (n : â„•) â†’ X n) (n : â„•) :
    trajContent Îº (frestrictLe n xâ‚€) (cylinder _ S) =
      lmarginalPTraj Îº n N ((cylinder _ S).indicator 1) xâ‚€ := by
  rw [trajContent_cylinder _ _ mS, â† lintegral_indicator_one mS, lmarginalPTraj]
  congr with x
  apply indicator_const_eq
  rw [mem_cylinder]
  congrm (fun i â†¦ ?_) âˆˆ S
  simp [updateFinset, i.2]

/-- The cylinders of a product space indexed by `â„•` can be seen as depending on the first
corrdinates. -/
theorem cylinders_nat :
    measurableCylinders X = â‹ƒ (N) (S) (_ : MeasurableSet S), {cylinder (Iic N) S} := by
  ext s
  simp only [mem_measurableCylinders, exists_prop, Set.mem_iUnion, mem_singleton]
  refine âŸ¨?_, fun âŸ¨N, S, mS, s_eqâŸ© â†¦ âŸ¨Iic N, S, mS, s_eqâŸ©âŸ©
  rintro âŸ¨t, S, mS, rflâŸ©
  refine âŸ¨t.sup id, restrictâ‚‚ t.sub_Iic â»Â¹' S, measurable_restrictâ‚‚ _ mS, ?_âŸ©
  unfold cylinder
  rw [â† Set.preimage_comp, restrictâ‚‚_comp_restrict]
  exact Set.mem_singleton _

variable [Nonempty (X 0)]

/-- This is an auxiliary result for `trajContent_tendsto_zero`.
Consider `f` a sequence of bounded measurable
functions such that `f n` depends only on the first coordinates up to `N n`.
Assume that when integrating `f n` against `ptraj (k + 1) (N n)`,
one gets a non-increasing sequence of functions wich converges to `l`.
Assume then that there exists `Îµ` and `y : (n : Iic k) â†’ X n` such that
when integrating `f n` against `ptraj k (N n) y`, you get something at least
`Îµ` for all `n`. Then there exists `z` such that this remains true when integrating
`f` against `ptraj (k + 1) (N n) (update y (k + 1) z)`. -/
theorem le_lmarginalPTraj_succ {f : â„• â†’ ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ} {N : â„• â†’ â„•}
    (hcte : âˆ€ n, DependsOn (f n) (Iic (N n))) (mf : âˆ€ n, Measurable (f n))
    {bound : â„â‰¥0âˆ} (fin_bound : bound â‰  âˆ) (le_bound : âˆ€ n x, f n x â‰¤ bound) {k : â„•}
    (anti : âˆ€ x, Antitone (fun n â†¦ lmarginalPTraj Îº (k + 1) (N n) (f n) x))
    {l : ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ}
    (htendsto : âˆ€ x, Tendsto (fun n â†¦ lmarginalPTraj Îº (k + 1) (N n) (f n) x)
      atTop (ğ“ (l x)))
    (Îµ : â„â‰¥0âˆ) (y : (n : Iic k) â†’ X n)
    (hpos : âˆ€ x n, Îµ â‰¤ lmarginalPTraj Îº k (N n) (f n) (updateFinset x _ y)) :
    âˆƒ z, âˆ€ x n, Îµ â‰¤ lmarginalPTraj Îº (k + 1) (N n) (f n)
      (update (updateFinset x _ y) (k + 1) z) := by
  have _ n : Nonempty (X n) := by
    refine Nat.case_strong_induction_on (p := fun n â†¦ Nonempty (X n)) _ inferInstance
      fun n hind â†¦ ?_
    have : Nonempty ((i : Iic n) â†’ X i) :=
      Nonempty.intro fun i â†¦ @Classical.ofNonempty _ (hind i.1 (mem_Iic.1 i.2))
    exact ProbabilityMeasure.nonempty âŸ¨Îº n Classical.ofNonempty, inferInstanceâŸ©
  let F : â„• â†’ ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ := fun n â†¦ lmarginalPTraj Îº (k + 1) (N n) (f n)
  -- `Fâ‚™` converges to `l` by hypothesis.
  have tendstoF x : Tendsto (F Â· x) atTop (ğ“ (l x)) := htendsto x
  -- Integrating `fâ‚™` between time `k` and `Nâ‚™` is the same as integrating
  -- `Fâ‚™` between time `k` and time `k + 1` variable.
  have f_eq x n : lmarginalPTraj Îº k (N n) (f n) x =
    lmarginalPTraj Îº k (k + 1) (F n) x := by
    simp_rw [F]
    rcases lt_trichotomy (k + 1) (N n) with h | h | h
    Â· rw [â† lmarginalPTraj_self k.le_succ h.le (mf n)]
    Â· rw [â† h, lmarginalPTraj_le _ (_root_.le_refl (k + 1)) (mf n)]
    Â· rw [lmarginalPTraj_le _ (by omega) (mf n),
        (hcte n).lmarginalPTraj_eq _ (mf n) (by omega),
        (hcte n).lmarginalPTraj_eq _ (mf n) (by omega)]
  -- `F` is also a bounded sequence.
  have F_le n x : F n x â‰¤ bound := by
    simp_rw [F, lmarginalPTraj]
    rw [â† mul_one bound, â† measure_univ (Î¼ := ptraj Îº (k + 1) (N n) (frestrictLe (k + 1) x)),
        â† MeasureTheory.lintegral_const]
    exact lintegral_mono fun _ â†¦ le_bound _ _
  -- By dominated convergence, the integral of `fâ‚™` between time `k` and time `N n` converges
  -- to the integral of `l` between time `k` and time `k + 1`.
  have tendsto_int x : Tendsto (fun n â†¦ lmarginalPTraj Îº k (N n) (f n) x) atTop
      (ğ“ (lmarginalPTraj Îº k (k + 1) l x)) := by
    simp_rw [f_eq, lmarginalPTraj]
    exact tendsto_lintegral_of_dominated_convergence (fun _ â†¦ bound)
      (fun n â†¦ (measurable_lmarginalPTraj _ _ (mf n)).comp measurable_updateFinset)
      (fun n â†¦ Eventually.of_forall <| fun y â†¦ F_le n _)
      (by simp [fin_bound]) (Eventually.of_forall (fun _ â†¦ tendstoF _))
  -- By hypothesis, we have `Îµ â‰¤ lmarginalPTraj Îº k (k + 1) (F n) (updateFinset x _ y)`,
  -- so this is also true for `l`.
  have Îµ_le_lint x : Îµ â‰¤ lmarginalPTraj Îº k (k + 1) l (updateFinset x _ y) :=
    ge_of_tendsto (tendsto_int _) (by simp [hpos])
  let x_ : (n : â„•) â†’ X n := Classical.ofNonempty
  -- We now have that the integral of `l` with respect to a probability measure is greater than `Îµ`,
  -- therefore there exists `x` such that `Îµ â‰¤ l(y, x)`.
  obtain âŸ¨x, hxâŸ© : âˆƒ x, Îµ â‰¤ l (update (updateFinset x_ _ y) (k + 1) x) := by
    have : âˆ«â» x, l (update (updateFinset x_ _ y) (k + 1) x) âˆ‚(Îº k y) â‰  âˆ :=
      ne_top_of_le_ne_top fin_bound <| lintegral_le_mul' _
        fun y â†¦ le_of_tendsto' (tendstoF _) <| fun _ â†¦ F_le _ _
    obtain âŸ¨x, hxâŸ© := exists_lintegral_le this
    refine âŸ¨x, (Îµ_le_lint x_).trans ?_âŸ©
    rwa [lmarginalPTraj_succ, frestrictLe_updateFinset]
    refine ENNReal.measurable_of_tendsto ?_ (tendsto_pi_nhds.2 htendsto)
    exact fun n â†¦ measurable_lmarginalPTraj _ _ (mf n)
  refine âŸ¨x, fun x' n â†¦ ?_âŸ©
  -- As `F` is a non-increasing sequence, we have `Îµ â‰¤ Fâ‚™(y, x')` for any `n`.
  have := le_trans hx ((anti _).le_of_tendsto (tendstoF _) n)
  -- This part below is just to say that this is true for any `x : (i : Î¹) â†’ X i`,
  -- as `Fâ‚™` technically depends on all the variables, but really depends only on the first `k + 1`.
  convert this using 1
  refine (hcte n).dependsOn_lmarginalPTraj _ (mf n) fun i hi â†¦ ?_
  simp only [update, updateFinset, mem_Iic, F]
  split_ifs with h1 h2 <;> try rfl
  rw [mem_coe, mem_Iic] at hi
  omega

/-- The indicator of a cylinder only depends on the variables whose the cylinder depends on. -/
theorem dependsOn_cylinder_indicator {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {I : Finset Î¹}
    (S : Set ((i : I) â†’ Î± i)) :
    DependsOn ((cylinder I S).indicator (1 : ((i : Î¹) â†’ Î± i) â†’ â„â‰¥0âˆ)) I :=
  fun x y hxy â†¦ indicator_const_eq _ (by simp [restrict_def, hxy])

/-- This is the key theorem to prove the existence of the `trajKernel`:
the `trajContent` of a decresaing sequence of cylinders with empty intersection
converges to `0`.
This implies the `Ïƒ`-additivity of `trajContent`
(see `sigma_additive_addContent_of_tendsto_zero`), which allows to extend it to the
`Ïƒ`-algebra by CarathÃ©odory's theorem. -/
theorem trajContent_tendsto_zero (A : â„• â†’ Set (Î  n : â„•, X n))
    (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X) (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…)
    {p : â„•} (xâ‚€ : Î  i : Iic p, X i) :
    Tendsto (fun n â†¦ trajContent Îº xâ‚€ (A n)) atTop (ğ“ 0) := by
  have _ n : Nonempty (X n) := by
    refine Nat.case_strong_induction_on (p := fun n â†¦ Nonempty (X n)) _ inferInstance
      fun n hind â†¦ ?_
    have : Nonempty ((i : Iic n) â†’ X i) :=
      Nonempty.intro fun i â†¦ @Classical.ofNonempty _ (hind i.1 (mem_Iic.1 i.2))
    exact ProbabilityMeasure.nonempty âŸ¨Îº n Classical.ofNonempty, inferInstanceâŸ©
  -- `Aâ‚™` is a cylinder, it can be written `cylinder (Iic (N n)) Sâ‚™`.
  have A_cyl n : âˆƒ N S, MeasurableSet S âˆ§ A n = cylinder (Iic N) S := by
    simpa [cylinders_nat] using A_mem n
  choose N S mS A_eq using A_cyl
  -- We write `Ï‡â‚™` for the indicator function of `Aâ‚™`.
  let Ï‡ n := (A n).indicator (1 : ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ)
  -- `Ï‡â‚™` is measurable.
  have mÏ‡ n : Measurable (Ï‡ n) := by
    simp_rw [Ï‡, A_eq]
    exact (measurable_indicator_const_iff 1).2 <| (mS n).cylinder
  -- `Ï‡â‚™` only depends on the first coordinates.
  have Ï‡_dep n : DependsOn (Ï‡ n) (Iic (N n)) := by
    simp_rw [Ï‡, A_eq]
    exact dependsOn_cylinder_indicator _
  -- Therefore its integral against `ptraj Îº k (N n)` is constant.
  have lma_const x y n :
      lmarginalPTraj Îº p (N n) (Ï‡ n) (updateFinset x _ xâ‚€) =
      lmarginalPTraj Îº p (N n) (Ï‡ n) (updateFinset y _ xâ‚€) := by
    refine (Ï‡_dep n).dependsOn_lmarginalPTraj p (mÏ‡ n) fun i hi â†¦ ?_
    rw [mem_coe, mem_Iic] at hi
    simp [updateFinset, hi]
  -- As `(Aâ‚™)` is non-increasing, so is `(Ï‡â‚™)`.
  have Ï‡_anti : Antitone Ï‡ := by
    refine fun m n hmn y â†¦ ?_
    apply Set.indicator_le fun a ha â†¦ ?_
    simp [Ï‡, A_anti hmn ha]
  -- Integrating `Ï‡â‚™` further than the last coordinate it depends on does nothing.
  -- This is used to then show that the integral of `Ï‡â‚™` from time `k` is non-increasing.
  have lma_inv k M n (h : N n â‰¤ M) :
      lmarginalPTraj Îº k M (Ï‡ n) = lmarginalPTraj Îº k (N n) (Ï‡ n) :=
    (Ï‡_dep n).lmarginalPTraj_right k (mÏ‡ n) h (_root_.le_refl _)
  -- the integral of `Ï‡â‚™` from time `k` is non-increasing.
  have anti_lma k x : Antitone fun n â†¦ lmarginalPTraj Îº k (N n) (Ï‡ n) x := by
    intro m n hmn
    simp only
    rw [â† lma_inv k ((N n).max (N m)) n (le_max_left _ _),
      â† lma_inv k ((N n).max (N m)) m (le_max_right _ _)]
    exact lmarginalPTraj_mono _ _ (Ï‡_anti hmn) _
  -- Therefore it converges to some function `lâ‚–`.
  have this k x : âˆƒ l,
      Tendsto (fun n â†¦ lmarginalPTraj Îº k (N n) (Ï‡ n) x) atTop (ğ“ l) := by
    rcases tendsto_of_antitone <| anti_lma k x with h | h
    Â· rw [OrderBot.atBot_eq] at h
      exact âŸ¨0, h.mono_right <| pure_le_nhds 0âŸ©
    Â· exact h
  choose l hl using this
  -- `lâ‚š` is constant because it is the limit of constant functions: we call it `Îµ`.
  have l_const x y : l p (updateFinset x _ xâ‚€) = l p (updateFinset y _ xâ‚€) := by
    have := hl p (updateFinset x _ xâ‚€)
    simp_rw [lma_const x y] at this
    exact tendsto_nhds_unique this (hl p _)
  obtain âŸ¨Îµ, hÎµâŸ© : âˆƒ Îµ, âˆ€ x, l p (updateFinset x _ xâ‚€) = Îµ :=
      âŸ¨l p (updateFinset Classical.ofNonempty _ xâ‚€), fun x â†¦ l_const _ _âŸ©
  -- As the sequence is decreasing, `Îµ â‰¤ âˆ« Ï‡â‚™`.
  have hpos x n : Îµ â‰¤ lmarginalPTraj Îº p (N n) (Ï‡ n) (updateFinset x _ xâ‚€) :=
    hÎµ x â–¸ ((anti_lma p _).le_of_tendsto (hl p _)) n
  -- Also, the indicators are bounded by `1`.
  have Ï‡_le n x : Ï‡ n x â‰¤ 1 := by
    apply Set.indicator_le
    simp
  -- We have all the conditions to apply `le_lmarginalPTraj_succ`.
  -- This allows us to recursively build a sequence `z` with the following property:
  -- for any `k â‰¥ p` and `n`, integrating `Ï‡ n` from time `k` to time `N n`
  -- with the trajectory up to `k` being equal to `z` gives something greater than `Îµ`.
  choose! ind hind using
    fun k y h â†¦ le_lmarginalPTraj_succ Îº Ï‡_dep mÏ‡ (by norm_num : (1 : â„â‰¥0âˆ) â‰  âˆ)
      Ï‡_le (anti_lma (k + 1)) (hl (k + 1)) Îµ y h
  let z := iterate_induction xâ‚€ ind
  have main k (hk : p â‰¤ k) : âˆ€ x n,
      Îµ â‰¤ lmarginalPTraj Îº k (N n) (Ï‡ n) (updateFinset x (Iic k) (frestrictLe k z)) := by
    refine Nat.le_induction (fun x n â†¦ ?_) (fun k hn h x n â†¦ ?_) k hk
    Â· rw [frestrictLe_iterate_induction]
      exact hpos x n
    Â· rw [â† update_updateFinset_eq]
      convert hind k (fun i â†¦ z i.1) h x n
      simp_rw [z, iterate_induction]
      simp [Nat.lt_succ.2 hn]
  -- We now want to prove that the integral of `Ï‡â‚™`, which is equal to the `trajContent`
  -- of `Aâ‚™`, converges to `0`.
  have aux x n : trajContent Îº xâ‚€ (A n) =
      lmarginalPTraj Îº p (N n) (Ï‡ n) (updateFinset x _ xâ‚€) := by
    simp_rw [Ï‡, A_eq]
    nth_rw 1 [â† frestrictLe_updateFinset x xâ‚€]
    exact trajContent_eq_lmarginalPTraj Îº (mS n) (updateFinset x _ xâ‚€) p
  simp_rw [aux Classical.ofNonempty]
  convert hl p (updateFinset Classical.ofNonempty _ xâ‚€)
  rw [hÎµ]
  by_contra!
  -- Which means that we want to prove that `Îµ = 0`. But if `Îµ > 0`, then for any `n`,
  -- choosing `k > Nâ‚™` we get `Îµ â‰¤ Ï‡â‚™(zâ‚€, ..., z_{Nâ‚™})` and therefore `z âˆˆ Aâ‚™`.
  -- This contradicts the fact that `(Aâ‚™)` has an empty intersection.
  have Îµ_pos : 0 < Îµ := this.symm.bot_lt
  have mem n : z âˆˆ A n := by
    have : 0 < Ï‡ n (z) := by
      rw [â† lmarginalPTraj_le Îº (le_max_right p (N n)) (mÏ‡ n),
        â† updateFinset_frestrictLe (i := N n) z]
      simpa using lt_of_lt_of_le Îµ_pos (main _ (le_max_left _ _) z n)
    exact Set.mem_of_indicator_ne_zero (ne_of_lt this).symm
  exact (A_inter â–¸ Set.mem_iInter.2 mem).elim

/-- The `trajContent` is sigma-subadditive. -/
theorem trajContent_sigma_subadditive {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    â¦ƒf : â„• â†’ Set ((n : â„•) â†’ X n)â¦„ (hf : âˆ€ n, f n âˆˆ measurableCylinders X)
    (hf_Union : (â‹ƒ n, f n) âˆˆ measurableCylinders X) :
    trajContent Îº xâ‚€ (â‹ƒ n, f n) â‰¤ âˆ‘' n, trajContent Îº xâ‚€ (f n) := by
  have _ n : Nonempty (X n) := by
    refine Nat.case_strong_induction_on (p := fun n â†¦ Nonempty (X n)) _ inferInstance
      fun n hind â†¦ ?_
    have : Nonempty ((i : Iic n) â†’ X i) :=
      Nonempty.intro fun i â†¦ @Classical.ofNonempty _ (hind i.1 (mem_Iic.1 i.2))
    exact ProbabilityMeasure.nonempty âŸ¨Îº n Classical.ofNonempty, inferInstanceâŸ©
  refine addContent_iUnion_le_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' â†¦ ?_) f hf hf_Union
  refine addContent_iUnion_eq_sum_of_tendsto_zero isSetRing_measurableCylinders
    (trajContent Îº xâ‚€) (fun s hs â†¦ ?_) ?_ hf hf_Union hf'
  Â· obtain âŸ¨N, S, mS, s_eqâŸ© : âˆƒ N S, MeasurableSet S âˆ§ s = cylinder (Iic N) S := by
      simpa [cylinders_nat] using hs
    let x_ : (n : â„•) â†’ X n := Classical.ofNonempty
    rw [s_eq, â† frestrictLe_updateFinset x_ xâ‚€,
      trajContent_eq_lmarginalPTraj Îº mS (updateFinset x_ _ xâ‚€)]
    refine ne_of_lt <| lt_of_le_of_lt (lintegral_le_mul' _ (Set.indicator_le (by simp)))
      (by norm_num : (1 : â„â‰¥0âˆ) < âˆ)
  Â· exact fun s hs anti_s inter_s â†¦ trajContent_tendsto_zero Îº s hs anti_s inter_s xâ‚€

/-- This function is the kernel given by the Ionescu-Tulcea theorem. -/
noncomputable def trajFun (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    Measure ((n : â„•) â†’ X n) :=
  Measure.ofAddContent isSetSemiring_measurableCylinders generateFrom_measurableCylinders
    (trajContent Îº xâ‚€) (trajContent_sigma_subadditive Îº xâ‚€)

theorem isProbabilityMeasure_trajFun (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    IsProbabilityMeasure (trajFun Îº p xâ‚€) where
  measure_univ := by
    rw [â† cylinder_univ (Iic 0), trajFun, Measure.ofAddContent_eq,
      trajContent_cylinder _ _ MeasurableSet.univ]
    Â· exact measure_univ
    Â· exact (mem_measurableCylinders _).2 âŸ¨Iic 0, Set.univ, MeasurableSet.univ, rflâŸ©

theorem isProjectiveLimit_trajFun (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    IsProjectiveLimit (trajFun Îº p xâ‚€) (inducedFamily (fun n â†¦ ptraj Îº p n xâ‚€)) := by
  refine isProjectiveLimit_nat_iff _
    (isProjectiveMeasureFamily_inducedFamily _ (fun _ _ â†¦ ptraj_map_frestrictLeâ‚‚_apply' Îº xâ‚€)) _ |>.2 fun n â†¦ ?_
  ext s ms
  rw [Measure.map_apply (measurable_frestrictLe n) ms]
  have h_mem : (frestrictLe n) â»Â¹' s âˆˆ measurableCylinders X :=
    (mem_measurableCylinders _).2 âŸ¨Iic n, s, ms, rflâŸ©
  rw [trajFun, Measure.ofAddContent_eq _ _ _ _ h_mem, trajContent,
    kolContent_congr _ (frestrictLe n â»Â¹' s) rfl ms]

theorem measurable_trajFun (p : â„•) : Measurable (trajFun Îº p) := by
  apply Measure.measurable_of_measurable_coe
  refine MeasurableSpace.induction_on_inter
    (C := fun t ht â†¦ Measurable (fun xâ‚€ â†¦ trajFun Îº p xâ‚€ t))
    (s := measurableCylinders X) generateFrom_measurableCylinders.symm
    isPiSystem_measurableCylinders (by simp) (fun t ht â†¦ ?cylinder) (fun t mt ht â†¦ ?compl)
    (fun f disf mf hf â†¦ ?union)
  Â· obtain âŸ¨N, S, mS, t_eqâŸ© : âˆƒ N S, MeasurableSet S âˆ§ t = cylinder (Iic N) S := by
      simpa [cylinders_nat] using ht
    simp_rw [trajFun, Measure.ofAddContent_eq _ _ _ _ ht, trajContent,
      kolContent_congr _ t t_eq mS, inducedFamily]
    refine Measure.measurable_measure.1 ?_ _ mS
    exact (Measure.measurable_map _ (measurable_restrictâ‚‚ _)).comp (measurable _)
  Â· have := isProbabilityMeasure_trajFun Îº p
    simp_rw [measure_compl mt (measure_ne_top _ _), measure_univ]
    exact Measurable.const_sub ht _
  Â· simp_rw [measure_iUnion disf mf]
    exact Measurable.ennreal_tsum hf

/-- *Ionescu-Tulcea Theorem* : Given a family of kernels `Îº k` taking variables in `Iic k` with
value in `X (k+1)`, the kernel `trajKernel Îº p` takes a variable `x` depending on the
variables `i â‰¤ p` and associates to it a kernel on trajectories depending on all variables,
where the entries with index `â‰¤ p` are those of `x`, and then one follows iteratively the
kernels `Îº p`, then `Îº (p+1)`, and so on.

The fact that such a kernel exists on infinite trajectories is not obvious, and is the content of
the Ionescu-Tulcea theorem. -/
noncomputable def trajKernel (p : â„•) : Kernel ((i : Iic p) â†’ X i) ((n : â„•) â†’ X n) where
  toFun := trajFun Îº p
  measurable' := measurable_trajFun Îº p

theorem trajKernel_apply (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    trajKernel Îº p xâ‚€ = trajFun Îº p xâ‚€ := rfl

instance (p : â„•) : IsMarkovKernel (trajKernel Îº p) :=
  âŸ¨fun _ â†¦ isProbabilityMeasure_trajFun ..âŸ©

theorem frestrictLe_trajKernel (a b : â„•) :
    (trajKernel Îº a).map (frestrictLe b) = ptraj Îº a b := by
  ext1 xâ‚€
  rw [map_apply _ (measurable_frestrictLe _), trajKernel_apply, frestrictLe,
    isProjectiveLimit_trajFun, inducedFamily_Iic]

theorem frestrictLe_trajKernel_le {a b : â„•} (hab : a â‰¤ b) :
    (trajKernel Îº b).map (frestrictLe a) =
      deterministic (frestrictLeâ‚‚ hab) (measurable_frestrictLeâ‚‚ _) := by
  rw [frestrictLe_trajKernel, ptraj_le]

theorem eq_trajKernel' {a : â„•} (n : â„•) (Î· : Kernel ((i : Iic a) â†’ X i) ((n : â„•) â†’ X n))
    (hÎ· : âˆ€ b â‰¥ n, Î·.map (frestrictLe b) = ptraj Îº a b) :
    Î· = trajKernel Îº a := by
  ext1 xâ‚€
  refine ((isProjectiveLimit_trajFun _ _ _).unique ?_).symm
  rw [isProjectiveLimit_nat_iff' _ _ _ n]
  Â· intro k hk
    rw [inducedFamily_Iic, â† map_apply _ (measurable_frestrictLe k), hÎ· k hk]
  Â· exact (isProjectiveMeasureFamily_inducedFamily _ (fun _ _ â†¦ ptraj_map_frestrictLeâ‚‚_apply' Îº xâ‚€))

theorem eq_trajKernel {a : â„•} (Î· : Kernel ((i : Iic a) â†’ X i) ((n : â„•) â†’ X n))
    (hÎ· : âˆ€ b, Î·.map (frestrictLe b) = ptraj Îº a b) :
    Î· = trajKernel Îº a := eq_trajKernel' Îº 0 Î· fun b _ â†¦ hÎ· b

theorem trajKernel_comp_ptraj {a b : â„•} (hab : a â‰¤ b) :
    (trajKernel Îº b) âˆ˜â‚– (ptraj Îº a b) = trajKernel Îº a := by
  refine eq_trajKernel _ _ fun n â†¦ ?_
  ext xâ‚€ s ms
  simp_rw [map_apply' _ (measurable_frestrictLe _) _ ms,
    comp_apply' _ _ _ (measurable_frestrictLe n ms),
    â† Measure.map_apply (measurable_frestrictLe n) ms,
    â† map_apply (trajKernel Îº b) (measurable_frestrictLe n), frestrictLe_trajKernel Îº b n,
    â† comp_apply' _ _ _ ms, ptraj_comp_ptraj' n hab]

end definition

section Filtration

variable {Î¹ : Type*} [Preorder Î¹] [LocallyFiniteOrderBot Î¹]
  {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]

/-- The canonical filtration on dependent functions indexed by `â„•`, where `ğ“• n` consists of
measurable sets depending only on coordinates `â‰¤ n`. -/
def Filtration.pi_preorder : @Filtration ((i : Î¹) â†’ X i) Î¹ _ inferInstance where
  seq n := (inferInstance : MeasurableSpace ((i : Iic n) â†’ X i)).comap (frestrictLe n)
  mono' i j hij := by
    simp only
    rw [â† frestrictLeâ‚‚_comp_frestrictLe hij, â† comap_comp]
    exact MeasurableSpace.comap_mono (measurable_frestrictLeâ‚‚ _).comap_le
  le' n := (measurable_frestrictLe n).comap_le

variable {E : Type*} [NormedAddCommGroup E]

/-- If a function is strongly measurable with respect to the Ïƒ-algebra generated by the
first coordinates, then it only depends on those first coordinates. -/
theorem stronglyMeasurable_dependsOn {i : Î¹} {f : ((i : Î¹) â†’ X i) â†’ E}
    (mf : StronglyMeasurable[Filtration.pi_preorder i] f) : DependsOn f (Set.Iic i) :=
  fun _ _ h â†¦ eq_of_stronglyMeasurable_comap _ mf (dependsOn_frestrictLe i h)

end Filtration

open Filtration

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]
variable (Îº : (k : â„•) â†’ Kernel ((i : Iic k) â†’ X i) (X (k + 1)))
variable [âˆ€ k, IsMarkovKernel (Îº k)]

variable [Nonempty (X 0)]

/-- This theorem shows that `trajKernel Îº n` is, up to an equivalence, the product of
a determinstic kernel with another kernel. This is an intermediate result to compute integrals
with respect to this kernel. -/
theorem trajKernel_eq (n : â„•) :
    trajKernel Îº n =
      (Kernel.id Ã—â‚– (trajKernel Îº n).map (Set.Ioi n).restrict).map (IicProdIoi n) := by
  refine (eq_trajKernel' _ (n + 1) _ fun a ha â†¦ ?_).symm
  ext x s ms
  rw [Kernel.map_map, map_apply' _ _ _ ms, id_prod_apply', map_apply']
  Â· have : (frestrictLe a) âˆ˜ (IicProdIoi n) âˆ˜ (Prod.mk x) âˆ˜ (Set.Ioi n).restrict =
        (fun y (i : Iic a) â†¦ if hi : i.1 â‰¤ n then x âŸ¨i.1, mem_Iic.2 hiâŸ© else y i) âˆ˜
          (frestrictLe a) := by
      ext x i
      by_cases hi : i.1 â‰¤ n <;> simp [hi, IicProdIoi]
    have hyp : Measurable (fun (y : (i : Iic a) â†’ X i) (i : Iic a) â†¦
        if hi : i.1 â‰¤ n then x âŸ¨i.1, mem_Iic.2 hiâŸ© else y i) := by
      refine measurable_pi_lambda _ (fun i â†¦ ?_)
      by_cases hi : i.1 â‰¤ n <;> simp only [hi, â†“reduceDIte, measurable_const]
      exact measurable_pi_apply _
    rw [â† Set.preimage_comp, â† Set.preimage_comp, Function.comp_assoc, this,
      â† map_apply' _ _ _ ms, â† map_map _ _ hyp, frestrictLe_trajKernel, map_apply' _ _ _ ms,
      ptraj_eq_prod, map_apply' _ _ _ (hyp ms), id_prod_apply',
      map_apply' _ _ _ ms, id_prod_apply']
    Â· congr with y
      simp only [IicProdIoc_def, Set.mem_preimage]
      congrm (fun i â†¦ ?_) âˆˆ s
      by_cases hi : i.1 â‰¤ n <;> simp [hi]
    any_goals fun_prop
    Â· exact measurable_IicProdIoc ms
    Â· exact measurable_IicProdIoc <| hyp ms
    Â· exact hyp
    Â· exact hyp.comp (measurable_frestrictLe _)
  any_goals fun_prop
  Â· exact measurable_prod_mk_left <| (IicProdIoi n).measurable <| (measurable_frestrictLe a) ms
  Â· exact (IicProdIoi n).measurable <| (measurable_frestrictLe a) ms

@[measurability, fun_prop]
theorem measurable_updateFinset' {Î¹ : Type*} [DecidableEq Î¹] {I : Finset Î¹}
    {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]
    {y : (i : I) â†’ X i} : Measurable (fun x â†¦ updateFinset x I y) := by
  refine measurable_pi_lambda _ (fun i â†¦ ?_)
  by_cases hi : i âˆˆ I <;> simp only [updateFinset, hi, â†“reduceDIte, measurable_const]
  exact measurable_pi_apply _

theorem trajKernel_map_updateFinset {n : â„•} (xâ‚€ : (i : Iic n) â†’ X i) :
    (trajKernel Îº n xâ‚€).map (fun y â†¦ updateFinset y _ xâ‚€) = trajKernel Îº n xâ‚€ := by
  ext s ms
  nth_rw 2 [trajKernel_eq]
  have : (fun y â†¦ updateFinset y _ xâ‚€) = (IicProdIoi n âˆ˜ (Prod.mk xâ‚€) âˆ˜ (Set.Ioi n).restrict) := by
    ext x i
    simp [IicProdIoi, updateFinset]
  rw [this, map_apply' _ _ _ ms, â† Measure.map_map, Measure.map_apply _ ms, id_prod_apply',
    â† Measure.map_map, Measure.map_apply, map_apply]
  any_goals fun_prop
  all_goals exact (IicProdIoi n).measurable ms

variable {E : Type*} [NormedAddCommGroup E]

theorem integrable_trajKernel {a b : â„•} (hab : a â‰¤ b) {f : ((n : â„•) â†’ X n) â†’ E}
    (xâ‚€ : (i : Iic a) â†’ X i) (i_f : Integrable f (trajKernel Îº a xâ‚€)) :
    âˆ€áµ x âˆ‚trajKernel Îº a xâ‚€, Integrable f (trajKernel Îº b (frestrictLe b x)) := by
  rw [â† trajKernel_comp_ptraj _ hab, integrable_comp_iff] at i_f
  Â· apply ae_of_ae_map (p := fun x â†¦ Integrable f (trajKernel Îº b x))
    Â· exact (measurable_frestrictLe b).aemeasurable
    Â· convert i_f.1
      rw [â† frestrictLe_trajKernel, Kernel.map_apply _ (measurable_frestrictLe _)]
  Â· exact i_f.aestronglyMeasurable

theorem aestronglyMeasurable_trajKernel {a b : â„•} (hab : a â‰¤ b)
    {f : ((n : â„•) â†’ X n) â†’ E} {xâ‚€ : (i : Iic a) â†’ X i}
    (hf : AEStronglyMeasurable f (trajKernel Îº a xâ‚€)) :
    âˆ€áµ x âˆ‚ptraj Îº a b xâ‚€, AEStronglyMeasurable f (trajKernel Îº b x) := by
  rw [â† trajKernel_comp_ptraj Îº hab] at hf
  exact hf.comp

variable [NormedSpace â„ E]

variable {Îº} in
/-- When computing `âˆ« x, f x âˆ‚trajKernel Îº n xâ‚€`, because the trajectory up to time `n` is
determined by `xâ‚€` we can replace `x` by `updateFinset x _ xâ‚€`. -/
theorem integral_trajKernel {n : â„•} (xâ‚€ : (i : Iic n) â†’ X i) {f : ((n : â„•) â†’ X n) â†’ E}
    (mf : AEStronglyMeasurable f (trajKernel Îº n xâ‚€)) :
    âˆ« x, f x âˆ‚trajKernel Îº n xâ‚€ = âˆ« x, f (updateFinset x _ xâ‚€) âˆ‚trajKernel Îº n xâ‚€ := by
  nth_rw 1 [â† trajKernel_map_updateFinset, integral_map]
  Â· exact measurable_updateFinset'.aemeasurable
  Â· convert mf
    nth_rw 2 [â† trajKernel_map_updateFinset]

lemma ptraj_comp_ptrajProd_trajKernel {a b : â„•} (hab : a â‰¤ b) (u : Î  i : Iic a, X i) :
    (trajKernel Îº a u).map (fun x â†¦ (frestrictLe b x, x)) =
      (ptraj Îº a b u) âŠ—â‚˜ (trajKernel Îº b) := by
  ext s ms
  rw [Measure.map_apply (by fun_prop) ms, Measure.compProd_apply ms, â† trajKernel_comp_ptraj Îº hab,
    comp_apply' _ _ _ (ms.preimage (by fun_prop))]
  conv_rhs => enter [2]; ext a; rw [â† trajKernel_map_updateFinset]
  conv_lhs =>
    enter [2]
    ext a
    rw [â† trajKernel_map_updateFinset, Measure.map_apply measurable_updateFinset']
    rfl
    exact ((measurable_frestrictLe b).prod_mk measurable_id) ms
  simp_rw [Measure.map_apply measurable_updateFinset' (measurable_prod_mk_left ms),
    â† Set.preimage_comp]
  congrm âˆ«â» x, (trajKernel _ _ _) ((fun y â†¦ ?_) â»Â¹' _) âˆ‚_
  ext i <;> simp [updateFinset]

variable {Îº}

theorem integral_trajKernel_ptraj' {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : (i : Iic a) â†’ X i}
    {f : (Î  i : Iic b, X i) â†’ (Î  n : â„•, X n) â†’ E}
    (hf : Integrable f.uncurry ((ptraj Îº a b xâ‚€) âŠ—â‚˜ (trajKernel Îº b))) :
    âˆ« x, âˆ« y, f x y âˆ‚trajKernel Îº b x âˆ‚ptraj Îº a b xâ‚€ =
      âˆ« x, f (frestrictLe b x) x âˆ‚trajKernel Îº a xâ‚€ := by
  have hf1 := hf
  rw [â† ptraj_comp_ptrajProd_trajKernel Îº hab] at hf1
  replace hf1 := hf1.comp_measurable (by fun_prop)
  have hf2 := aestronglyMeasurable_trajKernel Îº hab hf1.1
  rw [â† trajKernel_comp_ptraj Îº hab, Kernel.integral_comp]
  Â· apply integral_congr_ae
    filter_upwards [hf.1.compProd, hf2]
    intro x h1 h2
    rw [integral_trajKernel _ h1]
    nth_rw 2 [integral_trajKernel]
    Â· simp_rw [frestrictLe_updateFinset]
    Â· exact h2
  Â· rwa [trajKernel_comp_ptraj _ hab]

theorem integral_trajKernel_ptraj {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : (i : Iic a) â†’ X i}
    {f : (Î  n : â„•, X n) â†’ E} (hf : Integrable f (trajKernel Îº a xâ‚€)) :
    âˆ« x, âˆ« y, f y âˆ‚trajKernel Îº b x âˆ‚ptraj Îº a b xâ‚€ = âˆ« x, f x âˆ‚trajKernel Îº a xâ‚€ := by
  apply integral_trajKernel_ptraj' hab
  rw [â† trajKernel_comp_ptraj Îº hab, â† snd_compProd_kernel] at hf
  exact hf.comp_measurable measurable_snd

-- theorem setIntegral_trajKernel_ptraj' {a b : â„•} (hab : a â‰¤ b) {u : (Î  i : Iic a, X i)}
--     {f : (Î  i : Iic b, X i) â†’ (Î  n : â„•, X n) â†’ E}
--     (hf : Integrable f.uncurry ((ptraj Îº a b u) âŠ—â‚˜ (trajKernel Îº b)))
--     {A : Set (Î  n, X n)} (hA : MeasurableSet[pi_preorder b] A) :
--     âˆ« x in A, âˆ« y, f x y âˆ‚trajKernel Îº b (frestrictLe b x) âˆ‚trajKernel Îº a u =
--       âˆ« y in A, f (frestrictLe b y) y âˆ‚trajKernel Îº a u := by
--   simp_rw [setIntegral_eq _ hA, â† integral_smul]
--   rw [integral_trajKernel_ptraj' hab]
--   simp_rw [â† preimage_indicator, â† setIntegral_eq _ (measurable_frestrictLe b hA)]
--   refine hf.smul_of_top_right <| memâ„’p_top_of_bound (C := 1)
--     (((measurable_indicator_const_iff 1).2 hA).comp measurable_fst).aestronglyMeasurable
--     <| Eventually.of_forall fun x â†¦ ?_
--   by_cases hx : x.1 âˆˆ A <;> simp [hx]

theorem setIntegral_trajKernel_ptraj' {a b : â„•} (hab : a â‰¤ b) {u : (Î  i : Iic a, X i)}
    {f : (Î  i : Iic b, X i) â†’ (Î  n : â„•, X n) â†’ E}
    (hf : Integrable f.uncurry ((ptraj Îº a b u) âŠ—â‚˜ (trajKernel Îº b)))
    {A : Set (Î  i : Iic b, X i)} (hA : MeasurableSet A) :
    âˆ« x in A, âˆ« y, f x y âˆ‚trajKernel Îº b x âˆ‚ptraj Îº a b u =
      âˆ« y in frestrictLe b â»Â¹' A, f (frestrictLe b y) y âˆ‚trajKernel Îº a u := by
  simp_rw [setIntegral_eq _ hA, â† integral_smul]
  rw [integral_trajKernel_ptraj' hab]
  simp_rw [â† preimage_indicator, â† setIntegral_eq _ (measurable_frestrictLe b hA)]
  refine hf.smul_of_top_right <| memâ„’p_top_of_bound (C := 1)
    (((measurable_indicator_const_iff 1).2 hA).comp measurable_fst).aestronglyMeasurable
    <| Eventually.of_forall fun x â†¦ ?_
  by_cases hx : x.1 âˆˆ A <;> simp [hx]

theorem setIntegral_trajKernel_ptraj {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : (Î  i : Iic a, X i)}
    {f : (Î  n : â„•, X n) â†’ E} (hf : Integrable f (trajKernel Îº a xâ‚€))
    {A : Set (Î  i : Iic b, X i)} (hA : MeasurableSet A) :
    âˆ« x in A, âˆ« y, f y âˆ‚trajKernel Îº b x âˆ‚ptraj Îº a b xâ‚€ =
      âˆ« y in frestrictLe b â»Â¹' A, f y âˆ‚trajKernel Îº a xâ‚€ := by
  refine setIntegral_trajKernel_ptraj' hab ?_ hA
  rw [â† trajKernel_comp_ptraj Îº hab, â† snd_compProd_kernel] at hf
  exact hf.comp_measurable measurable_snd

variable [CompleteSpace E]

theorem condExp_trajKernel
    {a b : â„•} (hab : a â‰¤ b) {xâ‚€ : (i : Iic a) â†’ X i} {f : ((n : â„•) â†’ X n) â†’ E}
    (i_f : Integrable f (trajKernel Îº a xâ‚€)) :
    (trajKernel Îº a xâ‚€)[f|pi_preorder b] =áµ[trajKernel Îº a xâ‚€]
      fun x â†¦ âˆ« y, f y âˆ‚trajKernel Îº b (frestrictLe b x) := by
  have mf : Integrable (fun x â†¦ âˆ« y, f y âˆ‚(trajKernel Îº b) x)
      (((trajKernel Îº a) xâ‚€).map (frestrictLe b)) := by
    rw [â† map_apply _ (measurable_frestrictLe _), frestrictLe_trajKernel _ _]
    rw [â† trajKernel_comp_ptraj _ hab] at i_f
    exact i_f.integral_comp
  refine ae_eq_condExp_of_forall_setIntegral_eq (pi_preorder.le _) i_f
    (fun s _ _ â†¦
      (integrable_map_measure mf.1 (measurable_frestrictLe b).aemeasurable).1 mf |>.integrableOn)
    ?_ (mf.1.comp_ae_measurable' (measurable_frestrictLe b).aemeasurable) |>.symm
  rintro - âŸ¨t, mt, rflâŸ© -
  simp_rw [Function.comp_apply]
  rw [â† setIntegral_map mt mf.1, â† map_apply, frestrictLe_trajKernel,
    setIntegral_trajKernel_ptraj hab i_f mt]
  any_goals fun_prop

variable (Îº)

theorem condExp_trajKernel' {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c)
    (xâ‚€ : (i : Iic a) â†’ X i) (f : ((n : â„•) â†’ X n) â†’ E) :
    (trajKernel Îº a xâ‚€)[f|pi_preorder b] =áµ[trajKernel Îº a xâ‚€]
      fun x â†¦ âˆ« y, ((trajKernel Îº a xâ‚€)[f|pi_preorder c]) (updateFinset x _ y)
        âˆ‚ptraj Îº b c (frestrictLe b x) := by
  have i_cf : Integrable ((trajKernel Îº a xâ‚€)[f|pi_preorder c]) (trajKernel Îº a xâ‚€) :=
    integrable_condExp
  have mcf : StronglyMeasurable ((trajKernel Îº a xâ‚€)[f|pi_preorder c]) :=
    stronglyMeasurable_condExp.mono (pi_preorder.le c)
  filter_upwards [pi_preorder.condExp_condExp f hbc, condExp_trajKernel hab i_cf]
  intro x h1 h2
  rw [â† h1, h2, â† frestrictLe_trajKernel, Kernel.map_apply, integral_map]
  Â· congr with y
    apply stronglyMeasurable_dependsOn stronglyMeasurable_condExp
    simp only [Set.mem_Iic, updateFinset, mem_Iic, frestrictLe_apply, dite_eq_ite]
    exact fun i hi â†¦ (if_pos hi).symm
  any_goals fun_prop
  Â· exact (mcf.comp_measurable measurable_updateFinset).aestronglyMeasurable
