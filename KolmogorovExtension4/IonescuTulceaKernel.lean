/-
Copyright (c) 2024 Etienne Marion. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Etienne Marion
-/
import KolmogorovExtension4.compProdNat
import KolmogorovExtension4.Projective
import KolmogorovExtension4.DependsOn
import Mathlib.MeasureTheory.Measure.ProbabilityMeasure
import KolmogorovExtension4.KolmogorovExtension
import Batteries.Data.Nat.Lemmas

open MeasureTheory ProbabilityTheory Finset ENNReal Filter Topology Function MeasurableSpace Preorder

section castLemmas

variable {X : â„• â†’ Type*}

private lemma Iic_pi_eq {a b : â„•} (h : a = b) :
    ((i : Iic a) â†’ X i) = ((i : Iic b) â†’ X i) := by cases h; rfl

private lemma cast_pi {s t : Set â„•} (h : s = t) (h' : ((i : s) â†’ X i) = ((i : t) â†’ X i))
    (x : (i : s) â†’ X i) (i : t) :
    cast h' x i = x âŸ¨i.1, h.symm â–¸ i.2âŸ© := by
  subst h
  rfl

/-- This function takes a trajectory up to time `p` and a way of building the next step of the
trajectory and returns a whole trajectory whose first steps correspond
to the initial ones provided. -/
def iterate_induction {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    (ind : (k : â„•) â†’ ((n : Iic k) â†’ X n) â†’ X (k + 1)) :
    (k : â„•) â†’ X k := fun k â†¦ by
  cases k with
  | zero => exact xâ‚€ âŸ¨0, mem_Iic.2 <| zero_le pâŸ©
  | succ q =>
    exact if hq : q + 1 â‰¤ p
      then xâ‚€ âŸ¨q + 1, mem_Iic.2 hqâŸ©
      else ind q (fun i â†¦ iterate_induction xâ‚€ ind i)
  decreasing_by
    have := mem_Iic.1 i.2
    rename_i h
    rw [â† Nat.lt_succ, Nat.succ_eq_add_one, â† h] at this
    exact this

theorem iterate_induction_le {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    (ind : (k : â„•) â†’ ((n : Iic k) â†’ X n) â†’ X (k + 1)) (k : Iic p) :
    iterate_induction xâ‚€ ind k = xâ‚€ k := by
  rcases k with âŸ¨i, hiâŸ©
  cases i with
  | zero =>
    rw [iterate_induction, Nat.casesAuxOn_zero]
  | succ j =>
    rw [iterate_induction, Nat.casesAuxOn_succ]
    simp [mem_Iic.1 hi]

theorem proj_updateFinset {n : â„•} (x : (n : â„•) â†’ X n) (y : (i : Iic n) â†’ X i) :
    frestrictLe n (updateFinset x _ y) = y := by
  ext i
  simp [updateFinset, i.2]

variable [âˆ€ n, MeasurableSpace (X n)]

theorem aux {n : â„•} (xâ‚€ : (i : Iic n) â†’ X i) :
    (el' n âˆ˜ (Prod.mk xâ‚€) âˆ˜ (Set.Ioi n).restrict) = fun y â†¦ updateFinset y _ xâ‚€ := by
  ext y i
  simp [el', updateFinset]

private lemma measure_cast {a b : â„•} (h : a = b) (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) :
    (Î¼ a).map (cast (Iic_pi_eq h)) = Î¼ b := by
  subst h
  exact Measure.map_id

private lemma heq_measurableSpace_Iic_pi {a b : â„•} (h : a = b) :
    HEq (inferInstance : MeasurableSpace ((i : Iic a) â†’ X i))
    (inferInstance : MeasurableSpace ((i : Iic b) â†’ X i)) := by cases h; rfl

end castLemmas

section ProjectiveFamily

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]

/-- To check that a measure `Î½` is the projective limit of a projective family of measures indexed
by `Finset â„•`, it is enough to check on intervals of the form `Iic n`, where `n` is larger than
a given integer. -/
theorem isProjectiveLimit_nat_iff' (Î¼ : (I : Finset â„•) â†’ Measure ((i : I) â†’ X i))
    (hÎ¼ : IsProjectiveMeasureFamily Î¼) (Î½ : Measure ((n : â„•) â†’ X n)) (a : â„•) :
    IsProjectiveLimit Î½ Î¼ â†” âˆ€ n â‰¥ a, Î½.map (frestrictLe n) = Î¼ (Iic n) := by
  refine âŸ¨fun h n _ â†¦ h (Iic n), fun h I â†¦ ?_âŸ©
  rw [â† restrictâ‚‚_comp_restrict (I.sub_Iic.trans (Iic_subset_Iic.2 (le_max_left (I.sup id) a))),
    â† Measure.map_map (measurable_restrictâ‚‚ _) (measurable_restrict _), â† frestrictLe,
    h _ (le_max_right _ _), â† hÎ¼]

/-- To check that a measure `Î½` is the projective limit of a projective family of measures indexed
by `Finset â„•`, it is enough to check on intervals of the form `Iic n`. -/
theorem isProjectiveLimit_nat_iff (Î¼ : (I : Finset â„•) â†’ Measure ((i : I) â†’ X i))
    (hÎ¼ : IsProjectiveMeasureFamily Î¼) (Î½ : Measure ((n : â„•) â†’ X n)) :
    IsProjectiveLimit Î½ Î¼ â†” âˆ€ n, Î½.map (frestrictLe n) = Î¼ (Iic n) := by
  rw [isProjectiveLimit_nat_iff' _ hÎ¼ _ 0]
  simp

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)`, we can define a family
of measures indexed by `Finset â„•` by projecting the measures. -/
noncomputable def inducedFamily (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) :
    (S : Finset â„•) â†’ Measure ((k : S) â†’ X k) :=
  fun S â†¦ (Î¼ (S.sup id)).map (restrictâ‚‚ S.sub_Iic)

instance (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) [âˆ€ n, IsFiniteMeasure (Î¼ n)] (I : Finset â„•) :
    IsFiniteMeasure (inducedFamily Î¼ I) := by
  rw [inducedFamily]
  infer_instance

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)`, the induced family
equals `Î¼` over the intervals `Iic n`. -/
theorem inducedFamily_Iic (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)) (n : â„•) :
    inducedFamily Î¼ (Iic n) = Î¼ n := by
  rw [inducedFamily, â† measure_cast (sup_Iic n) Î¼]
  congr with x i
  rw [restrictâ‚‚, cast_pi _ (Iic_pi_eq (sup_Iic n)) x i]
  rw [sup_Iic n]

/-- Given a family of measures `Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i)`, the induced family
will be projective only if `Î¼` is projective, in the sense that if `a â‰¤ b`, then projecting
`Î¼ b` gives `Î¼ a`. -/
theorem isProjectiveMeasureFamily_inducedFamily (Î¼ : (n : â„•) â†’ Measure ((i : Iic n) â†’ X i))
    (h : âˆ€ a b : â„•, âˆ€ hab : a â‰¤ b, (Î¼ b).map (frestrictLeâ‚‚ hab) = Î¼ a) :
    IsProjectiveMeasureFamily (inducedFamily Î¼) := by
  intro I J hJI
  have sls : J.sup id â‰¤ I.sup id := sup_mono hJI
  simp only [inducedFamily]
  rw [Measure.map_map (measurable_restrictâ‚‚ hJI) (measurable_restrictâ‚‚ _), restrictâ‚‚_comp_restrictâ‚‚,
    â† restrictâ‚‚_comp_restrictâ‚‚ J.sub_Iic (Iic_subset_Iic.2 sls),
    â† Measure.map_map (measurable_restrictâ‚‚ _) (measurable_restrictâ‚‚ _), â† frestrictLeâ‚‚,
    h (J.sup id) (I.sup id) sls]

end ProjectiveFamily

open Kernel

section definition

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]
variable (Îº : (k : â„•) â†’ Kernel ((i : Iic k) â†’ X i) (X (k + 1)))
variable [âˆ€ k, IsMarkovKernel (Îº k)]

/-- Given a family of kernels `Îº : (n : â„•) â†’ Kernel ((i : Iic n) â†’ X i) (X (n + 1))`, and the
trajectory up to time `n` we can construct an additive content over cylinders. It corresponds
to composing the kernels by starting at time `n + 1`. -/
noncomputable def ionescuTulceaContent {n : â„•} (x : (i : Iic n) â†’ X i) :
    AddContent (measurableCylinders X) :=
  kolContent (isProjectiveMeasureFamily_inducedFamily _ (partialKernel_proj_apply Îº x))

/-- The `ionescuTulceaContent Îº x` of a cylinder indexed by first coordinates is given by
`partialKernel`. -/
theorem ionescuTulceaContent_cylinder {a b : â„•} (x : (i : Iic a) â†’ X i)
    {S : Set ((i : Iic b) â†’ X i)} (mS : MeasurableSet S) :
    ionescuTulceaContent Îº x (cylinder _ S) = partialKernel Îº a b x S := by
  rw [ionescuTulceaContent, kolContent_cylinder _ mS, inducedFamily_Iic]

/-- The `ionescuTulceaContent` of a cylinder is equal to the integral of its indicator function. -/
theorem ionescuTulceaContent_eq_lmarginalPartialKernel {N : â„•} {S : Set ((i : Iic N) â†’ X i)}
    (mS : MeasurableSet S) (x : (n : â„•) â†’ X n) (n : â„•) :
    ionescuTulceaContent Îº (frestrictLe n x) (cylinder _ S) =
    lmarginalPartialKernel Îº n N ((cylinder _ S).indicator 1) x := by
  rw [ionescuTulceaContent_cylinder _ _ mS, â† lintegral_indicator_one mS, lmarginalPartialKernel]
  congr with y
  apply indicator_const_eq
  rw [mem_cylinder]
  congrm (fun i â†¦ ?_) âˆˆ S
  simp [updateFinset, i.2]

/-- The cylinders of a product space indexed by `â„•` can be seen as depending on the first
corrdinates. -/
theorem cylinders_nat :
    measurableCylinders X = â‹ƒ (N) (S) (_ : MeasurableSet S), {cylinder (Iic N) S} := by
  ext s
  simp only [mem_measurableCylinders, exists_prop, Set.mem_iUnion, mem_singleton]
  refine âŸ¨?_, fun âŸ¨N, S, mS, s_eqâŸ© â†¦ âŸ¨Iic N, S, mS, s_eqâŸ©âŸ©
  rintro âŸ¨t, S, mS, rflâŸ©
  refine âŸ¨t.sup id, restrictâ‚‚ t.sub_Iic â»Â¹' S, measurable_restrictâ‚‚ _ mS, ?_âŸ©
  unfold cylinder
  rw [â† Set.preimage_comp]
  rfl

variable [Nonempty (X 0)]

/-- This is an auxiliary result for `ionescuTulceaContent_tendsto_zero`.
Consider `f` a sequence of bounded measurable
functions such that `f n` depends only on the first coordinates up to `N n`.
Assume that when integrating `f n` against `partialKernel (k + 1) (N n)`,
one gets a non-increasing sequence of functions wich converges to `l`.
Assume then that there exists `Îµ` and `y : (n : Iic k) â†’ X n` such that
when integrating `f n` against `partialKernel k (N n)`, you get something at least
`Îµ` for all. Then there exists `z` such that this remains true when integrating
`f` against `partialKernel (k + 1) (N n) (update y (k + 1) z)`. -/
theorem le_lmarginalPartialKernel_succ {f : â„• â†’ ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ} {N : â„• â†’ â„•}
    (hcte : âˆ€ n, DependsOn (f n) (Iic (N n))) (mf : âˆ€ n, Measurable (f n))
    {bound : â„â‰¥0âˆ} (fin_bound : bound â‰  âˆ) (le_bound : âˆ€ n x, f n x â‰¤ bound) {k : â„•}
    (anti : âˆ€ x, Antitone (fun n â†¦ lmarginalPartialKernel Îº (k + 1) (N n) (f n) x))
    {l : ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ}
    (htendsto : âˆ€ x, Tendsto (fun n â†¦ lmarginalPartialKernel Îº (k + 1) (N n) (f n) x)
      atTop (ğ“ (l x)))
    (Îµ : â„â‰¥0âˆ) (y : (n : Iic k) â†’ X n)
    (hpos : âˆ€ x n, Îµ â‰¤ lmarginalPartialKernel Îº k (N n) (f n) (updateFinset x _ y)) :
    âˆƒ z, âˆ€ x n, Îµ â‰¤ lmarginalPartialKernel Îº (k + 1) (N n) (f n)
      (Function.update (updateFinset x _ y) (k + 1) z) := by
  have _ n : Nonempty (X n) := by
    refine Nat.case_strong_induction_on (p := fun n â†¦ Nonempty (X n)) _ inferInstance
      fun n hind â†¦ ?_
    have : Nonempty ((i : Iic n) â†’ X i) :=
      Nonempty.intro fun i â†¦ @Classical.ofNonempty _ (hind i.1 (mem_Iic.1 i.2))
    exact ProbabilityMeasure.nonempty âŸ¨Îº n Classical.ofNonempty, inferInstanceâŸ©
  let F : â„• â†’ ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ := fun n â†¦ lmarginalPartialKernel Îº (k + 1) (N n) (f n)
  -- `Fâ‚™` converges to `l` by hypothesis.
  have tendstoF x : Tendsto (F Â· x) atTop (ğ“ (l x)) := htendsto x
  -- Integrating `fâ‚™` between time `k` and `Nâ‚™` is the same as integrating
  -- `Fâ‚™` between time `k` and time `k + 1` variable.
  have f_eq x n : lmarginalPartialKernel Îº k (N n) (f n) x =
    lmarginalPartialKernel Îº k (k + 1) (F n) x := by
    simp_rw [F]
    rcases lt_trichotomy (k + 1) (N n) with h | h | h
    Â· rw [â† lmarginalPartialKernel_self Îº k.le_succ h.le (mf n)]
    Â· rw [â† h, lmarginalPartialKernel_le _ (_root_.le_refl (k + 1)) (mf n)]
    Â· rw [lmarginalPartialKernel_le _ (by omega) (mf n),
        (hcte n).lmarginalPartialKernel_eq _ _ (mf n) (by omega),
        (hcte n).lmarginalPartialKernel_eq _ _ (mf n) (by omega)]
  -- `F` is also a bounded sequence.
  have F_le n x : F n x â‰¤ bound := by
    simp_rw [F, lmarginalPartialKernel]
    rw [â† mul_one bound, â† measure_univ (Î¼ := partialKernel Îº (k + 1) (N n) (frestrictLe (k + 1) x)),
        â† MeasureTheory.lintegral_const]
    exact lintegral_mono fun _ â†¦ le_bound _ _
  -- By dominated convergence, the integral of `fâ‚™` between time `k` and time `N n` converges
  -- to the integral of `l` between time `k` and time `k + 1`.
  have tendsto_int x : Tendsto (fun n â†¦ lmarginalPartialKernel Îº k (N n) (f n) x) atTop
      (ğ“ (lmarginalPartialKernel Îº k (k + 1) l x)) := by
    simp_rw [f_eq, lmarginalPartialKernel]
    exact tendsto_lintegral_of_dominated_convergence (fun _ â†¦ bound)
      (fun n â†¦ (measurable_lmarginalPartialKernel _ _ _ (mf n)).comp measurable_updateFinset)
      (fun n â†¦ Eventually.of_forall <| fun y â†¦ F_le n _)
      (by simp [fin_bound]) (Eventually.of_forall (fun _ â†¦ tendstoF _))
  -- By hypothesis, we have `Îµ â‰¤ lmarginalPartialKernel Îº k (k + 1) (F n) (updateFinset x _ y)`,
  -- so this is also true for `l`.
  have Îµ_le_lint x : Îµ â‰¤ lmarginalPartialKernel Îº k (k + 1) l (updateFinset x _ y) :=
    ge_of_tendsto (tendsto_int _) (by simp [hpos])
  let x_ : (n : â„•) â†’ X n := Classical.ofNonempty
  -- We now have that the integral of `l` with respect to a probability measure is greater than `Îµ`,
  -- therefore there exists `x'` such that `Îµ â‰¤ l(y, x')`.
  obtain âŸ¨x', hx'âŸ© : âˆƒ x', Îµ â‰¤ l (Function.update (updateFinset x_ _ y) (k + 1) x') := by
    have aux : âˆ«â» (a : X (k + 1)),
        l (update (updateFinset x_ _ y) (k + 1) a) âˆ‚(Îº k y) â‰  âˆ := by
      apply ne_top_of_le_ne_top fin_bound
      rw [â† mul_one bound, â† measure_univ (Î¼ := Îº k y), â† MeasureTheory.lintegral_const]
      exact lintegral_mono <| fun y â†¦ le_of_tendsto' (tendstoF _) <| fun _ â†¦ F_le _ _
    rcases exists_lintegral_le aux with âŸ¨x', hx'âŸ©
    refine âŸ¨x', ?_âŸ©
    calc
      Îµ â‰¤ âˆ«â» (z : X (k + 1)),
          l (update (updateFinset x_ _ y) (k + 1) z) âˆ‚(Îº k y) := by
          convert Îµ_le_lint x_
          rw [lmarginalPartialKernel_lt _ k.lt_succ_self, kerNat_succ_self, Kernel.map_apply,
            lintegral_map_equiv]
          Â· congrm âˆ«â» z, (l fun i â†¦ ?_) âˆ‚Îº k (fun i â†¦ ?_)
            Â· simp [i.2, updateFinset]
            Â· simp [update, updateFinset, e]
          Â· exact (e k).measurable
          Â· refine ENNReal.measurable_of_tendsto ?_ (tendsto_pi_nhds.2 htendsto)
            exact fun n â†¦ measurable_lmarginalPartialKernel _ _ _ (mf n)
      _ â‰¤ l (update (updateFinset x_ _ y) (k + 1) x') := hx'
  refine âŸ¨x', fun x n â†¦ ?_âŸ©
  -- As `F` is a non-increasing sequence, we have `Îµ â‰¤ Fâ‚™(y, x')` for any `n`.
  have := le_trans hx' ((anti _).le_of_tendsto (tendstoF _) n)
  -- This part below is just to say that this is true for any `x : (i : Î¹) â†’ X i`,
  -- as `Fâ‚™` technically depends on all the variables, but really depends only on the first `k + 1`.
  convert this using 1
  refine DependsOn.dependsOn_lmarginalPartialKernel _ _ (hcte n) (mf n) fun i hi â†¦ ?_
  simp only [update, updateFinset]
  split_ifs with h1 h2 <;> try rfl
  rw [mem_coe, mem_Iic] at *
  omega

/-- The indicator of a cylinder only depends on the variables whose the cylinder depends on. -/
theorem dependsOn_cylinder_indicator {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {I : Finset Î¹}
    (S : Set ((i : I) â†’ Î± i)) :
    DependsOn ((cylinder I S).indicator (1 : ((i : Î¹) â†’ Î± i) â†’ â„â‰¥0âˆ)) I :=
  fun x y hxy â†¦ indicator_const_eq _ (by simp [restrict_def, hxy])

/-- This is the key theorem to prove the existence of the `ionescuTulceaKernel`:
the `ionescuTulceaContent` of a decresaing sequence of cylinders with empty intersection
converges to `0`.
This implies the `Ïƒ`-additivity of `ionescuTulceaContent`
(see `sigma_additive_addContent_of_tendsto_zero`), which allows to extend it to the
`Ïƒ`-algebra by CarathÃ©odory's theorem. -/
theorem ionescuTulceaContent_tendsto_zero (A : â„• â†’ Set ((n : â„•) â†’ X n))
    (A_mem : âˆ€ n, A n âˆˆ measurableCylinders X) (A_anti : Antitone A) (A_inter : â‹‚ n, A n = âˆ…)
    {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i) :
    Tendsto (fun n â†¦ ionescuTulceaContent Îº xâ‚€ (A n)) atTop (ğ“ 0) := by
  have _ n : Nonempty (X n) := by
    refine Nat.case_strong_induction_on (p := fun n â†¦ Nonempty (X n)) _ inferInstance
      fun n hind â†¦ ?_
    have : Nonempty ((i : Iic n) â†’ X i) :=
      Nonempty.intro fun i â†¦ @Classical.ofNonempty _ (hind i.1 (mem_Iic.1 i.2))
    exact ProbabilityMeasure.nonempty âŸ¨Îº n Classical.ofNonempty, inferInstanceâŸ©
  -- `Aâ‚™` is a cylinder, it can be written `cylinder (Iic (N n)) Sâ‚™`.
  have A_cyl n : âˆƒ N S, MeasurableSet S âˆ§ A n = cylinder (Iic N) S := by
    simpa [cylinders_nat] using A_mem n
  choose N S mS A_eq using A_cyl
  -- We write `Ï‡â‚™` for the indicator function of `Aâ‚™`.
  let Ï‡ n := (A n).indicator (1 : ((n : â„•) â†’ X n) â†’ â„â‰¥0âˆ)
  -- `Ï‡â‚™` is measurable.
  have mÏ‡ n : Measurable (Ï‡ n) := by
    simp_rw [Ï‡, A_eq]
    exact (measurable_indicator_const_iff 1).2 <| (mS n).cylinder
  -- `Ï‡â‚™` only depends on the first coordinates.
  have Ï‡_dep n : DependsOn (Ï‡ n) (Iic (N n)) := by
    simp_rw [Ï‡, A_eq]
    exact dependsOn_cylinder_indicator _
  -- Therefore its integral against `partialKernel Îº k (N n)` is constant.
  have lma_const x y n :
      lmarginalPartialKernel Îº p (N n) (Ï‡ n) (updateFinset x _ xâ‚€) =
      lmarginalPartialKernel Îº p (N n) (Ï‡ n) (updateFinset y _ xâ‚€) := by
    apply (Ï‡_dep n).dependsOn_lmarginalPartialKernel Îº p (mÏ‡ n)
    intro i hi
    rw [mem_coe, mem_Iic] at hi
    simp [updateFinset, hi]
  -- As `(Aâ‚™)` is non-increasing, so is `(Ï‡â‚™)`.
  have Ï‡_anti : Antitone Ï‡ := by
    refine fun m n hmn y â†¦ ?_
    apply Set.indicator_le fun a ha â†¦ ?_
    simp [Ï‡, A_anti hmn ha]
  -- Integrating `Ï‡â‚™` further than the last coordinate it depends on does nothing.
  -- This is used to then show that the integral of `Ï‡â‚™` from time `k` is non-increasing.
  have lma_inv k M n (h : N n â‰¤ M) :
      lmarginalPartialKernel Îº k M (Ï‡ n) = lmarginalPartialKernel Îº k (N n) (Ï‡ n) :=
    (Ï‡_dep n).lmarginalPartialKernel_right Îº k (mÏ‡ n) h (_root_.le_refl _)
  -- the integral of `Ï‡â‚™` from time `k` is non-increasing.
  have anti_lma k x : Antitone fun n â†¦ lmarginalPartialKernel Îº k (N n) (Ï‡ n) x := by
    intro m n hmn
    simp only
    rw [â† lma_inv k ((N n).max (N m)) n (le_max_left _ _),
      â† lma_inv k ((N n).max (N m)) m (le_max_right _ _)]
    exact lmarginalPartialKernel_mono _ _ _ (Ï‡_anti hmn) _
  -- Therefore it converges to some function `lâ‚–`.
  have this k x : âˆƒ l,
      Tendsto (fun n â†¦ lmarginalPartialKernel Îº k (N n) (Ï‡ n) x) atTop (ğ“ l) := by
    rcases tendsto_of_antitone <| anti_lma k x with h | h
    Â· rw [OrderBot.atBot_eq] at h
      exact âŸ¨0, h.mono_right <| pure_le_nhds 0âŸ©
    Â· exact h
  choose l hl using this
  -- `lâ‚š` is constant because it is the limit of constant functions: we call it `Îµ`.
  have l_const x y : l p (updateFinset x _ xâ‚€) = l p (updateFinset y _ xâ‚€) := by
    have := hl p (updateFinset x _ xâ‚€)
    simp_rw [lma_const x y] at this
    exact tendsto_nhds_unique this (hl p _)
  obtain âŸ¨Îµ, hÎµâŸ© : âˆƒ Îµ, âˆ€ x, l p (updateFinset x _ xâ‚€) = Îµ :=
      âŸ¨l p (updateFinset Classical.ofNonempty _ xâ‚€), fun x â†¦ l_const _ _âŸ©
  -- As the sequence is decreasing, `Îµ â‰¤ âˆ« Ï‡â‚™`.
  have hpos x n : Îµ â‰¤ lmarginalPartialKernel Îº p (N n) (Ï‡ n) (updateFinset x _ xâ‚€) :=
    hÎµ x â–¸ ((anti_lma p _).le_of_tendsto (hl p _)) n
  -- Also, the indicators are bounded by `1`.
  have Ï‡_le n x : Ï‡ n x â‰¤ 1 := by
    apply Set.indicator_le
    simp
  -- We have all the conditions to apply ``. This allows us to recursively
  -- build a sequence `z` with the following property: for any `k â‰¥ p` and `n`,
  -- integrating `Ï‡ n` from time `k` to time `N n` with the trajectory up to `k` being equal to `z`
  -- gives something greater than `Îµ`.
  choose! ind hind using
    fun k y h â†¦ le_lmarginalPartialKernel_succ Îº Ï‡_dep mÏ‡ (by norm_num : (1 : â„â‰¥0âˆ) â‰  âˆ)
      Ï‡_le (anti_lma (k + 1)) (hl (k + 1)) Îµ y h
  let z := iterate_induction xâ‚€ ind
  have imp k (hk : p â‰¤ k) : âˆ€ x n,
      Îµ â‰¤ lmarginalPartialKernel Îº k (N n) (Ï‡ n) (updateFinset x (Iic k) (frestrictLe k z)) := by
    refine Nat.le_induction (fun x n â†¦ ?_) (fun k hn h x n â†¦ ?_) k hk
    Â· convert hpos x n
      ext i
      simp only [frestrictLe, restrict, z]
      exact iterate_induction_le ..
    Â· rw [â† update_updateFinset_eq]
      convert hind k (fun i â†¦ z i.1) h x n
      simp_rw [z]
      rw [iterate_induction, Nat.casesAuxOn_succ]
      simp [Nat.lt_succ.2 hn]
  -- We now want to prove that the integral of `Ï‡â‚™`, which is equal to the `ionescuTulceaContent`
  -- of `Aâ‚™`, converges to `0`.
  have aux x n : ionescuTulceaContent Îº xâ‚€ (A n) =
      lmarginalPartialKernel Îº p (N n) (Ï‡ n) (updateFinset x _ xâ‚€) := by
    simp_rw [Ï‡, A_eq]
    nth_rw 1 [â† proj_updateFinset x xâ‚€]
    exact ionescuTulceaContent_eq_lmarginalPartialKernel Îº (mS n) (updateFinset x _ xâ‚€) p
  simp_rw [aux Classical.ofNonempty]
  convert hl p (updateFinset Classical.ofNonempty _ xâ‚€)
  rw [hÎµ]
  by_contra!
  -- Which means that we want to prove that `Îµ = 0`. But if `Îµ > 0`, then for any `n`,
  -- choosing `k > Nâ‚™` we get `Îµ â‰¤ Ï‡â‚™(zâ‚€, ..., z_{Nâ‚™})` and therefore `z âˆˆ Aâ‚™`.
  -- This contradicts the fact that `(Aâ‚™)` has an empty intersection.
  have Îµ_pos : 0 < Îµ := this.symm.bot_lt
  have mem n : z âˆˆ A n := by
    have : Ï‡ n z = lmarginalPartialKernel Îº (max p (N n)) (N n) (Ï‡ n)
        (updateFinset z (Iic (N n)) (fun i â†¦ z i)) := by
      rw [lmarginalPartialKernel_le _ (le_max_right _ _) (mÏ‡ n)]
      congr with i
      simp [updateFinset]
    have : 0 < Ï‡ n (z) := by
      rw [this]
      convert lt_of_lt_of_le Îµ_pos (imp _ (le_max_left _ _) z n) using 2
      ext i
      simp [updateFinset]
    exact Set.mem_of_indicator_ne_zero (ne_of_lt this).symm
  exact (A_inter â–¸ Set.mem_iInter.2 mem).elim

/-- The `ionescuTulceaContent` is sigma-subadditive. -/
theorem ionescuTulceaContent_sigma_subadditive {p : â„•} (xâ‚€ : (i : Iic p) â†’ X i)
    â¦ƒf : â„• â†’ Set ((n : â„•) â†’ X n)â¦„
    (hf : âˆ€ n, f n âˆˆ measurableCylinders X)
    (hf_Union : (â‹ƒ n, f n) âˆˆ measurableCylinders X) :
    ionescuTulceaContent Îº xâ‚€ (â‹ƒ n, f n) â‰¤ âˆ‘' n, ionescuTulceaContent Îº xâ‚€ (f n) := by
  have _ n : Nonempty (X n) := by
    refine Nat.case_strong_induction_on (p := fun n â†¦ Nonempty (X n)) _ inferInstance
      fun n hind â†¦ ?_
    have : Nonempty ((i : Iic n) â†’ X i) :=
      Nonempty.intro fun i â†¦ @Classical.ofNonempty _ (hind i.1 (mem_Iic.1 i.2))
    exact ProbabilityMeasure.nonempty âŸ¨Îº n Classical.ofNonempty, inferInstanceâŸ©
  refine addContent_iUnion_le_of_addContent_iUnion_eq_tsum
    isSetRing_measurableCylinders (fun f hf hf_Union hf' â†¦ ?_) f hf hf_Union
  refine sigma_additive_addContent_of_tendsto_zero isSetRing_measurableCylinders
    (ionescuTulceaContent Îº xâ‚€) (fun s hs â†¦ ?_) ?_ hf hf_Union hf'
  Â· obtain âŸ¨N, S, mS, s_eqâŸ© : âˆƒ N S, MeasurableSet S âˆ§ s = cylinder (Iic N) S := by
      simpa [cylinders_nat] using hs
    let x_ : (n : â„•) â†’ X n := Classical.ofNonempty
    rw [s_eq, â† proj_updateFinset x_ xâ‚€,
      ionescuTulceaContent_eq_lmarginalPartialKernel Îº mS (updateFinset x_ _ xâ‚€)]
    refine ne_of_lt (lt_of_le_of_lt ?_ (by norm_num : (1 : â„â‰¥0âˆ) < âˆ))
    nth_rw 2 [â† mul_one 1,
      â† measure_univ (Î¼ := partialKernel Îº p N (fun i â†¦ updateFinset x_ _ xâ‚€ i))]
    rw [lmarginalPartialKernel, â† MeasureTheory.lintegral_const]
    exact lintegral_mono <| Set.indicator_le (by simp)
  Â· exact fun s hs anti_s inter_s â†¦ ionescuTulceaContent_tendsto_zero Îº s hs anti_s inter_s xâ‚€

/-- This function is the kernel given by the Ionescu-Tulcea theorem. -/
noncomputable def ionescuTulceaFun (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    Measure ((n : â„•) â†’ X n) :=
  Measure.ofAddContent isSetSemiring_measurableCylinders generateFrom_measurableCylinders
    (ionescuTulceaContent Îº xâ‚€) (ionescuTulceaContent_sigma_subadditive Îº xâ‚€)

theorem isProbabilityMeasure_ionescuTulceaFun (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    IsProbabilityMeasure (ionescuTulceaFun Îº p xâ‚€) where
  measure_univ := by
    rw [â† cylinder_univ (Iic 0), ionescuTulceaFun, Measure.ofAddContent_eq,
      ionescuTulceaContent_cylinder _ _ MeasurableSet.univ]
    Â· exact measure_univ
    Â· exact (mem_measurableCylinders _).2 âŸ¨Iic 0, Set.univ, MeasurableSet.univ, rflâŸ©

theorem isProjectiveLimit_ionescuTulceaFun (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    IsProjectiveLimit (ionescuTulceaFun Îº p xâ‚€)
      (inducedFamily (fun n â†¦ partialKernel Îº p n xâ‚€)) := by
  rw [isProjectiveLimit_nat_iff]
  Â· intro n
    ext s ms
    rw [Measure.map_apply (measurable_frestrictLe n) ms]
    have h_mem : (frestrictLe n) â»Â¹' s âˆˆ measurableCylinders X := by
      rw [mem_measurableCylinders]; exact âŸ¨Iic n, s, ms, rflâŸ©
    rw [ionescuTulceaFun, Measure.ofAddContent_eq _ _ _ _ h_mem, ionescuTulceaContent,
      kolContent_congr _ (frestrictLe n â»Â¹' s) rfl ms]
  Â· exact (isProjectiveMeasureFamily_inducedFamily _ (partialKernel_proj_apply Îº xâ‚€))

theorem measurable_ionescuTulceaFun (p : â„•) : Measurable (ionescuTulceaFun Îº p) := by
  apply Measure.measurable_of_measurable_coe
  refine MeasurableSpace.induction_on_inter
    (C := fun t â†¦ Measurable (fun xâ‚€ â†¦ ionescuTulceaFun Îº p xâ‚€ t))
    (s := measurableCylinders X) generateFrom_measurableCylinders.symm
    isPiSystem_measurableCylinders (by simp) (fun t ht â†¦ ?cylinder) (fun t mt ht â†¦ ?compl)
    (fun f disf mf hf â†¦ ?union)
  Â· obtain âŸ¨N, S, mS, t_eqâŸ© : âˆƒ N S, MeasurableSet S âˆ§ t = cylinder (Iic N) S := by
      simpa [cylinders_nat] using ht
    simp_rw [ionescuTulceaFun, Measure.ofAddContent_eq _ _ _ _ ht, ionescuTulceaContent,
      kolContent_congr _ t t_eq mS, inducedFamily]
    refine Measure.measurable_measure.1 ?_ _ mS
    exact (Measure.measurable_map _ (measurable_restrictâ‚‚ _)).comp (Kernel.measurable _)
  Â· have := isProbabilityMeasure_ionescuTulceaFun Îº p
    simp_rw [measure_compl mt (measure_ne_top _ _), measure_univ]
    exact Measurable.const_sub ht _
  Â· simp_rw [measure_iUnion disf mf]
    exact Measurable.ennreal_tsum hf

/-- *Ionescu-Tulcea Theorem* : Given a family of kernels `Îº k` taking variables in `Iic k` with
value in `X (k+1)`, the kernel `ionescuTulceaKernel Îº p` takes a variable `x` depending on the
variables `i â‰¤ p` and associates to it a kernel on trajectories depending on all variables,
where the entries with index `â‰¤ p` are those of `x`, and then one follows iteratively the
kernels `Îº p`, then `Îº (p+1)`, and so on.

The fact that such a kernel exists on infinite trajectories is not obvious, and is the content of
the Ionescu-Tulcea theorem. -/
noncomputable def ionescuTulceaKernel (p : â„•) : Kernel ((i : Iic p) â†’ X i) ((n : â„•) â†’ X n) :=
  { toFun := ionescuTulceaFun Îº p
    measurable' := measurable_ionescuTulceaFun Îº p }

theorem ionescuTulceaKernel_apply (p : â„•) (xâ‚€ : (i : Iic p) â†’ X i) :
    ionescuTulceaKernel Îº p xâ‚€ = ionescuTulceaFun Îº p xâ‚€ := rfl

instance (p : â„•) : IsMarkovKernel (ionescuTulceaKernel Îº p) :=
  âŸ¨fun _ â†¦ isProbabilityMeasure_ionescuTulceaFun ..âŸ©

theorem ionescuTulceaKernel_proj (a b : â„•) :
    (ionescuTulceaKernel Îº a).map (frestrictLe b) = partialKernel Îº a b := by
  ext1 xâ‚€
  rw [Kernel.map_apply _ (measurable_frestrictLe _), ionescuTulceaKernel_apply, frestrictLe,
    isProjectiveLimit_ionescuTulceaFun, inducedFamily_Iic]

theorem eq_ionescuTulceaKernel' {a : â„•} (n : â„•) (Î· : Kernel ((i : Iic a) â†’ X i) ((n : â„•) â†’ X n))
    (hÎ· : âˆ€ b â‰¥ n, Kernel.map Î· (frestrictLe b) = partialKernel Îº a b) :
    Î· = ionescuTulceaKernel Îº a := by
  ext1 xâ‚€
  refine ((isProjectiveLimit_ionescuTulceaFun _ _ _).unique ?_).symm
  rw [isProjectiveLimit_nat_iff' _ _ _ n]
  Â· intro k hk
    rw [inducedFamily_Iic, â† Kernel.map_apply _ (measurable_frestrictLe k), hÎ· k hk]
  Â· exact (isProjectiveMeasureFamily_inducedFamily _ (partialKernel_proj_apply Îº xâ‚€))

theorem eq_ionescuTulceaKernel {a : â„•} (Î· : Kernel ((i : Iic a) â†’ X i) ((n : â„•) â†’ X n))
    (hÎ· : âˆ€ b, Kernel.map Î· (frestrictLe b) = partialKernel Îº a b) :
    Î· = ionescuTulceaKernel Îº a := eq_ionescuTulceaKernel' Îº 0 Î· fun b _ â†¦ hÎ· b

theorem partialKernel_comp_ionescuTulceaKernel {a b : â„•} (hab : a â‰¤ b) :
    (ionescuTulceaKernel Îº b) âˆ˜â‚– (partialKernel Îº a b) = ionescuTulceaKernel Îº a := by
  refine eq_ionescuTulceaKernel _ _ fun n â†¦ ?_
  ext xâ‚€ s ms
  rw [Kernel.map_apply' _ (measurable_frestrictLe _) _ ms,
    Kernel.comp_apply' _ _ _ (measurable_frestrictLe n ms)]
  simp_rw [â† Measure.map_apply (measurable_frestrictLe n) ms,
    â† Kernel.map_apply (ionescuTulceaKernel Îº b) (measurable_frestrictLe n),
    ionescuTulceaKernel_proj Îº b n]
  rw [â† Kernel.comp_apply' _ _ _ ms, partialKernel_comp _ n hab]

theorem ionescuTulceaKernel_proj_le {a b : â„•} (hab : a â‰¤ b) :
    Kernel.map (ionescuTulceaKernel Îº b) (frestrictLe (Ï€ := X) a) =
    Kernel.deterministic (frestrictLeâ‚‚ hab) (measurable_frestrictLeâ‚‚ _) := by
  rw [ionescuTulceaKernel_proj, partialKernel, dif_neg (not_lt.2 hab)]

end definition

variable {X : â„• â†’ Type*} [âˆ€ n, MeasurableSpace (X n)]
variable (Îº : (k : â„•) â†’ Kernel ((i : Iic k) â†’ X i) (X (k + 1)))
variable [âˆ€ k, IsMarkovKernel (Îº k)]

variable {E : Type*} [NormedAddCommGroup E]

/-- The canonical filtration on dependent functions indexed by `â„•`, where `ğ“• n` consists of
measurable sets depending only on coordinates `â‰¤ n`. -/
def â„± : @Filtration ((n : â„•) â†’ X n) â„• _ inferInstance where
  seq n := (inferInstance : MeasurableSpace ((i : Iic n) â†’ X i)).comap (frestrictLe n)
  mono' i j hij := by
    simp only
    rw [â† frestrictLeâ‚‚_comp_frestrictLe hij, â† comap_comp]
    exact MeasurableSpace.comap_mono (measurable_frestrictLeâ‚‚ _).comap_le
  le' n := (measurable_frestrictLe n).comap_le

/-- If a function is strongly measurable with respect to the Ïƒ-algebra generated by the
first coordinates, then it only depends on those first coordinates. -/
theorem stronglyMeasurable_dependsOn {n : â„•} {f : ((n : â„•) â†’ X n) â†’ E}
    (mf : @StronglyMeasurable _ _ _ (â„± n) f) : DependsOn f (Set.Iic n) :=
  fun _ _ h â†¦ eq_of_stronglyMeasurable_comap _ mf (dependsOn_frestrictLe n h)

variable [Nonempty (X 0)]

/-- This theorem shows that `ionescuTulceaKernel Îº n` is, up to an equivalence, the product of
a determinstic kernel with another kernel. This is an intermediate result to compute integrals
with respect to this kernel. -/
theorem ionescuTulceaKernel_eq (n : â„•) :
    ionescuTulceaKernel Îº n =
    Kernel.map
      (deterministic (@id ((i : Iic n) â†’ X i)) measurable_id Ã—â‚–
        Kernel.map (ionescuTulceaKernel Îº n) (Set.Ioi n).restrict)
      (el' n) := by
  refine (eq_ionescuTulceaKernel' _ (n + 1) _ fun a ha â†¦ ?_).symm
  ext x s ms
  rw [Kernel.map_map, Kernel.map_apply' _ _ _ ms, Kernel.deterministic_prod_apply',
    Kernel.map_apply']
  Â· have : (frestrictLe a) âˆ˜ (el' n) âˆ˜ (Prod.mk x) âˆ˜
        (Set.Ioi n).restrict =
        (fun y (i : Iic a) â†¦ if hi : i.1 â‰¤ n then x âŸ¨i.1, mem_Iic.2 hiâŸ© else y i) âˆ˜
        (frestrictLe a) := by
      ext x i
      by_cases hi : i.1 â‰¤ n <;> simp [hi, el']
    have aux t : {c : (i : Set.Ioi n) â†’ X i | (id x, c) âˆˆ t} = Prod.mk x â»Â¹' t := rfl
    have hyp : Measurable
        (fun (y : (i : Iic a) â†’ X i) (i : Iic a) â†¦
          if hi : i.1 â‰¤ n then x âŸ¨i.1, mem_Iic.2 hiâŸ© else y i) := by
      refine measurable_pi_lambda _ (fun i â†¦ ?_)
      by_cases hi : i.1 â‰¤ n <;> simp [hi]
      exact measurable_pi_apply _
    rw [aux, â† Set.preimage_comp, â† Set.preimage_comp, comp.assoc, this,
      â† Kernel.map_apply' _ _ _ ms, â† Kernel.map_map _ (measurable_frestrictLe a) hyp,
      ionescuTulceaKernel_proj, Kernel.map_apply' _ _ _ ms, partialKernel_lt Îº (by omega),
      Kernel.map_apply' _ _ _ (hyp ms), Kernel.deterministic_prod_apply',
      Kernel.map_apply' _ _ _ ms, Kernel.deterministic_prod_apply']
    Â· congr with y
      simp only [id_eq, el, Nat.succ_eq_add_one, MeasurableEquiv.coe_mk, Equiv.coe_fn_mk,
        Set.mem_preimage, Set.mem_setOf_eq]
      congrm (fun i â†¦ ?_) âˆˆ s
      by_cases hi : i.1 â‰¤ n <;> simp [hi]
    Â· exact (el ..).measurable ms
    Â· exact (el ..).measurable
    Â· exact (el ..).measurable <| hyp ms
    Â· exact (el ..).measurable
    Â· exact hyp
    Â· exact hyp.comp (measurable_frestrictLe _)
  Â· exact Set.measurable_restrict _
  Â· exact measurable_prod_mk_left <| (el' n).measurable <| (measurable_frestrictLe a) ms
  Â· exact (el' n).measurable <| (measurable_frestrictLe a) ms
  Â· exact (measurable_frestrictLe _).comp (el' n).measurable
  Â· exact (el' n).measurable
  Â· exact measurable_frestrictLe _

theorem measurable_updateFinset' {Î¹ : Type*} [DecidableEq Î¹] {I : Finset Î¹}
    {X : Î¹ â†’ Type*} [âˆ€ i, MeasurableSpace (X i)]
    {y : (i : I) â†’ X i} : Measurable (fun x â†¦ updateFinset x I y) := by
  refine measurable_pi_lambda _ (fun i â†¦ ?_)
  by_cases hi : i âˆˆ I <;> simp only [updateFinset, hi, â†“reduceDIte, measurable_const]
  exact measurable_pi_apply _

theorem ionescuTulceaKernel_eq_map_updateFinset {n : â„•} (xâ‚€ : (i : Iic n) â†’ X i) :
    ionescuTulceaKernel Îº n xâ‚€ =
      (ionescuTulceaKernel Îº n xâ‚€).map (fun y â†¦ updateFinset y _ xâ‚€) := by
  ext s ms
  nth_rw 1 [ionescuTulceaKernel_eq]
  rw [â† aux, Kernel.map_apply' _ _ _ ms, â† Measure.map_map, Measure.map_apply _ ms,
    Kernel.deterministic_prod_apply', â† Measure.map_map, Measure.map_apply, Kernel.map_apply]
  Â· rfl
  Â· exact Set.measurable_restrict _
  Â· exact measurable_prod_mk_left
  Â· exact (el' n).measurable ms
  Â· exact measurable_prod_mk_left
  Â· exact measurable_proj _
  Â· exact (el' n).measurable ms
  Â· exact (el' n).measurable
  Â· exact (el' n).measurable
  Â· exact measurable_prod_mk_left.comp (measurable_proj _)
  Â· exact (el' n).measurable

theorem integrable_ionescuTulceaKernel {a b : â„•} (hab : a â‰¤ b) {f : ((n : â„•) â†’ X n) â†’ E}
    (xâ‚€ : (i : Iic a) â†’ X i)
    (i_f : Integrable f (ionescuTulceaKernel Îº a xâ‚€)) :
    âˆ€áµ x âˆ‚ionescuTulceaKernel Îº a xâ‚€, Integrable f (ionescuTulceaKernel Îº b (frestrictLe b x)) := by
  rw [â† partialKernel_comp_ionescuTulceaKernel _ hab, Kernel.integrable_comp_iff] at i_f
  Â· apply ae_of_ae_map (p := fun x â†¦ Integrable f (ionescuTulceaKernel Îº b x))
    Â· exact (measurable_frestrictLe b).aemeasurable
    Â· convert i_f.1
      rw [â† ionescuTulceaKernel_proj, Kernel.map_apply _ (measurable_frestrictLe _)]
  Â· exact i_f.aestronglyMeasurable

variable [NormedSpace â„ E]

theorem integral_ionescuTulceaKernel {n : â„•} (xâ‚€ : (i : Iic n) â†’ X i) {f : ((n : â„•) â†’ X n) â†’ E}
    (mf : AEStronglyMeasurable f (ionescuTulceaKernel Îº n xâ‚€)) :
    âˆ« x, f x âˆ‚ionescuTulceaKernel Îº n xâ‚€ =
      âˆ« x, f (updateFinset x _ xâ‚€) âˆ‚ionescuTulceaKernel Îº n xâ‚€ := by
  nth_rw 1 [ionescuTulceaKernel_eq_map_updateFinset, integral_map]
  Â· exact measurable_updateFinset'.aemeasurable
  Â· convert mf
    nth_rw 2 [ionescuTulceaKernel_eq_map_updateFinset]

theorem partialKernel_comp_ionescuTulceaKernel_apply {a b : â„•} (hab : a â‰¤ b)
    (f : ((i : Iic b) â†’ X i) â†’ ((n : â„•) â†’ X n) â†’ E)
    (hf : StronglyMeasurable f.uncurry)
    (xâ‚€ : (i : Iic a) â†’ X i)
    (i_f : Integrable (fun x â†¦ f (frestrictLe b x) x) (ionescuTulceaKernel Îº a xâ‚€)) :
    âˆ« x, âˆ« y, f x y âˆ‚ionescuTulceaKernel Îº b x âˆ‚partialKernel Îº a b xâ‚€ =
      âˆ« x, f (frestrictLe b x) x âˆ‚ionescuTulceaKernel Îº a xâ‚€ := by
  rw [â† partialKernel_comp_ionescuTulceaKernel Îº hab, Kernel.integral_comp]
  Â· congr with x
    rw [integral_ionescuTulceaKernel]
    Â· nth_rw 2 [integral_ionescuTulceaKernel]
      Â· congrm âˆ« y, f (fun i â†¦ ?_) _ âˆ‚_
        simp [updateFinset, i.2]
      Â· exact hf.aestronglyMeasurable.comp_measurable
          ((measurable_frestrictLe b).prod_mk measurable_id)
    Â· exact hf.of_uncurry_left.aestronglyMeasurable
  Â· convert i_f
    rw [partialKernel_comp_ionescuTulceaKernel _ hab]

variable [CompleteSpace E]

theorem condexp_ionescuTulceaKernel
    {a b : â„•} (hab : a â‰¤ b) (xâ‚€ : (i : Iic a) â†’ X i) {f : ((n : â„•) â†’ X n) â†’ E}
    (i_f : Integrable f (ionescuTulceaKernel Îº a xâ‚€)) (mf : StronglyMeasurable f) :
    (ionescuTulceaKernel Îº a xâ‚€)[f|â„± b] =áµ[ionescuTulceaKernel Îº a xâ‚€]
      fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b (frestrictLe b x) := by
  refine (ae_eq_condexp_of_forall_setIntegral_eq _ i_f ?_ ?_ ?_).symm
  Â· rintro s - -
    apply Integrable.integrableOn
    conv => enter [1]; change (fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b x) âˆ˜ (frestrictLe b)
    rw [â† partialKernel_comp_ionescuTulceaKernel Îº hab, Kernel.integrable_comp_iff] at i_f
    Â· rw [â† integrable_map_measure, â† Kernel.map_apply, ionescuTulceaKernel_proj,
        â† integrable_norm_iff]
      Â· apply i_f.2.mono'
        Â· apply AEStronglyMeasurable.norm
          exact (mf.comp_measurable measurable_snd).integral_kernel_prod_right'.aestronglyMeasurable
        Â· refine Eventually.of_forall fun x â†¦ ?_
          rw [norm_norm]
          exact norm_integral_le_integral_norm _
      Â· exact (mf.comp_measurable measurable_snd).integral_kernel_prod_right'.aestronglyMeasurable
      Â· exact measurable_frestrictLe _
      Â· exact (mf.comp_measurable measurable_snd).integral_kernel_prod_right'.aestronglyMeasurable
      Â· exact (measurable_frestrictLe b).aemeasurable
    Â· exact mf.aestronglyMeasurable
  Â· rintro - âŸ¨t, mt, rflâŸ© -
    rw [â† integral_indicator]
    Â· have this x : ((frestrictLe b) â»Â¹' t).indicator
          (fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b (frestrictLe b x)) x =
          t.indicator (fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b x) ((frestrictLe b) x) :=
        Set.indicator_comp_right (frestrictLe b) (g := fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b x)
      simp_rw [this]
      rw [â† integral_map, â† Kernel.map_apply, ionescuTulceaKernel_proj Îº]
      simp_rw [Set.indicator_one_smul_apply (M := â„)
        (fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b x), â† integral_smul]
      Â· rw [partialKernel_comp_ionescuTulceaKernel_apply _ hab, â† integral_indicator]
        Â· congr with x
          by_cases h : frestrictLe b x âˆˆ t <;> simp [h]
        Â· exact measurable_frestrictLe b mt
        Â· rw [uncurry_def]
          apply StronglyMeasurable.smul
          Â· exact (stronglyMeasurable_const.indicator mt).comp_measurable measurable_fst
          Â· exact mf.comp_measurable measurable_snd
        Â· simp_rw [â† Set.indicator_comp_right]
          change Integrable (fun _ â†¦ (Set.indicator _ (fun _ â†¦ 1) _) â€¢ _) _
          simp_rw [â† Set.indicator_one_smul_apply]
          exact i_f.indicator (measurable_frestrictLe b mt)
      Â· exact measurable_frestrictLe _
      Â· exact (measurable_frestrictLe b).aemeasurable
      Â· refine (StronglyMeasurable.indicator ?_ mt).aestronglyMeasurable
        exact (mf.comp_measurable measurable_snd).integral_kernel_prod_right'
    Â· exact measurable_frestrictLe b mt
  Â· conv => enter [2]; change (fun x â†¦ âˆ« y, f y âˆ‚ionescuTulceaKernel Îº b x) âˆ˜ (frestrictLe b)
    apply AEStronglyMeasurable.comp_ae_measurable'
    Â· exact (mf.comp_measurable measurable_snd).integral_kernel_prod_right'.aestronglyMeasurable
    Â· exact (measurable_frestrictLe b).aemeasurable

theorem condexp_ionescuTulceaKernel' {a b c : â„•} (hab : a â‰¤ b) (hbc : b â‰¤ c)
    (xâ‚€ : (i : Iic a) â†’ X i) {f : ((n : â„•) â†’ X n) â†’ E} :
    (ionescuTulceaKernel Îº a xâ‚€)[f|â„± b] =áµ[ionescuTulceaKernel Îº a xâ‚€]
      fun x â†¦ âˆ« y, ((ionescuTulceaKernel Îº a xâ‚€)[f|â„± c]) (updateFinset x _ y)
        âˆ‚partialKernel Îº b c (frestrictLe b x) := by
  have i_cf : Integrable ((ionescuTulceaKernel Îº a xâ‚€)[f|â„± c])
      (ionescuTulceaKernel Îº a xâ‚€) := integrable_condexp
  have mcf : StronglyMeasurable ((ionescuTulceaKernel Îº a xâ‚€)[f|â„± c]) :=
    stronglyMeasurable_condexp.mono (â„±.le c)
  filter_upwards [â„±.condexp_condexp f hbc, condexp_ionescuTulceaKernel Îº hab xâ‚€ i_cf mcf]
  intro x h1 h2
  rw [â† h1, h2, â† ionescuTulceaKernel_proj, Kernel.map_apply, integral_map]
  Â· congr with y
    apply stronglyMeasurable_dependsOn stronglyMeasurable_condexp
    simp [updateFinset]
    exact fun i hi â†¦ (if_pos hi).symm
  Â· exact (measurable_frestrictLe c).aemeasurable
  Â· exact (mcf.comp_measurable measurable_updateFinset).aestronglyMeasurable
  Â· exact measurable_frestrictLe _
