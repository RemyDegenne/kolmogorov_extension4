/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Peter Pfaffelhuber
-/
import KolmogorovExtension4.AuxLemmas
import Mathlib.MeasureTheory.Measure.Regular
import Mathlib.Topology.MetricSpace.Polish

open Set MeasureTheory

open scoped ENNReal Topology NNReal

section Misc

variable {Î± : Type*}

namespace Set

-- actually not used anymore
theorem monotone_iUnion {s : â„• â†’ Set Î±} (hs : Monotone s) (n : â„•) : (â‹ƒ m â‰¤ n, s m) = s n := by
  apply subset_antisymm
  Â· exact iUnion_subset fun m â†¦ iUnion_subset fun hm â†¦ hs hm
  Â· exact subset_iUnion_of_subset n (subset_iUnion_of_subset le_rfl subset_rfl)

-- actually not used anymore
theorem antitone_iInter {s : â„• â†’ Set Î±} (hs : Antitone s) (n : â„•) : (â‹‚ m â‰¤ n, s m) = s n := by
  apply subset_antisymm
  Â· exact iInter_subset_of_subset n (iInter_subset _ le_rfl)
  Â· exact subset_iInter fun i â†¦ subset_iInter fun hin â†¦ hs hin

theorem eq_iInter_iInter {s : â„• â†’ Set Î±} : (â‹‚ n, s n) = â‹‚ (n : â„•) (m : â„•) (_ : m â‰¤ n), s m := by
  ext x; simp only [Set.mem_iInter]; exact âŸ¨fun h _ k _ â†¦ h k, fun h i â†¦ h i i le_rflâŸ©

end Set

namespace ENNReal

theorem tendsto_atTop_zero_iff_of_antitone (f : â„• â†’ â„â‰¥0âˆ) (hf : Antitone f) :
    Filter.Tendsto f Filter.atTop (ğ“ 0) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n : â„•, f n â‰¤ Îµ := by
  rw [ENNReal.tendsto_atTop_zero]
  refine âŸ¨fun h â†¦ fun Îµ hÎµ â†¦ ?_, fun h â†¦ fun Îµ hÎµ â†¦ ?_âŸ©
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, hn n le_rflâŸ©
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, fun m hm â†¦ (hf hm).trans hnâŸ©

theorem tendsto_atTop_of_antitone (f : â„• â†’ â„â‰¥0âˆ) (hf : Antitone f) :
    Filter.Tendsto f Filter.atTop (ğ“ 0) â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ n : â„•, f n < Îµ := by
  rw [ENNReal.tendsto_atTop_zero_iff_of_antitone f hf]
  constructor <;> intro h Îµ hÎµ
  have hÎµ' : (min 1 (Îµ / 2)) > 0 := by
    simp only [ge_iff_le, gt_iff_lt, lt_min_iff, zero_lt_one, div_pos_iff, ne_eq, and_true,
      true_and]
    simp only [two_ne_top, not_false_eq_true, and_true]
    intro g
    exact hÎµ.ne g.symm
  Â· obtain âŸ¨n, hnâŸ© := h (min 1 (Îµ / 2)) hÎµ'
    Â· refine âŸ¨n, hn.trans_lt ?_âŸ©
      by_cases hÎµ_top : Îµ = âˆ
      Â· rw [hÎµ_top]
        exact (min_le_left _ _).trans_lt ENNReal.one_lt_top
      refine (min_le_right _ _).trans_lt ?_
      rw [ENNReal.div_lt_iff (Or.inr hÎµ.ne') (Or.inr hÎµ_top)]
      conv_lhs => rw [â† mul_one Îµ]
      rw [ENNReal.mul_lt_mul_left hÎµ.ne' hÎµ_top]
      norm_num
  Â· obtain âŸ¨n, hnâŸ© := h Îµ hÎµ
    exact âŸ¨n, hn.leâŸ©

end ENNReal

end Misc

variable {Î± : Type*}

namespace MeasureTheory

variable [MeasurableSpace Î±]

/-- Some version of continuity of a measure in the emptyset using a decreasing sequence of sets. -/
theorem tendsto_zero_measure_of_antitone (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] {s : â„• â†’ Set Î±}
    (hs1 : âˆ€ n, MeasurableSet (s n)) (hs2 : Antitone s) (hs3 : (â‹‚ n, s n) = âˆ…) :
    Filter.Tendsto (fun n â†¦ Î¼ (s n)) Filter.atTop (ğ“ 0) := by
  convert MeasureTheory.tendsto_measure_iInter hs1 hs2 _
  Â· rw [hs3]
    exact measure_empty.symm
  Â· exact âŸ¨0, measure_ne_top Î¼ _âŸ©

theorem tendsto_zero_measure_of_antitone' (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] {s : â„• â†’ Set Î±}
    (hs1 : âˆ€ n, MeasurableSet (s n)) (hs2 : Antitone s) (hs3 : (â‹‚ n, s n) = âˆ…)
    (Îµ : â„â‰¥0âˆ) (hÎµ : 0 < Îµ) :
    âˆƒ n, Î¼ (s n) < Îµ :=
  (ENNReal.tendsto_atTop_of_antitone (fun n â†¦ Î¼ (s n))
    (fun _ _ h12 â†¦ measure_mono (hs2 h12))).mp (tendsto_zero_measure_of_antitone Î¼ hs1 hs2 hs3)
    Îµ hÎµ

/-- Some version of continuity of a measure in the emptyset using the intersection along a set of
sets. -/
theorem continuous_at_emptyset_inter (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] (S : â„• â†’ Set Î±)
    (hS2 : âˆ€ n, MeasurableSet (S n)) (hS3 : â‹‚ n, S n = âˆ…) {Îµ : â„â‰¥0âˆ} (hÎµ : 0 < Îµ) :
    âˆƒ m, Î¼ (â‹‚ n â‰¤ m, S n) < Îµ := by
  let s m := (Accumulate (fun n â†¦ (S n)á¶œ) m)á¶œ
  have hs_anti : Antitone s := by
    intro i j hij
    simp only [compl_le_compl_iff_le, le_eq_subset, s]
    exact monotone_accumulate hij
  have hs_iInter : â‹‚ n, s n = âˆ… := by
    simp only [s]
    rw [â† hS3, â† compl_iUnion, iUnion_accumulate, compl_iUnion]
    simp_rw [compl_compl]
  have hs_meas n : MeasurableSet (s n) := (MeasurableSet.accumulate (fun m â†¦ (hS2 m).compl) n).compl
  suffices âˆƒ m, Î¼ (s m) < Îµ by
    obtain âŸ¨m, hmâŸ© := this
    refine âŸ¨m, ?_âŸ©
    simpa [s, accumulate_def] using hm
  suffices Filter.Tendsto (fun m â†¦ Î¼ (s m)) Filter.atTop (ğ“ 0) by
    rw [ENNReal.tendsto_atTop_of_antitone] at this
    Â· exact this Îµ hÎµ
    Â· exact fun _ _ h â†¦ measure_mono (hs_anti h)
  convert tendsto_measure_iInter hs_meas hs_anti âŸ¨0, measure_ne_top Î¼ _âŸ©
  simp [hs_iInter]

end MeasureTheory

section Topology

namespace UniformSpace

lemma _root_.MeasurableSet.ball {_ : MeasurableSpace Î±} (x : Î±)
    {s : Set (Î± Ã— Î±)} (hs : MeasurableSet s) :
    MeasurableSet (UniformSpace.ball x s) := measurable_prod_mk_left hs

/-- Given a family of sets `s' n` and a family of entourages `V n` of the diagonal, the
intersection over `n` of the `V n`-neighborhood of `s' n`. Designed to be relatively compact
when the `s' n` are finite and `V n` tends to the diagonal. -/
def interUnionBalls (s' : â„• â†’ Set Î±) (V : â„• â†’ Set (Î± Ã— Î±)) : Set Î± :=
  â‹‚ n, â‹ƒ x âˆˆ s' n, UniformSpace.ball x (Prod.swap â»Â¹' V n)

theorem totallyBounded_interUnionBalls [UniformSpace Î±] {p : â„• â†’ Prop} {U : â„• â†’ Set (Î± Ã— Î±)}
    (H : (uniformity Î±).HasBasis p U) (s' : â„• â†’ Finset Î±) :
    TotallyBounded (interUnionBalls (fun n â†¦ â†‘(s' n)) U) := by
  rw [Filter.HasBasis.totallyBounded_iff H]
  intro i _
  let A := interUnionBalls (fun n â†¦ (s' n : Set Î±)) U
  have hA2 : A âŠ† â‹ƒ x âˆˆ s' i, UniformSpace.ball x (Prod.swap â»Â¹' U i) :=
    fun x hx â†¦ Set.mem_iInter.1 hx i
  refine âŸ¨s' i, Finset.finite_toSet (s' i), ?_âŸ©
  simp only [Finset.mem_coe]
  simp only [UniformSpace.ball] at hA2
  intro x hx
  let B x := Prod.mk x â»Â¹' (Prod.swap â»Â¹' U i)
  let C x := {y : Î± | (y, x) âˆˆ U i}
  have h : B = C := by ext x y; rfl
  change x âˆˆ â‹ƒ x âˆˆ s' i, C x
  rw [â† h]
  exact hA2 hx

/-- The construction `interUnionBalls` is used to have a relatively compact set. -/
theorem isCompact_closure_interUnionBalls [UniformSpace Î±] {p : â„• â†’ Prop} {U : â„• â†’ Set (Î± Ã— Î±)}
    (H : (uniformity Î±).HasBasis p U) [CompleteSpace Î±] (s' : â„• â†’ Finset Î±) :
    IsCompact (closure (interUnionBalls (fun n â†¦ (s' n : Set Î±)) U)) := by
  rw [isCompact_iff_totallyBounded_isComplete]
  refine âŸ¨TotallyBounded.closure ?_, isClosed_closure.isCompleteâŸ©
  exact totallyBounded_interUnionBalls H s'

theorem _root_.MeasureTheory.measure_compl_interUnionBalls_le {_ : MeasurableSpace Î±}
    (Î¼ : Measure Î±) (s' : â„• â†’ Set Î±) (V : â„• â†’ Set (Î± Ã— Î±)) :
    Î¼ (UniformSpace.interUnionBalls s' V)á¶œ â‰¤
      âˆ‘' n, Î¼ (â‹ƒ x âˆˆ s' n, UniformSpace.ball x (Prod.swap â»Â¹' V n))á¶œ := by
  rw [UniformSpace.interUnionBalls, Set.compl_iInter]
  exact measure_iUnion_le _

theorem _root_.MeasureTheory.measure_compl_interUnionBalls_lt {_ : MeasurableSpace Î±} (Îµ : â„â‰¥0âˆ)
    (Î¼ : Measure Î±) (s' : â„• â†’ Set Î±)
    (V : â„• â†’ Set (Î± Ã— Î±)) (Î´ : â„• â†’ â„â‰¥0âˆ)
    (hÎ´1 : âˆ€ n, Î¼ (â‹ƒ x âˆˆ s' n, UniformSpace.ball x (Prod.swap â»Â¹' V n))á¶œ â‰¤ Î´ n)
    (hÎ´3 : âˆ‘' n, Î´ n < Îµ) :
    Î¼ (UniformSpace.interUnionBalls s' V)á¶œ < Îµ :=
  ((measure_compl_interUnionBalls_le Î¼ s' V).trans
    (ENNReal.tsum_le_tsum fun n â†¦ hÎ´1 n)).trans_lt hÎ´3

end UniformSpace

end Topology

namespace MeasureTheory

variable [MeasurableSpace Î±] {Î¼ : Measure Î±}

theorem innerRegularWRT_isCompact_closure_iff [TopologicalSpace Î±] [R1Space Î±] :
    Î¼.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed â†” Î¼.InnerRegularWRT IsCompact IsClosed := by
  constructor <;> intro h A hA r hr
  Â· rcases h hA r hr with âŸ¨K, âŸ¨hK1, hK2, hK3âŸ©âŸ©
    exact âŸ¨closure K, closure_minimal hK1 hA, hK2, hK3.trans_le (measure_mono subset_closure)âŸ©
  Â· rcases h hA r hr with âŸ¨K, âŸ¨hK1, hK2, hK3âŸ©âŸ©
    refine âŸ¨closure K, closure_minimal hK1 hA, ?_, ?_âŸ©
    Â· simpa only [closure_closure, Function.comp_apply] using hK2.closure
    Â· exact hK3.trans_le (measure_mono subset_closure)

lemma innerRegularWRT_isCompact_isClosed_iff_innerRegularWRT_isCompact_closure
    [TopologicalSpace Î±] [R1Space Î±] :
    Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsClosed
      â†” Î¼.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed := by
  constructor <;> intro h A hA r hr
  Â· obtain âŸ¨K, hK1, âŸ¨hK2, _âŸ©, hK4âŸ© := h hA r hr
    refine âŸ¨K, hK1, ?_, hK4âŸ©
    simp only [closure_closure, Function.comp_apply]
    exact hK2.closure
  Â· obtain âŸ¨K, hK1, hK2, hK3âŸ© := h hA r hr
    refine âŸ¨closure K, closure_minimal hK1 hA, ?_, ?_âŸ©
    Â· simpa only [isClosed_closure, and_true]
    Â· exact hK3.trans_le (measure_mono subset_closure)

lemma innerRegularWRT_isCompact_isClosed_iff [TopologicalSpace Î±] [R1Space Î±] :
    Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsClosed
      â†” Î¼.InnerRegularWRT IsCompact IsClosed :=
  innerRegularWRT_isCompact_isClosed_iff_innerRegularWRT_isCompact_closure.trans
    innerRegularWRT_isCompact_closure_iff

theorem innerRegularWRT_of_exists_compl_lt {p q : Set Î± â†’ Prop} (hpq : âˆ€ A B, p A â†’ q B â†’ p (A âˆ© B))
    (hÎ¼ : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ K, p K âˆ§ Î¼ Ká¶œ < Îµ) :
    Î¼.InnerRegularWRT p q := by
  intro A hA r hr
  obtain âŸ¨K, hK, hK_subset, h_ltâŸ© : âˆƒ K, p K âˆ§ K âŠ† A âˆ§ Î¼ (A \ K) < Î¼ A - r := by
    obtain âŸ¨K', hpK', hK'_ltâŸ© := hÎ¼ (Î¼ A - r) (tsub_pos_of_lt hr)
    refine âŸ¨K' âˆ© A, hpq K' A hpK' hA, inter_subset_right, ?_âŸ©
    Â· refine (measure_mono fun x â†¦ ?_).trans_lt hK'_lt
      simp only [diff_inter_self_eq_diff, mem_diff, mem_compl_iff, and_imp, imp_self, imp_true_iff]
  refine âŸ¨K, hK_subset, hK, ?_âŸ©
  have h_lt' : Î¼ A - Î¼ K < Î¼ A - r := le_measure_diff.trans_lt h_lt
  exact lt_of_tsub_lt_tsub_left h_lt'

theorem innerRegularWRT_isCompact_closure_of_univ [TopologicalSpace Î±]
    (hÎ¼ : âˆ€ Îµ, 0 < Îµ â†’ âˆƒ K, IsCompact (closure K) âˆ§ Î¼ (Ká¶œ) < Îµ) :
    Î¼.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed := by
  refine innerRegularWRT_of_exists_compl_lt (fun s t hs ht â†¦ ?_) hÎ¼
  have : IsCompact (closure s âˆ© t) := hs.inter_right ht
  refine this.of_isClosed_subset isClosed_closure ?_
  refine (closure_inter_subset_inter_closure _ _).trans_eq ?_
  rw [IsClosed.closure_eq ht]

theorem inner_regular_isCompact_is_closed_of_complete_countable' [UniformSpace Î±] [CompleteSpace Î±]
    [SecondCountableTopology Î±] [(uniformity Î±).IsCountablyGenerated]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] (Îµ : â„â‰¥0âˆ) (hÎµ : 0 < Îµ) :
    âˆƒ K, IsCompact (closure K) âˆ§ P Ká¶œ < Îµ := by
  cases isEmpty_or_nonempty Î±
  case inl =>
    refine âŸ¨âˆ…, by simp, ?_âŸ©
    rw [â† Set.univ_eq_empty_iff.mpr]
    Â· simpa only [compl_univ, measure_empty, ENNReal.coe_pos] using hÎµ
    Â· assumption
  case inr =>
    let seq := TopologicalSpace.denseSeq Î±
    have hseq_dense : DenseRange seq := TopologicalSpace.denseRange_denseSeq Î±
    obtain âŸ¨t : â„• â†’ Set (Î± Ã— Î±),
        hto : âˆ€ i, t i âˆˆ (uniformity Î±).sets âˆ§ IsOpen (t i) âˆ§ SymmetricRel (t i),
        h_basis : (uniformity Î±).HasAntitoneBasis tâŸ© :=
      (@uniformity_hasBasis_open_symmetric Î± _).exists_antitone_subbasis
    let f : â„• â†’ â„• â†’ Set Î± := fun n m â†¦ UniformSpace.ball (seq m) (t n)
    have h_univ n : (â‹ƒ m, f n m) = univ := hseq_dense.iUnion_uniformity_ball (hto n).1
    have h3 n (Îµ : â„â‰¥0âˆ) (hÎµ : 0 < Îµ) : âˆƒ m, P (â‹ƒ m' â‰¤ m, f n m')á¶œ < Îµ := by
      simp only [compl_iUnion]
      refine continuous_at_emptyset_inter P _ (fun m â†¦ ?_) ?_ hÎµ
      Â· exact ((IsOpen.measurableSet (hto n).2.1).ball _).compl
      Â· rw [â† compl_iUnion, h_univ, compl_univ]
    choose! s' s'bound using h3
    rcases ENNReal.exists_seq_pos_lt Îµ hÎµ with âŸ¨Î´, hÎ´1, hÎ´2âŸ©
    classical
    let u : â„• â†’ Finset Î± := fun n â†¦ Finset.image seq (Finset.range (s' n (Î´ n) + 1))
    let A := UniformSpace.interUnionBalls (fun n â†¦ (u n : Set Î±)) t
    refine âŸ¨A, UniformSpace.isCompact_closure_interUnionBalls h_basis.toHasBasis u, ?_âŸ©
    refine measure_compl_interUnionBalls_lt Îµ P (fun n â†¦ â†‘(u n)) t Î´ (fun n â†¦ ?_) hÎ´2
    have h'' n : Prod.swap â»Â¹' t n = t n := SymmetricRel.eq (hto n).2.2
    simp only [Finset.mem_coe, compl_iUnion, h'', Finset.mem_image, Finset.mem_range, iInter_exists,
      biInter_and', iInter_iInter_eq_right, ge_iff_le, u, Nat.lt_succ_iff]
    simp only [compl_iUnion] at s'bound
    exact (s'bound n (Î´ n) (hÎ´1 n)).le

theorem innerRegularWRT_isCompact_closure_of_complete_countable [UniformSpace Î±] [CompleteSpace Î±]
    [SecondCountableTopology Î±] [(uniformity Î±).IsCountablyGenerated]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (IsCompact âˆ˜ closure) IsClosed :=
  innerRegularWRT_isCompact_closure_of_univ
    (inner_regular_isCompact_is_closed_of_complete_countable' P)

theorem innerRegularWRT_isCompact_isClosed_of_complete_countable [UniformSpace Î±] [CompleteSpace Î±]
    [SecondCountableTopology Î±] [(uniformity Î±).IsCountablyGenerated]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsClosed := by
  rw [innerRegularWRT_isCompact_isClosed_iff_innerRegularWRT_isCompact_closure]
  exact innerRegularWRT_isCompact_closure_of_univ
    (inner_regular_isCompact_is_closed_of_complete_countable' P)

theorem innerRegularWRT_isCompact_of_complete_countable [UniformSpace Î±] [CompleteSpace Î±]
    [SecondCountableTopology Î±] [(uniformity Î±).IsCountablyGenerated]
    [OpensMeasurableSpace Î±] (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT IsCompact IsClosed := by
  rw [â† innerRegularWRT_isCompact_closure_iff]
  exact innerRegularWRT_isCompact_closure_of_complete_countable P

theorem innerRegularWRT_isCompact_isClosed_isOpen_of_complete_countable [PseudoEMetricSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±] [OpensMeasurableSpace Î±]
    (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) IsOpen :=
  (innerRegularWRT_isCompact_isClosed_of_complete_countable P).trans
    (Measure.InnerRegularWRT.of_pseudoMetrizableSpace P)

theorem innerRegularWRT_isCompact_isOpen_of_complete_countable [PseudoEMetricSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±] [OpensMeasurableSpace Î±]
    (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT IsCompact IsOpen :=
  (innerRegularWRT_isCompact_of_complete_countable P).trans
    (Measure.InnerRegularWRT.of_pseudoMetrizableSpace P)

lemma InnerRegularCompactLTTop_of_complete_countable [PseudoEMetricSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±] [BorelSpace Î±]
    (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularCompactLTTop := by
  refine âŸ¨Measure.InnerRegularWRT.measurableSet_of_isOpen ?_ ?_âŸ©
  Â· exact innerRegularWRT_isCompact_isOpen_of_complete_countable P
  Â· exact fun s t hs_compact ht_open â†¦ hs_compact.inter_right ht_open.isClosed_compl

theorem innerRegular_isCompact_isClosed_measurableSet_of_complete_countable [PseudoEMetricSpace Î±]
    [CompleteSpace Î±] [SecondCountableTopology Î±] [BorelSpace Î±]
    (P : Measure Î±) [IsFiniteMeasure P] :
    P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) MeasurableSet := by
  suffices P.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s)
      fun s â†¦ MeasurableSet s âˆ§ P s â‰  âˆ by
    convert this
    simp only [eq_iff_iff, iff_self_and]
    exact fun _ â†¦ measure_ne_top P _
  refine Measure.InnerRegularWRT.measurableSet_of_isOpen ?_ ?_
  Â· exact innerRegularWRT_isCompact_isClosed_isOpen_of_complete_countable P
  Â· rintro s t âŸ¨hs_compact, hs_closedâŸ© ht_open
    rw [diff_eq]
    exact âŸ¨hs_compact.inter_right ht_open.isClosed_compl,
      hs_closed.inter (isClosed_compl_iff.mpr ht_open)âŸ©

/-- On a Polish space, any finite measure is regular with respect to compact and closed sets. -/
theorem PolishSpace.innerRegular_isCompact_measurableSet [TopologicalSpace Î±] [PolishSpace Î±]
    [BorelSpace Î±] (Î¼ : Measure Î±) [IsFiniteMeasure Î¼] :
    Î¼.InnerRegularWRT (fun s â†¦ IsCompact s âˆ§ IsClosed s) MeasurableSet := by
  letI := upgradePolishSpace Î±
  exact innerRegular_isCompact_isClosed_measurableSet_of_complete_countable Î¼

end MeasureTheory
