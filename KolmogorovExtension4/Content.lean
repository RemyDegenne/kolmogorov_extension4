/-
Copyright (c) 2023 RÃ©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©my Degenne, Peter Pfaffelhuber
-/
import Mathlib.MeasureTheory.Measure.AddContent

open Set Finset Filter

open scoped ENNReal Topology

namespace MeasureTheory

variable {Î± : Type*} {C : Set (Set Î±)} {s t : Set Î±}

section Ring

/-- If an additive content is Ïƒ-additive on a set ring, then the content of a monotone sequence of
sets tends to the content of the union. -/
theorem tendsto_atTop_addContent_iUnion_of_addContent_iUnion_eq_tsum {m : AddContent C}
    (hC : IsSetRing C)
    (m_iUnion : âˆ€ (f : â„• â†’ Set Î±) (_ : âˆ€ i, f i âˆˆ C) (_ : (â‹ƒ i, f i) âˆˆ C)
        (_hf_disj : Pairwise (Function.onFun Disjoint f)), m (â‹ƒ i, f i) = âˆ‘' i, m (f i))
    (f : â„• â†’ Set Î±) (hf_mono : Monotone f) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : â‹ƒ i, f i âˆˆ C) :
    Tendsto (fun n â†¦ m (f n)) atTop (ğ“ (m (â‹ƒ i, f i))) := by
  classical
  let g := disjointed f
  have hg_Union : (â‹ƒ i, g i) = â‹ƒ i, f i := iUnion_disjointed
  simp_rw [â† hg_Union,
    m_iUnion g (hC.disjointed_mem hf) (by rwa [hg_Union]) (disjoint_disjointed f)]
  have h : âˆ€ n, m (f n) = âˆ‘ i âˆˆ range (n + 1), m (g i) := by
    intro n
    have h1 : f n = â‹ƒâ‚€ Finset.image g (range (n + 1)) := by
      rw [â† Monotone.partialSups_eq hf_mono, â† partialSups_disjointed, â†
        partialSups_eq_sUnion_image g]
    rw [h1, addContent_sUnion]
    Â· rw [sum_image_of_disjoint addContent_empty ((disjoint_disjointed f).pairwiseDisjoint _)]
    Â· intro s
      rw [mem_coe, Finset.mem_image]
      rintro âŸ¨i, _, rflâŸ©
      exact hC.disjointed_mem hf i
    Â· intro s hs t ht hst
      rw [mem_coe, Finset.mem_image] at hs ht
      obtain âŸ¨i, _, rflâŸ© := hs
      obtain âŸ¨j, _, rflâŸ© := ht
      have hij : i â‰  j := by intro h_eq; rw [h_eq] at hst; exact hst rfl
      exact disjoint_disjointed f hij
    Â· rw [â† h1]; exact hf n
  simp_rw [h]
  change Tendsto (fun n â†¦ (fun k â†¦ âˆ‘ i âˆˆ range k, m (g i)) (n + 1)) atTop (ğ“ (âˆ‘' i, m (g i)))
  rw [tendsto_add_atTop_iff_nat (f := (fun k â†¦ âˆ‘ i âˆˆ range k, m (g i))) 1]
  exact ENNReal.tendsto_nat_tsum _

/-- If an additive content is Ïƒ-additive on a set ring, then it is Ïƒ-subadditive. -/
theorem addContent_iUnion_le_of_addContent_iUnion_eq_tsum {m : AddContent C} (hC : IsSetRing C)
    (m_iUnion : âˆ€ (f : â„• â†’ Set Î±) (_ : âˆ€ i, f i âˆˆ C) (_ : (â‹ƒ i, f i) âˆˆ C)
      (_hf_disj : Pairwise (Function.onFun Disjoint f)), m (â‹ƒ i, f i) = âˆ‘' i, m (f i))
    (f : â„• â†’ Set Î±) (hf : âˆ€ i, f i âˆˆ C) (hf_Union : â‹ƒ i, f i âˆˆ C) :
    m (â‹ƒ i, f i) â‰¤ âˆ‘' i, m (f i) := by
  classical
  have h_tendsto : Tendsto (fun n â†¦ m (partialSups f n)) atTop (ğ“ (m (â‹ƒ i, f i))) := by
    rw [â† iSup_eq_iUnion, â† iSup_partialSups_eq]
    refine tendsto_atTop_addContent_iUnion_of_addContent_iUnion_eq_tsum hC m_iUnion (partialSups f)
      (partialSups_monotone f) (hC.partialSups_mem hf) ?_
    rwa [â† iSup_eq_iUnion, iSup_partialSups_eq]
  have h_tendsto' : Tendsto (fun n â†¦ âˆ‘ i âˆˆ range (n + 1), m (f i)) atTop (ğ“ (âˆ‘' i, m (f i))) := by
    rw [tendsto_add_atTop_iff_nat (f := (fun k â†¦ âˆ‘ i âˆˆ range k, m (f i))) 1]
    exact ENNReal.tendsto_nat_tsum _
  refine le_of_tendsto_of_tendsto' h_tendsto h_tendsto' fun n â†¦ ?_
  rw [partialSups_eq_sUnion_image]
  refine (addContent_le_sum_of_subset_sUnion hC.isSetSemiring
    (J := (Finset.range (n + 1)).image f) (fun s â†¦ ?_) ?_ subset_rfl).trans ?_
  Â· rw [mem_coe, Finset.mem_image]
    rintro âŸ¨i, _, rflâŸ©
    exact hf i
  Â· rw [â† partialSups_eq_sUnion_image]
    exact hC.partialSups_mem hf n
  Â· exact Finset.sum_image_le_of_nonneg fun _ _ â†¦ zero_le _

end Ring

end MeasureTheory
